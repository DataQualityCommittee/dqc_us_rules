/**

DQC Rules
Copyright (c) 2017 XBRL US Inc.
Taxonomy: US GAAP 2017

**/

rule-name-prefix DQC

assert US.0049.7483 satisfied
 
$must_be_present_concepts = set('CashAndCashEquivalentsPeriodIncreaseDecrease','CashPeriodIncreaseDecrease','CashAndCashEquivalentsPeriodIncreaseDecreaseExcludingExchangeRateEffect','CashCashEquivalentsRestrictedCashandRestrictedCashEquivalentsPeriodIncreaseDecreaseIncludingExchangeRateEffect','CashCashEquivalentsRestrictedCashandRestrictedCashEquivalentsPeriodIncreaseDecreaseExcludingExchangeRateEffect')

$nonallowed_root_elements = filter (navigate summation-item descendants from CashAndCashEquivalentsPeriodIncreaseDecreaseExcludingExchangeRateEffect taxonomy $us-gaap returns set (target-name)) returns $item.local-name

$networkPresRole = filter taxonomy().networks(parent-child) where ($item.concept-names.contains(StatementOfCashFlowsAbstract) or $item.role.uri.lower-case.contains('cashflow')) and  $item.role.description.contains('- Statement ') and not $item.role.uri.lower-case.contains('parenthetical') returns $item.role;

		
/* This uses  navigation. It finds the root relationships and returns the networks. Since it  returns a set, the dups will be eliminated. */

for ($calcNetwork in 
	filter taxonomy().networks(summation-item) where $item.role in $networkPresRole)
	$roots = set(
		for $root in $calcNetwork.roots 
			if ($root.name.namespace-uri != $extension_ns and $root.name != NoncashOrPartNoncashAcquisitionNetNonmonetaryAssetsAcquiredLiabilitiesAssumed1)
				$root.name.local-name
			else
				none)
				
	$root_string	= $roots.join(', ');

	($roots intersect $must_be_present_concepts).length > 0 and
	($roots intersect $nonallowed_root_elements).length > 0 


		
message
"The following elements {$root_string} are parent (root) elements defined in the calculation relationship for the cash flow statement using the group {$calcNetwork.role.uri}. The cash flow statement should only have one calculation parent for durational concepts representing the increase or decrease in cash during the period. If the company has adopted ASU-2016-18 then the root element used to represent the aggregate change in cash should be the element CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseIncludingExchangeRateEffect.  If the company specifically excludes the exchange rate effect from the total then the element CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseExcludingExchangeRateEffect should be used.

Rule Element Id:7483
Rule Version: 5.0"

element_id 7483

severity error

