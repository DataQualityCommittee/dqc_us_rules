/**
DQC Rules
Copyright (c) 2017 - 2018 XBRL US Inc.
Taxonomy: US GAAP 2017
**/


rule-name-prefix DQC

assert US.0015 satisfied

$fact =
	{@concept in $non_neg_items.keys @unit=* where $fact < 0
		and not match_set_substring($fact, $exclude_non_neg_string_members)
		and (to-set($fact.dimensions.values.name) intersect $exclude_non_neg_members).length == 0
		and ($fact.dimensions.keys.name intersect $exclude_non_neg_axis).length == 0
		and not_elim_adjust($fact)
		};
$rule_suffix_id = $non_neg_items[$fact.concept.name];

true

message
"{$fact.concept.label.text} has a value of {$fact} which is less than zero. This element should not have a negative value. The preparer should consider if the value is input correctly for this assertion and, after considering the appropriateness of the input, if incorrect, input the amount as a positive (i.e., absolute) value and provide a negated label.

The properties of this {$fact.concept} fact are:
Period :{$fact.period}
Dimensions : {$fact.dimensions.join(', ','=')}
Unit : {$fact.unit}
Rule version: 5.0.0"

rule-suffix
$rule_suffix_id

severity error





