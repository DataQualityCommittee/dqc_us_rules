assert US.0085.9361 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** Identify all presentation networks in the company extension **/

$all_networks = taxonomy().networks(parent-child);

/** Get those networks that are the financial Statements **/
$FS_Networks = filter $all_networks where $item.role.description.contains('- Statement ');

/** Get all concepts in FS Networks **/
$FS_Concepts = sum(set(for ($network in $FS_Networks)
						$network.concepts));

/** For each of these concepts identify if either of the elements  DeferredTaxAssetsLiabilitiesNet and DeferredTaxAssetsNet 
have been used.  Do this by comparing the set of target elemnts from the statement to the set of set(DeferredTaxAssetsLiabilitiesNet, DeferredTaxAssetsNet)**/

$incorrect_concepts = $FS_Concepts intersect set(DeferredTaxAssetsLiabilitiesNet, DeferredTaxAssetsNet);

/** Output an error for every item in the intersection of the two sets **/

for $incorrect_concept in $incorrect_concepts
	true
	
message
"The element {$incorrect_concept} appears in the face financial statements of the filing.  This element is footnote concept that should only be used in a fottnote dislosure.  To report the value of defered taxes on the balance sheet either the element DeferredIncomeTaxAssetsNet or DeferredIncomeTaxLiabilitiesNet should be used depending if it is a liability or an asset. If the value of {$incorrect_concept} is the same as DeferredIncomeTaxAssetsNet or DeferredIncomeTaxLiabilitiesNet, then one of these elements should be used on the balance sheet and not the footnote element {$incorrect_concept}.

Rule Element Id:9365
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$rule_id]