/**

DQC Rules
 (c) Copyright 2017 - 2022, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2021

**/

rule-name-prefix DQC

assert US.0133.9831 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];
$ComponentAmount = {nonils @concept in list(EquityMethodInvestmentOwnershipPercentage, EquityMethodInvestmentDescriptionOfPrincipalActivities)};

$Member = $ComponentAmount.dimension(srt:ScheduleOfEquityMethodInvestmentEquityMethodInvesteeNameAxis);

$Member == none

message
"The filer has reported a value for the element {$ComponentAmount.concept.name} with a value of {$ComponentAmount} without using the axis ScheduleOfEquityMethodInvestmentEquityMethodInvesteeNameAxis. This axis must be used when reporting a value for {$ComponentAmount.concept.name}. Please refer to the FASB implementation guide on Dimensional Modeling for Disclosures of Consolidated and Nonconsolidated Entities on the FASB website.

The properties of this {$ComponentAmount.concept.name} fact are:
Period :{$ComponentAmount.period}
Dimensions : {$ComponentAmount.dimensions.join(', ','=')}
Unit : {$ComponentAmount.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

rule-focus $ComponentAmount
status $status
effectiveDate $effective_dates[$rule_id]


assert US.0133.9832 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$FinancialDataEMI = navigate parent-child descendants from list(EquityMethodInvestmentSummarizedFinancialInformationAbstract) taxonomy $US-GAAP where $relationship.target.is-monetary == true returns set (target-name);

exists({nonils @concept in $FinancialDataEMI @srt:ScheduleOfEquityMethodInvestmentEquityMethodInvesteeNameAxis = *}#EquityMethodFact)


message
"The filer has reported a value for the element {$EquityMethodFact.concept.name} with a value of {$EquityMethodFact}  using the axis ScheduleOfEquityMethodInvestmentEquityMethodInvesteeNameAxis. This axis must be not be used when reporting a value for {$EquityMethodFact.concept.name}. The filer should use the axis EquityMethodInvestmentNonconsolidatedInvesteeAxis without the axis ScheduleOfEquityMethodInvestmentEquityMethodInvesteeNameAxis. Please refer to the FASB implementation guide on Dimensional Modeling for Disclosures of Consolidated and Nonconsolidated Entities on the FASB website.

The properties of this {$EquityMethodFact.concept.name} fact are:
Period :{$EquityMethodFact.period}
Dimensions : {$EquityMethodFact.dimensions.join(', ','=')}
Unit : {$EquityMethodFact.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


SEVERITY error

rule-focus $EquityMethodFact
status $status
effectiveDate $effective_dates[$rule_id]

assert US.0133.9833 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$NonFinancialDataEMI = navigate parent-child descendants from list(ScheduleOfEquityMethodInvestmentsLineItems)  stop when $relationship.target.name == EquityMethodInvestmentSummarizedFinancialInformationAbstract taxonomy $US-GAAP where $relationship.target.is-abstract == false returns set (target);

exists({nonils @concept in $NonFinancialDataEMI @EquityMethodInvestmentNonconsolidatedInvesteeAxis = *}#EquityMethodFact)


message
"The filer has reported a value for the element {$EquityMethodFact.concept.name} with a value of {$EquityMethodFact}  using the axis EquityMethodInvestmentNonconsolidatedInvesteeAxis. This axis must not be used when reporting a value for {$EquityMethodFact.concept.name}. The filer should use the axis ScheduleOfEquityMethodInvestmentEquityMethodInvesteeNameAxis. Please refer to the FASB implementation guide on Dimensional Modeling for Disclosures of Consolidated and Nonconsolidated Entities on the FASB website.

The properties of this {$EquityMethodFact.concept.name} fact are:
Period :{$EquityMethodFact.period}
Dimensions : {$EquityMethodFact.dimensions.join(', ','=')}
Unit : {$EquityMethodFact.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


SEVERITY error

rule-focus $EquityMethodFact
status $status
effectiveDate $effective_dates[$rule_id]
