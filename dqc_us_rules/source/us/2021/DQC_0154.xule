/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2022
**/

rule-name-prefix DQC

/** Ensure that tax reconciliation is updated for FDII elements added to the taxonomy.
**/

assert US.0154.10071  satisfied
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

$IncomeTaxElements = set(IncomeTaxReconciliationIncomeTaxExpenseBenefitAtFederalStatutoryIncomeTaxRate,
EffectiveIncomeTaxRateReconciliationAtFederalStatutoryIncomeTaxRate,
IncomeTaxExpenseBenefit,
EffectiveIncomeTaxRateContinuingOperations);

$incomeTaxNetworks = FILTER $PRES_NETWORKS where $item.role.description.contains('- Disclosure -') and (($item.concept-names).to-set intersect $IncomeTaxElements).length > 0

for $incomeTaxNetwork in $incomeTaxNetworks.sort
    $incomeTaxConcepts =  $incomeTaxNetwork.concepts;
    $incomeTaxRelationships = $incomeTaxNetwork.relationships
    $preferredLabels = (filter $incomeTaxRelationships returns $item.preferred-label).to-set
    for $x in (filter $incomeTaxConcepts where $item.is-numeric returns $item.name)
        for $labelRole in $preferredLabels
            $label = taxonomy().concept($x).label($labelRole).text
            if ($label.lower-case.contains('foreign') and $label.lower-case.contains('rate') and $label.lower-case.contains('differential')) and ($x not in list(IncomeTaxReconciliationForeignIncomeTaxRateDifferential, EffectiveIncomeTaxRateReconciliationForeignIncomeTaxRateDifferential)) and not ($label.lower-case.contains('fdii') or $label.lower-case.contains('other') or $label.lower-case.contains('gilti') or $label.lower-case.contains('beat') or $label.lower-case.contains('and') or $label.lower-case.contains(' net of'))
                true
            else
                false
message
"The filer has reported a value for Foreign Income Tax Rate Differentials that is not using the element defined in the US-GAAP taxonomy.  The FASB has elements for Foreign Income Tax Rate Differential percentage amounts and monetary amounts in the taxonomy.  These elements are:

1. EffectiveIncomeTaxRateReconciliationForeignIncomeTaxRateDifferential
2. IncomeTaxReconciliationForeignIncomeTaxRateDifferential

The  element {$x}  with the label \"{$label}\" has been used in the filing in the disclosure section \"{$incomeTaxNetwork.role.description}\" that should be updated to the appropriate  US-GAAP element listed above.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}
"
SEVERITY error
effectiveDate $effective_dates[$ruleId]