/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2021
Release Version 18
**/

RULE-NAME-PREFIX DQC

ASSERT US.0128.9594 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];
/**  This rule looks at facts on specific dimensions and makes sure that they are smaller then the value of the default.
** The rule identifies a list of dimensions that should not have negative values and make sure any of the dimensionalized values
** that are monetary are always less than the default value.
** In many filings companies use the default value not realizing they have used the same element on a dimension elswhere in the filing
** thus creating a conflict**/

$collective_dimensions = set(PropertyPlantAndEquipmentByTypeAxis, StatementClassOfStockAxis, LongtermDebtTypeAxis, srt:ProductOrServiceAxis, FiniteLivedIntangibleAssetsByMajorClassAxis, DebtInstrumentAxis)

for $dimension in $collective_dimensions
                $dimension_fact =[nonils @@concept in $MONETARY_NON_NEGS @$dimension = *]
                $default_value_of_dimension = [nonils @@concept in $MONETARY_NON_NEGS]
                $dimension_fact > $default_value_of_dimension and $dimension_fact.decimals ==  $default_value_of_dimension.decimals

message
"The concept {$dimension_fact.concept.name} with a value of {$dimension_fact} and dimensions of {$dimension_fact.dimensions.join(', ','=')} is greater than the default value for this concept of {$default_value_of_dimension}. Dimension values used with the dimension {$dimension} should aggregate to a value no greater than the default value of {$default_value_of_dimension}. Either update the default value or the dimension value.
Element : {$dimension_fact.concept.name}
Period : {$default_value_of_dimension.period} 
Dimensions : {$dimension_fact.dimensions.join(', ','=')}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


severity error

effectiveDate $effective_dates[$rule_id]

rule-focus $dimension_fact

