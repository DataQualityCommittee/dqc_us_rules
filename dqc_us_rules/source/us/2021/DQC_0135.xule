/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2021
**/


rule-name-prefix DQC

constant $EXT_ENUM_DICT = $EXT_ENUM.agg-to-dict(1) 

assert US.0135.9835 satisfied

$ruleId = (rule-name().split('.'))[rule-name().split('.').length];
$decimal_tolerance_factor = 20

/** Is this report a 10-K **/
if set('10-K','10-KT','10-K/A','20-F').contains([covered @concept.local-name ='DocumentType'])
	
	/** Identify all presentation networks in the company extension **/
	$fsNetworks = filter $PRES_NETWORKS where $item.role.description.contains('- Statement ');

	/** eliminate reports with no presentations such as an 8-K with no financial data**/
	if $fsNetworks.length == 0
			skip
	else

		/** Get all concepts in FS Networks **/
		$fsConcepts = sum(set(for ($network in $fsNetworks)
									$network.concepts));

		/** Get the QName of FS elements that are monetary **/
		$fsConceptsQname_pre_all = filter $fsConcepts where $item.is-monetary returns $item.name;

		/** Get calculation descendants of $fsConceptsQname as this indicates they have a reconciliation of the items in the financials**/

		$fsConceptsQname = navigate summation-item descendants include start from $fsConceptsQname_pre_all returns set (target-name)

		/** Get the list of items to check **/
		for $ext_pairs in $EXT_ENUM
			$FS_concept = $ext_pairs[1]
			$related_ext_enum = $ext_pairs[2]
			$NotDisclosedAsImmaterial = $related_ext_enum.local-name + 'NotDisclosedFlag'

			$FS_Concepts_With_OCI = set(FairValueAssetLiabilityRecurringBasisStillHeldUnrealizedGainLossOci, FairValueAssetRecurringBasisStillHeldUnrealizedGainLossOci, FairValueLiabilityRecurringBasisStillHeldUnrealizedGainLossOci, FairValueNetDerivativeAssetLiabilityRecurringBasisStillHeldUnrealizedGainLossOci, FairValueRecurringBasisUnobservableInputReconciliationAssetLiabilityGainLossOci);

			/** Identify if required element disclosure  is contained in the financial statements 
			** Exclude lease items already checked in other rules.
			**/
			if ($fsConceptsQname.contains($FS_concept)) or ($FS_concept in ($LEASE_ITEMS + $NON_REQUIRED_CAPTION_DISCLOSURE))
				 or ($FS_concept in $DEFINED_BENEFIT_COST_FS_LINE_ITEMS and  NetPeriodicDefinedBenefitsExpenseReversalOfExpenseExcludingServiceCostComponent in $fsConceptsQname)
				 or ($FS_concept in $FS_Concepts_With_OCI and ($OCI_SECURITY_RELATED_ITEMS.intersect($fsConceptsQname).length > 0))
				 or ($FS_concept in ($DERIVATIVE_ASSETS_FS_LINE_ITEMS + set(DerivativeAssets)) and ($fsConceptsQname intersect ($DERIVATIVE_ASSETS_FS_LINE_ITEMS + set(DerivativeAssets))).length > 0)
				 or ($FS_concept in ($DERIVATIVE_LIABILITIES_FS_LINE_ITEMS + set(DerivativeLiabilities)) and ($fsConceptsQname intersect ($DERIVATIVE_LIABILITIES_FS_LINE_ITEMS + set(DerivativeLiabilities))).length > 0)
				 or (list({covered @concept.local-name = $NotDisclosedAsImmaterial}).length > 0)
                    
				/** If the concept is in the FSor its equivalent is then stop the rule **/
				false
			else

				/** get set of ancestor calculation concepts and test if in financial statement items to exclude from rule as has linkage back to financials through a calculation**/

				$fsConceptsQnameAncestor = navigate summation-item ancestors from $FS_concept returns set (target-name)

				/** We do not get calc ancestors defined in the taxonomy as this would potentially return something in the FS.  We deal with the pension case of NetPeriodicDefinedBenefitsExpenseReversalOfExpenseExcludingServiceCostComponent above as this calc will not be defined in the company calc
				as it excludes service costs. **/
				
				if $fsConceptsQnameAncestor.intersect($fsConceptsQname).length > 0
					false
				else
					/** Get a list of values for this element, but exclude detailed breakdowns of disclosure for financial instruments **/
					/** But exclude some member items not to check **/
					$FairValueBreakdownItemsNotChecked = list(FairValueInputsLevel2Member, FairValueInputsLevel1Member, FairValueMeasuredAtNetAssetValuePerShareMember, FairValueInputsLevel12And3Member, srt:ScenarioForecastMember)

					$FS_Concept_Item = list({nonils covered @concept = $FS_concept @FairValueByAssetClassAxis != * @FairValueByLiabilityClassAxis != * @FinancialInstrumentAxis != * @RetirementPlanNameAxis != * @RetirementPlanTypeAxis != *  @RetirementPlanSponsorLocationAxis !=* @DerivativeInstrumentRiskAxis != * @DerivativeInstrumentsGainLossByHedgingRelationshipAxis != * @EnvironmentalRemediationSiteAxis != * @HedgingDesignationAxis != * where $fact != 0 or ($fact.dimensions-explicit.values.name not in $FairValueBreakdownItemsNotChecked)}).sort;
					
					if $FS_Concept_Item.length > 0

						/** Get ancestor items **/
						$ancestor_ext_enum = list(for $ancestor in $fsConceptsQnameAncestor
													for $ext_pairs in $EXT_ENUM
														if $ancestor == $ext_pairs[1]
															$ext_pairs[2]
														else
															skip
												)

						/** For pensions we allow the high level element to be used DefinedBenefitPlanNetPeriodicBenefitCostCreditExcludingServiceCostStatementOfIncomeOrComprehensiveIncomeExtensibleList if
						** any of the pension elements are used. So if interest cost is tagged it is ok to use the generic all element as companies often say all of the above items are included in other expenses.
						**/
						
						$related_pension_item_list = if $related_ext_enum in $DEFINED_BENEFIT_COST_EXT_ENUM
													list(DefinedBenefitPlanNetPeriodicBenefitCostCreditExcludingServiceCostStatementOfIncomeOrComprehensiveIncomeExtensibleList)
												else
													list()

						/** Government Assistance element GovernmentAssistanceAmount has ext enum of GovernmentAssistanceStatementOfIncomeOrComprehensiveIncomeExtensibleEnumeration which is used as a catch all for government assistance items of GovernmentAssistanceNonoperatingIncome with GovernmentAssistanceNonoperatingIncomeStatementOfIncomeOrComprehensiveIncomeExtensibleEnumeration and GovernmentAssistanceOperatingIncome with GovernmentAssistanceOperatingIncomeStatementOfIncomeOrComprehensiveIncomeExtensibleEnumeration **/

						/** Get the children of the FS concept to see if any of these are in the ext enum list. We do this because the total amount reported could be broken up and allocated to different FS accounts.  So a total without an extensible enum is OK if its children have an ext enum showing the accounts in the FS they are allocated to. **/

						$children = navigate summation-item descendants from $FS_concept returns set (target-name)
						$related_ext_enum_item_list	 =	list(for $child in $children
																if $EXT_ENUM_DICT[$child]== none
																	skip
																else
																	$EXT_ENUM_DICT[$child][1][2])

						/** This determines the allowable enum items that can be used for this concept **/
						$related_ext_enum_list = list($related_ext_enum) + $related_pension_item_list + $ancestor_ext_enum + $related_ext_enum_item_list

						/** Generate pension specific message **/
						$pension_message = if $related_pension_item_list.length > 0
												"In the case of pension elements the element DefinedBenefitPlanNetPeriodicBenefitCostCreditExcludingServiceCostStatementOfIncomeOrComprehensiveIncomeExtensibleList can be used as a catch all for those cases where a company indicates that all pension costs are included in a specific line item on the financial statements. "
											else
												""

						/** Determine if any the extensible list element has been used with a value 
						** Because we use covered we get all the facts with dimensions or not.  THis means no errors are reported if alignment is incorrect.
						** If any of location axis are used then no error is reported. The next rule checks that alignment is appropriate for location axis.
						** In some cases this may cause duplicates
						**/

						if list({nonils covered @concept in $related_ext_enum_list}).length > 0 
							false
						else
							$fs_fact_values_decimals = filter (list([nonils covered @concept.is-monetary = true])) returns $item.decimals;
							$fs_decimals = most_frequent($fs_fact_values_decimals)
							$tolerance = (10.power(-1 * $fs_decimals)) * $decimal_tolerance_factor;
							/** This gets the max value either neg or pos **/
							$FS_Concept_Item_abs_only = filter $FS_Concept_Item returns $item.abs;
							$FS_Concept_Item_abs = filter $FS_Concept_Item returns list($item.abs, $item);
							$FS_Concept_Item_abs_sort = $FS_Concept_Item_abs.sort("desc")
							$FS_Concept_Item_max_first = first($FS_Concept_Item_abs_sort)
							$FS_Concept_Item_max = $FS_Concept_Item_max_first[2]
							/** Test if amount is larger than tolerance **/
							if max($FS_Concept_Item_abs_only) > $tolerance
								true
							else 
								false
					else
						false
else
 	false
 
message

"The element {$FS_concept} has been used in the filing but does not appear in the financial statements of the filing. This disclosure should appear in the face financials unless it is included in another account caption. If this is the case the following element(s) {$related_ext_enum_list.to-set.join(",")} should be used to identify the account where the financial statement element {$FS_concept} is included. 
{$pension_message}

If the filing does not disclose the financial statement captions where the amounts appear, an extension element {$NotDisclosedAsImmaterial} can be used. If this element is used, the rule will not produce an error.

The following is an example of a fact that does not have an extensible list.
 
Concept : {$FS_concept}
Value : {$FS_Concept_Item_max}
Period : {$FS_Concept_Item_max.period}
Dimensions : {$FS_Concept_Item_max.dimensions.join(', ','=')}
Unit : {$FS_Concept_Item_max.unit}
Decimals: {$FS_Concept_Item_max.decimals}

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

SEVERITY error
effectiveDate $effective_dates[$ruleId]

rule-focus $FS_Concept_Item_max 

assert US.0135.9836 satisfied

/** THIS RULE ONLY CHECKS THOSE FACTS THAT HAVE USED THE BALANCE SHEET AND INCOME STATEMENT LOCATION AXIS **/
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

/** Is this report a 10-K **/
if set('10-K','10-KT','10-K/A','20-F').contains([covered @concept.local-name ='DocumentType'])
	
	/** Identify all presentation networks in the company extension **/

	$fsNetworks = filter $PRES_NETWORKS where $item.role.description.contains('- Statement ');

	/** eliminate reports with no presentations such as an 8-K with no financial data**/
	if $fsNetworks.length == 0
			skip
	else

		/** Get all concepts in FS Networks **/
		$fsConcepts = sum(set(for ($network in $fsNetworks)
									$network.concepts));

		/** Get the QName of FS elements that are monetary **/
		$fsConceptsQname = filter $fsConcepts where $item.is-monetary returns $item.name;

		/** Get the list of items to check **/
		for $ext_pairs in $EXT_ENUM
			$FS_concept = $ext_pairs[1]
			$related_ext_enum = $ext_pairs[2]
			$NotDisclosedAsImmaterial = $related_ext_enum.local-name + 'NotDisclosedFlag'

			$FS_Concepts_With_OCI = set(FairValueAssetLiabilityRecurringBasisStillHeldUnrealizedGainLossOci, FairValueAssetRecurringBasisStillHeldUnrealizedGainLossOci, FairValueLiabilityRecurringBasisStillHeldUnrealizedGainLossOci, FairValueNetDerivativeAssetLiabilityRecurringBasisStillHeldUnrealizedGainLossOci, FairValueRecurringBasisUnobservableInputReconciliationAssetLiabilityGainLossOci);

			/** Identify if required element disclosure  is contained in the financial statements 
			** Exclude lease items already checked in other rules.
			**/
			if ($fsConceptsQname.contains($FS_concept)) or ($FS_concept in ($LEASE_ITEMS + $NON_REQUIRED_CAPTION_DISCLOSURE))
				 or ($FS_concept in $DEFINED_BENEFIT_COST_FS_LINE_ITEMS and  NetPeriodicDefinedBenefitsExpenseReversalOfExpenseExcludingServiceCostComponent in $fsConceptsQname)
				 or ($FS_concept in $FS_Concepts_With_OCI and ($OCI_SECURITY_RELATED_ITEMS.intersect($fsConceptsQname).length > 0))
				 or ($FS_concept in ($DERIVATIVE_ASSETS_FS_LINE_ITEMS + set(DerivativeAssets)) and ($fsConceptsQname intersect ($DERIVATIVE_ASSETS_FS_LINE_ITEMS + set(DerivativeAssets))).length > 0)
				 or ($FS_concept in ($DERIVATIVE_LIABILITIES_FS_LINE_ITEMS + set(DerivativeLiabilities)) and ($fsConceptsQname intersect ($DERIVATIVE_LIABILITIES_FS_LINE_ITEMS + set(DerivativeLiabilities))).length > 0)
				 or (list({covered @concept.local-name = $NotDisclosedAsImmaterial}).length > 0)
				/** If the concept is in the FS then stop the rule **/
				false
			else
				/** Get a list of elements but exclude dimensional breakdowns of investments by classes.**/
				$FairValueBreakdownItemsNotChecked = list(FairValueInputsLevel2Member, FairValueInputsLevel1Member, FairValueMeasuredAtNetAssetValuePerShareMember, FairValueInputsLevel12And3Member)

				if taxonomy().concept($FS_concept).period-type == instant

					/** Exclude breakdowns of key elements where broken down in schedules.  This means the rule will only pick up totals if they are provided.
					* TO DO At some point we may want to update to check if there is a total and if not then check the detail.
					***/

					$FS_Concept_Item1 = {nonils @concept = $FS_concept @@BalanceSheetLocationAxis = * @unit = * @@BalanceSheetLocationAxis = * @@FairValueByAssetClassAxis != * @@FairValueByLiabilityClassAxis != * @@FinancialInstrumentAxis != * @@RetirementPlanNameAxis != * @@RetirementPlanTypeAxis != * @@RetirementPlanSponsorLocationAxis !=*  @@DerivativeInstrumentRiskAxis != * @@DerivativeInstrumentsGainLossByHedgingRelationshipAxis != * @@EnvironmentalRemediationSiteAxis != * @@HedgingDesignationAxis != * where $fact != 0 or ($fact.dimensions-explicit.values.name not in $FairValueBreakdownItemsNotChecked)};
				    
				
				/** Determine if any the extensible list element has been used with a value 
				** In some cases the context does not need the related extension because it is provided at a different dimensional level.
				** We need to check that either the BS Location or IS location axis has also been used with the item. If any of these axis are used then no error is reported
				**/
					if exists({nonils @concept = $related_ext_enum @@BalanceSheetLocationAxis = *})
						
							false
					else 
						
							true
				else
					$FS_Concept_Item1 = {nonils @concept = $FS_concept @@IncomeStatementLocationAxis = * @unit = *  @@FairValueByAssetClassAxis != * @@FairValueByLiabilityClassAxis != * @@FinancialInstrumentAxis != * @@RetirementPlanNameAxis != * @@RetirementPlanTypeAxis != * @@RetirementPlanSponsorLocationAxis !=* @@DerivativeInstrumentRiskAxis != * @@DerivativeInstrumentsGainLossByHedgingRelationshipAxis != * @@EnvironmentalRemediationSiteAxis != * @@HedgingDesignationAxis != * where $fact != 0 or ($fact.dimensions-explicit.values.name not in $FairValueBreakdownItemsNotChecked)};
					
				/** Determine if any the extensible list element has been used with a value 
				** In some cases the context does not need the related extension because it is provided at a different dimensional level.
				** We need to check that either the BS Location or IS location axis has also been used with the item. If any of these axis are used then no error is reported
				**/
					if exists({nonils @concept = $related_ext_enum @@IncomeStatementLocationAxis = *})
						
							false
					else 
						
							true
else
 	false
 
message

"The element {$FS_concept} with a value of {$FS_Concept_Item1} has been used in the filing but does not appear in the financial statements of the filing. This disclosure should appear in the face financials unless it is included in another account caption. If this is the case the element {$related_ext_enum} should be used to identify the account where the element {$FS_concept} is included.  Because this value uses a statement location axis the enumerated value should also include the appropriate statement location axis. If there is only one statement location axis value the location axis does not have to be used.

If the filing does not disclose the financial statement captions where the amounts appear, an extension element {$NotDisclosedAsImmaterial} can be used. If this element is used, the rule will not produce an error.

Concept : {$FS_concept}
Period :{$FS_Concept_Item1.period}
Dimensions : {$FS_Concept_Item1.dimensions.join(', ','=')}
Unit : {$FS_Concept_Item1.unit}
Decimals: {$FS_Concept_Item1.decimals}

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

SEVERITY error
effectiveDate $effective_dates[$ruleId]

rule-focus $FS_Concept_Item1 