/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP ALL

WARNING : THIS IS A SINGLE FILE.  CHANGES TO THIS FILE AFFECTS 5 files 2024, 2023, 2022, 2021, 2020
This was created using a hardlink ln {original} {new}

**/

RULE-NAME-PREFIX DQC

constant $DIVIDENDS_PER_SHARE_NOT_DISCLOSED = if {covered @concept.local-name = 'DividendsPerShareNotDisclosed'} == true
                                                        true
                                                else
                                                        false

ASSERT US.0180.10147 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

for $presNetwork in $STATEMENT_PRES_NETWORKS.sort
		
		$presConceptItems = $presNetwork.concept-names;

		// Income Statement Processing 
		if $presConceptItems.contains(IncomeStatementAbstract) and $presConceptItems.contains(DividendsPayableAmountPerShare)
            set({covered @DividendsPayableAmountPerShare}).length > 0
		else 
			skip


message
"The company has included the concept DividendsPayableAmountPerShare as a component of the Income Statement using the group {$presNetwork.role.description}.  This is a an instant item and does not reflect the dividends applicable for the reporting period.  Filers should use the element CommonStockDividendsPerShareDeclared with the appropriate reporting period instead of the concept DividendsPayableAmountPerShare. The rule will only report an error if a value has been reported for the element DividendsPayableAmountPerShare.

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$rule_id]


/** This rule looks for dividends expense and dividends payment and if they are different checks that dividends declared per share is reported.  If dividends paid per share is reported this should be changed. **/

ASSERT US.0180.10148 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$dividendsPayments = {nonils @concept = PaymentsOfDividendsCommonStock @unit = * }

$dividendsExpense = first-value({nonils @concept = DividendsCommonStock @unit = * },{nonils @concept = DividendsCommonStockCash @unit = * })

if $dividendsPayments != $dividendsExpense
    $divPaidElement  = {nonils @concept = CommonStockDividendsPerShareCashPaid @unit=*}
    $divPaidElementUsed = exists($divPaidElement)
    $messageIfDivPaidElementUsed = if $divPaidElementUsed
                                        "The filer has reported a value for CommonStockDividendsPerShareCashPaid of " + $divPaidElement  + ", but has not reported a value for CommonStockDividendsPerShareDeclared. If the cash paid amount and expense are different the value for  CommonStockDividendsPerShareDeclared should also be reported."
                                    else 
                                        ""
    (not exists({nonils @concept = CommonStockDividendsPerShareDeclared @unit=*}))  and (not $DIVIDENDS_PER_SHARE_NOT_DISCLOSED)
else
    skip


message
"The filing has reported a value for PaymentsOfDividendsCommonStock with a value of  {$dividendsPayments} and a value for  DividendsCommonStockCash with a value of {$dividendsExpense}. Given these values are not the same the value of CommonStockDividendsPerShareDeclared should be reported for the period.  If the values were the same the element CommonStockDividendsPerShareCashPaid could be used.  {$messageIfDivPaidElementUsed}

If CommonStockDividendsPerShareDeclared is not reported in the filing an extension boolean element should be defined called DividendsPerShareNotDisclosed with a value of true.

The properties of the PaymentsOfDividendsCommonStock fact are:
Period :{$dividendsPayments.period}
Dimensions : {$dividendsPayments.dimensions.join(', ','=')}
Unit : {$dividendsPayments.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $dividendsPayments

effectiveDate $effective_dates[$rule_id]

ASSERT US.0180.10149 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$dividendsPayments = {nonils @concept = PaymentsOfDividendsPreferredStockAndPreferenceStock @unit = * }

$dividendsExpense = first-value({nonils @concept = DividendsPreferredStock @unit = * },{nonils @concept = DividendsPreferredStockCash @unit = * }) 

if $dividendsPayments != $dividendsExpense
    $divPaidElement  = {nonils @concept = PreferredStockDividendsPerShareCashPaid @unit=*}
    $divPaidElementUsed = exists($divPaidElement)
    $messageIfDivPaidElementUsed = if $divPaidElementUsed
                                        "The filer has reported a value for PreferredStockDividendsPerShareDeclared of " + $divPaidElement  + ", but has not reported a value for PreferredStockDividendsPerShareDeclared. If the cash paid amount and expense are different the value for  PreferredStockDividendsPerShareDeclared should also be reported."
                                    else 
                                        ""
    (not exists({nonils @concept = PreferredStockDividendsPerShareDeclared @unit=*})) and (not $DIVIDENDS_PER_SHARE_NOT_DISCLOSED)
else
    skip


message
"The filing has reported a value for PaymentsOfDividendsPreferredStockAndPreferenceStock with a value of  {$dividendsPayments} and a value for  DividendsPreferredStockCash with a value of {$dividendsExpense}. Given these values are not the same the value of PreferredStockDividendsPerShareDeclared should be reported for the period.  If the values were the same the element PreferredStockDividendsPerShareCashPaid could be used.  {$messageIfDivPaidElementUsed}

If PreferredStockDividendsPerShareDeclared is not reported in the filing an extension boolean element should be defined called DividendsPerShareNotDisclosed with a value of true.

The properties of the PaymentsOfDividendsPreferredStockAndPreferenceStock fact are:
Period :{$dividendsPayments.period}
Dimensions : {$dividendsPayments.dimensions.join(', ','=')}
Unit : {$dividendsPayments.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $dividendsPayments

effectiveDate $effective_dates[$rule_id]

ASSERT US.0180.10154 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

for $presNetwork in $PARENTHETICAL_STATEMENT_PRES_NETWORKS.sort
		
		$presConceptItems = $presNetwork.concept-names;

		// Income Statement Processing 
		if $presConceptItems.contains(DividendsPayableAmountPerShare) 
            $factsReported = list({covered @DividendsPayableAmountPerShare})
            $factsReported.length > 0
		else 
			skip


message
"The company has included the concept DividendsPayableAmountPerShare as a parenthetical disclosure using the group {$presNetwork.role.description}.  This is a an instant item and does not reflect the dividends applicable for the reporting period.  Filers should use the element CommonStockDividendsPerShareDeclared with the appropriate reporting period instead of the concept DividendsPayableAmountPerShare. The rule will only report an error if a value has been reported for the element DividendsPayableAmountPerShare.

The value(s) reported are {$factsReported.join(', ')}

Period :{first($factsReported).period}
Unit : {first($factsReported).unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$rule_id]