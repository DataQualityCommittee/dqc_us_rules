/**
DQC Rules
 (c) Copyright 2017 - 2021, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2021
Release Version 15
**/

/** Concentration Risk **/

/** Address the situation where the concentration is in the default. In this case it should not really exist.  However in some cases comanies report a total of 100%.  This is OK as the value represents the sum of all exposures.  This means the element can only have 1 value in the default of 100%.  The rule flags any default value where the value is not 1.

SEE FASB GUIDE: https://xbrl.fasb.org/ix/?doc=../impdocs/CCR_TIG/inlinef2017icededcreditriskg.htm**/

RULE-NAME-PREFIX DQC


/** RULE 9565 Identifies where ConcentrationRiskPercentage1 is used in the default and does not have a value of 100% **/

ASSERT US.0109.9565 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

exists([nonils @concept=ConcentrationRiskPercentage1 where $fact != 1]#fact1)
message
	"{$fact1.concept.name} with value {$fact1} should always be used with the ConcentrationRiskByTypeAxis and ConcentrationRiskByBenchmarkAxis.  This element has no meaning without these axis at a minimum.  Any data provided needs to be used in combination with an additional axis to define the actual risk as the numerator. The member used on the ConcentrationRiskByBenchmarkAxis represents the benchmark denominator. The properties of this {$fact1.concept.name} fact are:

Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]
 
/** RULE 9566 Identifies where element ConcentrationRiskPercentage1 is used and ConcentrationRiskByBenchmarkAxis and ConcentrationRiskByTypeAxis is missing but another third axis is used.
** WIth any value other than 1 the concentration risk element must have a numerator of ConcentrationRiskByTypeAxis and  a denominator of ConcentrationRiskByBenchmarkAxis **/

ASSERT US.0109.9566 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

if exists([nonils @concept=ConcentrationRiskPercentage1])
    skip
else
    exists({nonils @concept=ConcentrationRiskPercentage1 @ConcentrationRiskByBenchmarkAxis = none @ConcentrationRiskByTypeAxis = none}#fact1)

message
	"{$fact1.concept.name} with value {$fact1} should always be used with the ConcentrationRiskByTypeAxis and ConcentrationRiskByBenchmarkAxis.  This element has no meaning without these axis at a minimum.  Any data provided needs to be used in combination with an additional axis to define the actual risk as the numerator. The member used on the ConcentrationRiskByBenchmarkAxis represents the benchmark denominator. The properties of this {$fact1.concept.name} fact are:

Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]

/** RULE 9567 Identifies where ConcentrationRiskByBenchmarkAxis is missing **/

ASSERT US.0109.9567 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

exists({nonils @concept=ConcentrationRiskPercentage1 @ConcentrationRiskByBenchmarkAxis = none @ConcentrationRiskByTypeAxis = *}#fact1)

message
	"{$fact1.concept.name} with value {$fact1} should always be used with both the ConcentrationRiskByTypeAxis and the ConcentrationRiskByBenchmarkAxis.  This element has no meaning without both these axis at a minimum.  This fact only contains the numerator of ConcentrationRiskByTypeAxis, and not the member used on the ConcentrationRiskByBenchmarkAxis which represents the benchmark denominator. The properties of this {$fact1.concept.name} fact are:

Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]

/** RULE 9568 Identifies where ConcentrationRiskByTypeAxis is missing **/

ASSERT US.0109.9568 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

exists({nonils @concept=ConcentrationRiskPercentage1 @ConcentrationRiskByBenchmarkAxis = * @ConcentrationRiskByTypeAxis = none}#fact1)

message
"{$fact1.concept.name} with value {$fact1} should always be used with both the ConcentrationRiskByTypeAxis and the ConcentrationRiskByBenchmarkAxis.  This element has no meaning without both these axis at a minimum.  This fact only contains the denominator of ConcentrationRiskByBenchmarkAxis, and not the member used on the ConcentrationRiskByTypeAxis which represents the benchmark numerator. The properties of this {$fact1.concept.name} fact are:

Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]

/** RULE 9569 Identifies where a fact has both ConcentrationRiskByTypeAxis  and ConcentrationRiskByBenchmarkAxis but the disaggregated axis is missing **/


ASSERT US.0109.9569 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

exists([nonils @concept=ConcentrationRiskPercentage1 @ConcentrationRiskByBenchmarkAxis = * @ConcentrationRiskByTypeAxis = * where $fact != 1]#fact1)

message
"{$fact1.concept.name} with value {$fact1} should always be used with the ConcentrationRiskByTypeAxis the ConcentrationRiskByBenchmarkAxis and a third axis to identify the risk area.  This element can only have a value of 1 when only the ConcentrationRiskByTypeAxis and ConcentrationRiskByBenchmarkAxis are used.  This fact needs another axis to identify the specific {$fact1.dimension(ConcentrationRiskByTypeAxis)} risk. The properties of this {$fact1.concept.name} fact are:

Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]