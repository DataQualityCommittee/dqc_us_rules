/**
DQC Rules
 (c) Copyright 2017 - 2020, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2021
Release Version 16
**/

RULE-NAME-PREFIX DQC


assert US.0121.9579 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$transition_elements = navigate parent-child descendants from set(Asu201601TransitionAbstract, Asu201618TransitionAbstract)taxonomy $us-gaap where not $relationship.target.is-abstract returns (target-name);

for $transition_element in $transition_elements

	$fact1 = {@concept = $transition_element}
	
	if exists($fact1)
		true
	else
		skip

message

"The element {$fact1.concept.name} with a value of {$fact1} has been used in the filing.  This element is a transition element and is intended to be used in periods before adoption of amendments from Accounting Standards Update 2016-01 or Accounting Standards Update 2016-18. The element should no longer be used for filings with fiscal years beginning after December 15th 2017 for ASU 2016-01 - Financial Instruments and December 15th 2018 for ASU 2016-18 - Restricted Cash.  

Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}
Decimals: {$fact1.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$rule_id]

assert US.0112.9581 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$EGCFlag = if list([covered @concept.local-name ='EntityEmergingGrowthCompany']).length > 0
				first(list([covered @concept.local-name ='EntityEmergingGrowthCompany']))
			else 
				false
				
if (any(set(exists({covered @concept.local-name ='DocumentType' where $fact in $report_type_exclusions})))) OR $EGCFlag
	skip
else
	$transition_elements = navigate parent-child descendants from set(ASU201602TransitionAbstract, Asu201712TransitionAbstract, ASU201807TransitionAbstract)taxonomy $us-gaap where not $relationship.target.is-abstract returns (target-name);

	for $transition_element in $transition_elements

		$fact1 = {@concept = $transition_element}
		
		if exists($fact1)
			true
		else
			skip

message

"The element {$fact1.concept.name} with a value of {$fact1} has been used in the filing.  This element is a transition element and is intended to be used in periods before adoption of amendments from Accounting Standards Update 2016-02,  Accounting Standards Update 2017-12, or Accounting Standards Update 2018-07. The element should no longer be used for filings with fiscal years beginning after:

December 15, 2018 for ASU 2016-02 - Leases
December 15, 2018 for ASU 2017-12 - Derivatives and Hedging
December 15, 2019 for ASU 2018-07 - Compensation - Stock Compensation  

Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}
Decimals: {$fact1.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$rule_id]





