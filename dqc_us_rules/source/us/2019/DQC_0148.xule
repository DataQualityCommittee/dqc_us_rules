/**
DQC Rules
 (c) Copyright 2017 - 2022, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2019
**/

rule-name-prefix DQC

//-----------DQC.US.0148.9872 ---------------//

assert US.0148.9872  satisfied
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

$calcNetworks = FILTER $CALC_NETWORKS where $item.role.description.contains('148410 - Statement -') 
	or $item.role.description.contains('148411 - Statement -') 
	or $item.role.description.contains('148412 - Statement -')
	or $item.role.description.contains('148413 - Statement -')

$OCI_Concepts = set(for $calcNetwork in $calcNetworks.sort
						$calcNetwork.concept-names
					);

$Calc_Descendants_of_NetIncome = navigate summation-item descendants from list(NetIncomeLoss, ProfitLoss, NetIncomeLossAvailableToCommonStockholdersBasic, NetIncomeLossAvailableToCommonStockholdersDiluted) returns set (target-name)

$invalidConcepts = $Calc_Descendants_of_NetIncome.intersect($OCI_Concepts)

for $invalidConcept in $invalidConcepts
	true

message
"The company has reported the concept {$invalidConcept} as a descendant of Income Losss in the calculation linkbase of the filing.  This element is an OCI item and should not be included as a component of Net Income Loss.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

rule-focus taxonomy().concept($invalidConcept)