/**
DQC Rules
 (c) Copyright 2017 - 2021, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2019
Release Version 16
**/

RULE-NAME-PREFIX DQC

ASSERT US.0119.9576 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

exists([@concept=IncomeLossFromContinuingOperationsBeforeIncomeTaxesMinorityInterestAndIncomeLossFromEquityMethodInvestments])#b4tax and 
count(list([@concept=IncomeLossFromEquityMethodInvestments])) == 0 and count(list([@concept=IncomeLossFromAffordableHousingProjectsEquityMethodInvestments]))==0

message
"The company has reported a value for the concept representing Income before income tax, Non Controlling Interest and Equity Method Investments with a value of {$b4tax}. However the company has not reported a value for equity method investments. If the company has no equity method investements then the filer should use the element IncomeLossFromEquityMethodInvestments.

Element : {$b4tax}
Value : {$b4tax} 
Period : {$b4tax.period} 
Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
status $status
effectiveDate $effective_dates[$rule_id]

rule-focus $b4tax


ASSERT US.0119.9577 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** Equity method investments is excluded from rule 0081, so this rule addresses this **/

$ComponentsOfIncome = navigate summation-item descendants from  IncomeLossFromContinuingOperationsBeforeIncomeTaxesMinorityInterestAndIncomeLossFromEquityMethodInvestments returns set (target-name);

if $ComponentsOfIncome.contains(IncomeLossFromEquityMethodInvestments)
	true
else
	false

message
"The company has included IncomeLossFromEquityMethodInvestments as a component of the concept \"Income Loss From Continuing Operations Before Income Taxes Minority Interest And Income Loss From Equity Method Investments\". The element IncomeLossFromEquityMethodInvestments cannot be included as a component of this element as this element excludes minority interest.  The filer should use the concept IncomeLossFromContinuingOperationsBeforeIncomeTaxesExtraordinaryItemsNoncontrollingInterest instead.

Element : IncomeLossFromEquityMethodInvestments
Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
status $status
effectiveDate $effective_dates[$rule_id]

rule-focus IncomeLossFromEquityMethodInvestments




