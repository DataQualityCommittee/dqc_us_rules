/**
DQC Rules
 (c) Copyright 2017 - 2021, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2019
Release Version 17
**/

RULE-NAME-PREFIX DQC

ASSERT US.0131.9729 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];
/**  This rule identifies thos case where an extensible list item has been used and checks if the balance sheet has a suficient balance to carry the aount.**/

/** 
Identify if any of the following extensible list items have been used
**/

$leaseItems = list(list(FinanceLeaseLiability, FinanceLeaseLiabilityStatementOfFinancialPositionExtensibleList),
                    list(FinanceLeaseLiabilityCurrent, FinanceLeaseLiabilityCurrentStatementOfFinancialPositionExtensibleList),
                    list(FinanceLeaseLiabilityNoncurrent, FinanceLeaseLiabilityNoncurrentStatementOfFinancialPositionExtensibleList),
                    list(FinanceLeaseRightOfUseAsset, FinanceLeaseRightOfUseAssetStatementOfFinancialPositionExtensibleList),
                    list(OperatingLeaseRightOfUseAsset,OperatingLeaseRightOfUseAssetStatementOfFinancialPositionExtensibleList),
                    list(OperatingLeaseLiability, OperatingLeaseLiabilityStatementOfFinancialPositionExtensibleList),
                    list(OperatingLeaseLiabilityCurrent, OperatingLeaseLiabilityCurrentStatementOfFinancialPositionExtensibleList),
                    list(OperatingLeaseLiabilityNoncurrent, OperatingLeaseLiabilityNoncurrentStatementOfFinancialPositionExtensibleList)
                    );

for $leaseItem in $leaseItems

    /** Check if an extensible list item exists. If it does then get the qname and look up the value of the BS Item and compare it to value of the lease
    If the value of the lease is greater than the balance sheet line item then report an error. **/
    if exists({@concept = $leaseItem[2] @unit}) and exists({@concept = $leaseItem[1] @unit=*})
        $extensibleListQname = set({@concept = $leaseItem[2] @unit});
        
        /** Get Value of the QName **/
        
        $leaseLocationQnames  = set(for $x in $extensibleListQname
                                                        if $x.contains('us-gaap:')
                                                            $x.to-qname 
                                                        else
                                                            $colan_loc = $x.index-of(':') + 1;
		                                                    $local-name-ext = $x.substring($colan_loc);
                                                            qname(taxonomy().entry-point-namespace,$local-name-ext)
                                                        )
        /** Handles where the lease is split across line items **/
        $ValueOfLeaseLineItemINBS = first-value(sum(list({@concept in $leaseLocationQnames @unit=*})), skip);
        $ValueOfLeaseItem = {@concept = $leaseItem[1] @unit=*};
        $ValueOfLeaseItem > $ValueOfLeaseLineItemINBS
    else
        skip

message
"The concept {$leaseItem[2]} has been reported with a value of {$extensibleListQname.join(",")}. The value of this element is recorded as {$ValueOfLeaseLineItemINBS} in the financial statements.  This value is less than the value recorded for {$leaseItem[1]} with a value of {$ValueOfLeaseItem}. It is expected that the value of {$leaseItem[1]} should be less than the value of the Financial statement line item as it comprises this total.
Element : {$ValueOfLeaseItem .concept.name}
Period : {$ValueOfLeaseItem .period} 
Dimensions : {$ValueOfLeaseItem.dimensions.join(', ','=')}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


severity error
status $status
effectiveDate $effective_dates[$rule_id]

rule-focus $ValueOfLeaseItem 

