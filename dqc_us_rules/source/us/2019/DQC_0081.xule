/**

DQC Rules
 (c) Copyright 2017 - 2019, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2019

**/


rule-name-prefix DQC

assert US.0081.9277 satisfied

$rule_id = '9277';
/** Get all the calculations defined in the Instance document. **/

$extCalcRelationship1 = navigate summation-item descendants returns by network set (relationship);
		
for ($network in $extCalcRelationship1.keys)
	for ($extCalcRelationship in (filter $extCalcRelationship1[$network] where $item.role.description.contains('- Statement ')))
		$extCalcNetwork = $network.role.description;
		$extCalcSourceName = $extCalcRelationship.source.name;
		$extCalcTargetName = set($extCalcRelationship.target.name.local-name);
		$parent_child_allowed = list($extCalcSourceName, $extCalcRelationship.target.name);
		if  ($parent_child_allowed in $siblings_demotable_to_child or
			$extCalcSourceName == AdjustmentsToReconcileNetIncomeLossToCashProvidedByUsedInOperatingActivities or 
			$extCalcSourceName in $elements_excluded_from_rule or
			$extCalcRelationship.target.name in $child_elements_promotable_to_sibling_of_parent_element or
			$extCalcRelationship.target.name in $cash_flow_exceptions
			)
			skip
		/**else		
			$a1 = (navigate summation-item ancestors from $extCalcRelationship.target.name taxonomy $us-gaap returns (target-name)).to-set;
			$allowable_sibling = set(for ($x in $siblings_demotable_to_child)
											$x[2]);
			if ($a1.intersect($allowable_sibling).length > 0)
				skip **/
		else
			$s1 = navigate summation-item siblings from $extCalcSourceName taxonomy $us-gaap returns (target-name);
			/** This makes a list of the descendants of the siblings but excludes the cash flow adjustment items as they are add backs and should be children. It also excludes any descendants that deduct from the sibling. For example it may take one item out of a category and add to another.**/
			$sibling_items = navigate summation-item descendants include start from ($s1) stop when ($relationship.source.name == AdjustmentsToReconcileNetIncomeLossToCashProvidedByUsedInOperatingActivities) taxonomy $us-gaap returns (target-name);
			$sibling_items_names = filter $sibling_items.to-set returns $item.local-name;
			$inconsistencies = $extCalcTargetName.intersect($sibling_items_names);
			if ($inconsistencies.length > 0)
				$inappropriate_child = [nonils @concept.local-name in $inconsistencies where $fact != 0];
				$source_value = [nonils @concept = $extCalcSourceName];
				/** Evaluate the expression **/
				exists($inappropriate_child) and 
				exists($source_value) and
				not any(set(for ($parent in $sibling_elements_with_siblings_demotable_to_descendant)
									$extCalcSourceName == $parent[1] and $extCalcRelationship.target.name not in $parent[2]))
			
			else
				false
						
						
	 
message
"The line item concept {$inappropriate_child.concept.name} with a value of {$inappropriate_child} is represented as a child of {$extCalcSourceName} with a value of {$source_value} in the companies extension taxonomy role {$extCalcNetwork}. The element {$inappropriate_child.concept.name} however, is not represented as a component of {$extCalcSourceName} in the US-GAAP taxonomies. The US-GAAP taxonomies include {$inappropriate_child.concept.name} as a sibling element (or child of that sibling) relative to {$extCalcSourceName}. Please review the element selection to determine that an appropriate element has been used for the parent element {$extCalcSourceName} and the child element {$inappropriate_child.concept.name}.

The properties of this {$inappropriate_child.concept.name} fact are:

Period :{$inappropriate_child.period}
Dimensions : {$inappropriate_child.dimensions.join(', ','=')}
Unit : {$inappropriate_child.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}
"

RULE-FOCUS $inappropriate_child

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]


assert US.0081.9278 satisfied

$rule_id = '9277';
/** Get all the calculations defined in the Instance document. **/

$fact_values = list([nonils covered]);

/** Exludes NetIncomeLoss, ProfitLoss as these are are used in the CF for addbacks **/
$value-concepts = (filter $fact_values returns $item.concept.name).to-set - $income_items;
for ($value-con in $value-concepts)
	$extCalcRelationship1 = navigate summation-item siblings from $value-con returns by network set (relationship);

		
	for ($network in $extCalcRelationship1.keys)
		for ($extCalcRelationship in (filter $extCalcRelationship1[$network] where $item.role.description.contains('- Statement ')))
			$extCalcNetwork = $network.role.description;
			$extCalcSourceName = $extCalcRelationship.source.name;
			$extCalcTargetName = set($extCalcRelationship.target.name.local-name);
			$parent_sibling_allowed = list($value-con, $extCalcRelationship.target.name);
			if  ($parent_sibling_allowed in $child_elements_promotable_to_sibling)
				skip
			else
				$child_items = navigate summation-item descendants from $value-con taxonomy $us-gaap returns (target-name);
				$child_items_names = filter $child_items.to-set returns $item.local-name;
				$inconsistencies = $extCalcTargetName.intersect($child_items_names);
				$inappropriate_sibling = [nonils @concept.local-name in $inconsistencies where $fact != 0];
			
				/** Evaluate the expression **/
			
				$inconsistencies.length > 0 and exists({$inappropriate_sibling}) and 
				not ($value-con in $elements_excluded_from_rule) and 
				not ($extCalcRelationship.target.name in $child_elements_promotable_to_sibling_of_parent_element) and
				not any(set(for ($parent in $parent_elements_with_children_promotable_to_sibling)
									$value-con == $parent[1] and $extCalcRelationship.target.name not in $parent[2]))
				 				
	 
message
"The line item concept {$inappropriate_sibling.concept.name} with a value of {$inappropriate_sibling} and a calculation parent of {$extCalcSourceName} is represented as a sibling of {$value-con} in the extension taxonomy role {$extCalcNetwork}, but {$inappropriate_sibling.concept.name} is represented as a descendent of {$value-con} in the us-gaap taxonomies. Please review the element selection to determine that an appropriate element has been used for these two line item concepts.

The properties of this {$inappropriate_sibling.concept.name} fact are:

Period :{$inappropriate_sibling.period}
Dimensions : {$inappropriate_sibling.dimensions.join(', ','=')}
Unit : {$inappropriate_sibling.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}
"
RULE-FOCUS $inappropriate_sibling

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]