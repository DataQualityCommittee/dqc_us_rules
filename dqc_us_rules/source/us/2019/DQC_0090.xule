/**
DQC Rules
 (c) Copyright 2017 - 2021, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2019
**/


rule-name-prefix DQC

assert US.0090.9374 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** Rule only applies to an annual filing **/

if set('10-K','10-KT','20-F').contains([covered @concept.local-name ='DocumentType']) 

/** Identifies that the concept LesseeOperatingLeaseLiabilityPaymentsDue is the sum of components OperatingLeaseLiability and LesseeOperatingLeaseLiabilityUndiscountedExcessAmount if either one is used. **/

	$sum1 = [@LesseeOperatingLeaseLiabilityPaymentsDue];

	$addend = [@OperatingLeaseLiability] <+ [@LesseeOperatingLeaseLiabilityUndiscountedExcessAmount];
	
	if tolerance_for_decimals($sum1,$addend,2)
		$paymentsDueCalc =  navigate summation-item descendants from LesseeOperatingLeaseLiabilityPaymentsDue returns set (target-name);
		$extInCalc = $paymentsDueCalc intersect $extension_qname
		if $extInCalc.length == 0
			true
		else
			false
	else
		false
else
	false

message

"The element {$sum1.concept.name} with a value of {$sum1} should be equal to the sum of OperatingLeaseLiability plus LesseeOperatingLeaseLiabilityUndiscountedExcessAmount. The value of these adds to {$addend}.  A difference of {$sum1 - $addend}. In an annual filing this reconciliation is required and all elements should have a value in the current reporting period.

If the difference is due to an extension reconciliation item, then the filer should add a calculation relationship as a child of LesseeOperatingLeaseLiabilityPaymentsDue to indicate that there are additional items in the reconciliation between FinanceLeaseLiabilityPaymentsDue, LesseeOperatingLeaseLiabilityUndiscountedExcessAmount and OperatingLeaseLiability. If an extension item appears as part of the reconcilaition then no error is produced.
 
Period :{$sum1.period}
Dimensions : {$sum1.dimensions.join(', ','=')}
Unit : {$sum1.unit}
Decimals: {$sum1.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$rule_id]

assert US.0090.9375 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** Rule only applies to an annual filing **/

if set('10-K','10-KT','20-F').contains([covered @concept.local-name ='DocumentType']) 
	
/** Identifies that the concept FinanceLeaseLiabilityPaymentsDue is the sum of components FinanceLeaseLiability and FinanceLeaseLiabilityUndiscountedExcessAmount if either one is used **/
/** Rule only works at the default level **/

	$sum1 = [@FinanceLeaseLiabilityPaymentsDue];

	$addend = [@FinanceLeaseLiability] <+ [@FinanceLeaseLiabilityUndiscountedExcessAmount];

	if tolerance_for_decimals($sum1,$addend,2)
		$paymentsDueCalc =  navigate summation-item descendants from FinanceLeaseLiabilityPaymentsDue returns set (target-name);
		$extInCalc = $paymentsDueCalc intersect $extension_qname
		if $extInCalc.length == 0
			true
		else
			false
	else
		false

else
	false
message

"The element {$sum1.concept.name} with a value of {$sum1} should be equal to the sum of FinanceLeaseLiability plus FinanceLeaseLiabilityUndiscountedExcessAmount. The value of these adds to {$addend}.  A difference of {$sum1 - $addend}. In an annual filing this reconciliation is required and all elements should have a value in the current reporting period.

If the difference is due to an extension reconcilaition item, then the filer should add a calculation relationship as a child of FinanceLeaseLiabilityPaymentsDue to indicate that there are additional items in the reconciliation between FinanceLeaseLiabilityPaymentsDue, FinanceLeaseLiabilityUndiscountedExcessAmount and FinanceLeaseLiability. If an extension item appears as part of the reconcilaition then no error is produced.
 
Period :{$sum1.period}
Unit : {$sum1.unit}
Decimals: {$sum1.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$rule_id]

