/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2023

**/

RULE-NAME-PREFIX DQC

ASSERT US.0159.10081 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

$liquidation_networks = filter $PRES_NETWORKS where ($item.concept-names.intersect($LIQUIDATION_BASIS_CONCEPTS)).length > 0 returns $item;

$NonFinancialStatement = set('PRE-14A','PRE 14A','DEF-14A','DEF 14A','DEF-14C','DEF 14C','11-K','11-K/A').contains([covered @concept.local-name ='DocumentType']);
  
if $liquidation_networks.length > 0 or $NonFinancialStatement 
    skip
else

    $All_statement_abstract_elements = sum(set(for $presNetwork in $BROAD_STATEMENT_PRES_NETWORKS.sort
                                    filter $presNetwork.concepts where $item.is-abstract returns $item.name))

    $Required_Abstracts = set(IncomeStatementAbstract, StatementOfFinancialPositionAbstract, StatementOfCashFlowsAbstract)

    for $header in $Required_Abstracts
            if $header == IncomeStatementAbstract
                    if $All_statement_abstract_elements.length > 0 and not ($All_statement_abstract_elements.contains($header) or $All_statement_abstract_elements.contains(StatementOfIncomeAndComprehensiveIncomeAbstract))
                    true
                else
                    false
            else
                 if $All_statement_abstract_elements.length > 0 and not $All_statement_abstract_elements.contains($header)
                    if $header == StatementOfCashFlowsAbstract and (
                                set( '10-Q', '10-Q/A').contains([covered @concept.local-name ='DocumentType']) or 
                                /** ASC 230  Allows no CF if statement of Investment and No level 3 Assets and No debt**/
                                ($All_statement_abstract_elements.contains(ScheduleOfInvestmentsAbstract) and
                                list({covered nonils @FairValueByFairValueHierarchyLevelAxis = FairValueInputsLevel3Member where $fact > 0}).length == 0
                                ))
                        false 
                    else
                        true
                else
                    false

message
"The concept {$header} does not appear in the financial statements of the filer.  This element needs to be included as a component of the financial statements.  Please add the element  {$header}  to the presentation of the filing.


Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}
"

SEVERITY error

effectiveDate $effective_dates[$rule_id]