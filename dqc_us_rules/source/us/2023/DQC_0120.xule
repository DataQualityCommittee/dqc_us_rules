/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2023
Release Version 18
**/

RULE-NAME-PREFIX DQC

ASSERT US.0120.9578 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

$balanceSheetRoles = (filter $PRES_NETWORKS where ($item.concept-names.contains(StatementOfFinancialPositionAbstract) or $item.role.uri.lower-case.contains('balancesheet') or $item.role.uri.lower-case.contains('financialposition')) and  $item.role.description.contains('- Statement ') and not $item.role.uri.lower-case.contains('parenthetical') returns $item.role).to-list

if $balanceSheetRoles.length == 0
	skip
else
	/** Sort so we always get the first  statement **/
	$balanceSheetRolesSorted = (filter $balanceSheetRoles returns list($item.description, $item.uri, $item )).sort

	$balanceSheetRole = $balanceSheetRolesSorted[1][3]

	$balanceSheetRoleUri = $balanceSheetRolesSorted[1][2]

	$balanceSheetItems = 
				navigate parent-child descendants from list(StatementOfFinancialPositionAbstract, AssetsAbstract, LiabilitiesAbstract, LiabilitiesAndStockholdersEquityAbstract) role $balanceSheetRole where $relationship.target.is-monetary == true returns set (target-name)

	for $y in list(FinanceLeaseLiabilityCurrentStatementOfFinancialPositionExtensibleList, FinanceLeaseLiabilityNoncurrentStatementOfFinancialPositionExtensibleList, FinanceLeaseRightOfUseAssetStatementOfFinancialPositionExtensibleList, OperatingLeaseLiabilityNoncurrentStatementOfFinancialPositionExtensibleList, OperatingLeaseLiabilityCurrentStatementOfFinancialPositionExtensibleList, OperatingLeaseRightOfUseAssetStatementOfFinancialPositionExtensibleList)
		$listItem = {@concept = $y}
		for $x in $listItem
			not $balanceSheetItems.contains($x)
			

	message
	"The extensible list item {$y} has a value of {$x}. This item refers to the location of this item in the financial statements.  However, the concept {$x} does not appear in the presentation role {$balanceSheetRole.description}. Either update the value of the extensible list item {$y} with the correct balance sheet element or add the balance sheet line item {$x} to the presentation linkbase in the balance sheet role.

	The format of the value in the extensible list has to be namespace-uri#concept-name an example is as follows:

	http://fasb.org/us-gaap/2023#PropertyPlantAndEquipmentNet

	Element : {$y}
	Value : {$x} 
	Period : {$listItem.period} 

	Rule Element Id:{$rule_id}
	Rule version: {$ruleVersion}"

	severity error
	
	effectiveDate $effective_dates[$rule_id]



