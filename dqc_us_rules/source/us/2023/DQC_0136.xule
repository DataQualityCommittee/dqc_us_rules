/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2023
**/


rule-name-prefix DQC


ASSERT US.0136.9837 SATISFIED

/** This rule identifies if an extended link role has a valid value. **/

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

/** Identify all presentation networks in the company extension **/

	$allNetworks = $PRES_NETWORKS;

	/** eliminate reports with no presentations containg a statement **/

	$fsNetworks = filter $allNetworks where $item.role.description.contains('- Statement ');
	
	if $fsNetworks.length == 0
			skip
	else
		/** Get all concepts in FS Networks **/
		$fsConcepts = sum(set(for ($network in $fsNetworks)
								$network.concepts));

		/** Get those concepts that are monetary items **/					
		$fsConceptsQname = filter $fsConcepts where $item.is-monetary returns $item.name;

		$Ext_Enum_Minus_Leases = set(for $ext_pair in $EXT_ENUM
									if $ext_pair[1] not in $LEASE_ITEMS
										$ext_pair
									else 
										skip)

		/** Go through each pair of extensible enumerations defined in the taxonomy **/
		for $ext_pairs in $Ext_Enum_Minus_Leases
			$FS_concept = $ext_pairs[1]
			$related_ext_enum = $ext_pairs[2]

			/** get any values for the extensible list concept and ensure that they appear in the financial statements.**/
			$listItem = {nonils @concept = $related_ext_enum}
			for $x in $listItem
				not $fsConceptsQname.contains($x)
			

	message
	"The extensible list item {$related_ext_enum} has a value of {$x}. This item refers to the location of this item in the financial statements.  However, the concept {$x} does not appear in the face financial statements. Either update the value of the extensible list item {$x} with the correct element or add the line item {$x} to the presentation and calculation linkbase in the appropriate statement role.

	Rule Element Id:{$rule_id}
	Rule version: {$ruleVersion}"

	severity error
	effectiveDate $effective_dates[$rule_id]

