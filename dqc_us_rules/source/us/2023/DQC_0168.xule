/**
DQC Rules
 (c) Copyright 2017 - 2023, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2023

**/

/** BAD FILINGS
ABCO ENERGY  -- missing calcs

**/

RULE-NAME-PREFIX DQC

ASSERT US.0168.10096 SATISFIED
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

if set('10-K','10-KT','20-F').contains([covered @concept.local-name ='DocumentType']) 

        /**Check the statutory tax rate percentage appears in a calculation if reported **/

        $is_statutory_taxRate_in_calc = navigate summation-item  descendants from EffectiveIncomeTaxRateContinuingOperations returns set target-name

        not $is_statutory_taxRate_in_calc.contains(EffectiveIncomeTaxRateReconciliationAtFederalStatutoryIncomeTaxRate)
        and 
        exists([@concept = EffectiveIncomeTaxRateReconciliationAtFederalStatutoryIncomeTaxRate]#stat)
        and
        exists([@concept = EffectiveIncomeTaxRateContinuingOperations]#effective)

else
    skip

message
"The concept {$stat.concept.name} is used in the filing with a value of {$stat}.  This element is reported but is not included in a calculation relationship with the element  {$effective.concept.name} with a value of {$effective}.  The filer should add a calulation relationship between these two elements to capture the details of the effective tax rate reonciliation."

SEVERITY error
status $status
effectiveDate $effective_dates[$ruleId]

rule-focus $stat
/*
Add rules to ensure taxo reconcilation calculations have been reported for monetary items
*/

ASSERT US.0168.10097 SATISFIED
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

if set('10-K','10-KT','20-F').contains([covered @concept.local-name ='DocumentType']) 

        /**Check the statutory tax rate percentage appears in a caluation if reported **/

        $is_statutory_taxRate_in_calc = navigate summation-item  descendants from IncomeTaxExpenseBenefit returns set target-name

        not $is_statutory_taxRate_in_calc.contains(IncomeTaxReconciliationIncomeTaxExpenseBenefitAtFederalStatutoryIncomeTaxRate)
        and 
        exists([@concept = IncomeTaxReconciliationIncomeTaxExpenseBenefitAtFederalStatutoryIncomeTaxRate]#stat)
        and
        exists([@concept = IncomeTaxExpenseBenefit]#effective)

else
    skip

message
"The concept {$stat.concept.name} is used in the filing with a value of {$stat}.  This element is reported but is not included in a calculation relationship with the element  {$effective.concept.name} with a value of {$effective}.  The filer should add a calulation relationship between these two elements to capture the details of the effective tax rate reonciliation."

SEVERITY error
status $status
effectiveDate $effective_dates[$ruleId]

rule-focus $stat