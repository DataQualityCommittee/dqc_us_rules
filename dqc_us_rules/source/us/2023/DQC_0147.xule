/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2023
**/


rule-name-prefix DQC

//-----------DQC.US.0147.9871 ---------------//

assert US.0147.9871  satisfied
$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

{nonils @@concept in $NON_NEG_ITEMS.keys @srt:ConsolidationItemsAxis = srt:ConsolidationEliminationsMember where $fact != 0 and $fact.is-numeric and
		(not match_set_substring($fact, ($EXCLUDE_NON_NEG_STRING_MEMBERS - set('eliminat','consolidat')))
		and (to-set($fact.dimensions-explicit.values.name) intersect $EXCLUDE_NON_NEG_MEMBERS).length == 0
		and ($fact.dimensions.keys.name intersect $EXCLUDE_NON_NEG_AXIS).length == 0
		and not_elim_adjust($fact, $EXCLUDE_NON_NEG_AXIS_MEMBERS))}#fact1.signum == {nonils @srt:ConsolidationItemsAxis = none where $fact.is-numeric and $fact != 0}#fact2.signum
message
	"The concept {$fact1.concept.name} with an elimination value of {$fact1} has a sign which is the same as the defaulted value for {$fact2.concept.name} of {$fact2}. It is expected that the defaulted value and eliminations for the line item {$fact1.concept.name} would be the opposite of each other.
Review the elimination amount to determine that the sign is correct.

The properties of this {$fact1.concept.name} fact are:

Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}\n
Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$rule_id]

rule-focus $fact1
