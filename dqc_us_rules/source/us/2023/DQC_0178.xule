/**
DQC Rules
 (c) Copyright 2017 - 2023, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2023

**/

RULE-NAME-PREFIX DQC

/** This rule checks that when dividends are reported in the statement of shareholders equity that the appropriate signs are used and that values are reported with the correct members on the StatementEquityComponentsAxis.  

It has been observed that filers have used the elements used for the issuance of dividends as stock to represent a payment in actual cash.**/

constant $DIVIDEND_CONCEPTS = list(DividendsCommonStock, DividendsCommonStockStock, DividendsCommonStockCash, DividendsCommonStock, DividendsCommonStockPaidinkind, DividendsPreferredStockCash, DividendsPreferredStock, DividendsPreferredStockStock, DividendsPreferredStockPaidinkind, Dividends, DividendsCash, DividendsStock, DividendsPaidinkind, DividendsShareBasedCompensationCash, DividendsSharebasedCompensationStock)

constant $INVALID_DIVIDEND_CONCEPTS_WITH_MEMBER = dict(
                                                list(RetainedEarningsAppropriatedMember,$DIVIDEND_CONCEPTS),
                                                list(CommonStockMember,list(DividendsPreferredStockCash, DividendsPreferredStock, DividendsPreferredStockStock, DividendsPreferredStockPaidinkind, DividendsCash, DividendsCommonStockCash)),
                                                list(CommonStockIncludingAdditionalPaidInCapitalMember,list(DividendsPreferredStockCash, DividendsPreferredStock, DividendsPreferredStockStock, DividendsPreferredStockPaidinkind)),
                                                list(PreferredStockMember,list(DividendsCommonStock, DividendsCommonStockStock, DividendsCommonStockCash, DividendsCommonStock, DividendsCommonStockPaidinkind, DividendsPreferredStockCash, DividendsCash)),
                                                list(PreferredStockIncludingAdditionalPaidInCapitalMember,list(DividendsCommonStock, DividendsCommonStockStock, DividendsCommonStockCash, DividendsCommonStock, DividendsCommonStockPaidinkind)),
                                                list(TreasuryStockPreferredMember,$DIVIDEND_CONCEPTS),
                                                list(TreasuryStockCommonMember,$DIVIDEND_CONCEPTS),
                                                list(AccumulatedOtherComprehensiveIncomeMember,$DIVIDEND_CONCEPTS),
                                                list(ComprehensiveIncomeMember,$DIVIDEND_CONCEPTS),
                                            )

/**--NONNEG Dividend Items Impact on Retained earnings - This should never increase retained earnings**/


ASSERT US.0178.10134 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$retainedEarningsTypes = list(RetainedEarningsMember, RetainedEarningsUnappropriatedMember)

$negativeDividends = {@concept in $DIVIDEND_CONCEPTS @StatementEquityComponentsAxis in $retainedEarningsTypes where $fact < 0}

exists($negativeDividends)

message
"The filing includes a value for dividends using the concept {$negativeDividends.concept.name} and the retained earnings member with a value of {$negativeDividends}. Dividends must be deducted from retained earnings.  Please check that the sign of the dividends value is positive.

The properties of this  fact are:
Period :{$negativeDividends.period}
Dimensions : {$negativeDividends.dimensions.join(', ','=')}
Unit : {$negativeDividends.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $negativeDividends
status $status
effectiveDate $effective_dates[$rule_id]



/** AdjustmentsToAdditionalPaidInCapitalDividendsInExcessOfRetainedEarnings **/
ASSERT US.0178.10135 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$additionPaidInCapital = list(AdditionalPaidInCapitalMember, OtherAdditionalCapitalMember)

$dividendsInAPIC = {@concept in $DIVIDEND_CONCEPTS @StatementEquityComponentsAxis in $additionPaidInCapital where $fact > 0}

exists($dividendsInAPIC)

message
"The filing includes a value for dividends using the concept {$dividendsInAPIC.concept.name} and the additional paid in capital member with a decreasing value of {$dividendsInAPIC}. Dividends should generally be deducted from retained earnings and not deducted from additional paid in capital. In certain cases dividends can be deducted from paid in capital, but in these cases the element AdjustmentsToAdditionalPaidInCapitalDividendsInExcessOfRetainedEarnings defined in the taxonomy should be used.  Please update the filing to reflect this.

The properties of this  fact are:
Period :{$dividendsInAPIC.period}
Dimensions : {$dividendsInAPIC.dimensions.join(', ','=')}
Unit : {$dividendsInAPIC.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $dividendsInAPIC
status $status
effectiveDate $effective_dates[$rule_id]

ASSERT US.0178.10136 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$additionPaidInCapital = list(AdditionalPaidInCapitalMember, OtherAdditionalCapitalMember)

$dividendsInAPIC = {@concept in list(DividendsCommonStockCash,DividendsPreferredStockCash) @StatementEquityComponentsAxis in $additionPaidInCapital where $fact < 0}

exists($dividendsInAPIC)

message
"The filing includes a negative value for dividends using the concept {$dividendsInAPIC.concept.name} and the additional paid in capital member with a value of {$dividendsInAPIC}. If APIC is increasing due to the declaration of dividends this indicates that the filer is doing issuance of stock as a dividend.  This has the impact of increasing APIC and reducing retained earnings. In these cases the elements DividendsCommonStockStock, or DividendsCommonStock should be used.

The properties of this  fact are:
Period :{$dividendsInAPIC.period}
Dimensions : {$dividendsInAPIC.dimensions.join(', ','=')}
Unit : {$dividendsInAPIC.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $dividendsInAPIC
status $status
effectiveDate $effective_dates[$rule_id]

/** DividendsCommonStockStock used instead of DividendsCommonStockCash**/
ASSERT US.0178.10137 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$retainedEarningsTypes = list(RetainedEarningsMember, RetainedEarningsUnappropriatedMember)

$additionPaidInCapital = list(AdditionalPaidInCapitalMember, OtherAdditionalCapitalMember);
$DivdedndImpactRE = {nonils @@concept = list(DividendsCommonStockStock, StockIssuedDuringPeriodValueStockDividend) @StatementEquityComponentsAxis in $retainedEarningsTypes where $fact != 0}
$DivdedndImpactSH = {nonils @@concept =list(DividendsCommonStockStock, StockIssuedDuringPeriodValueStockDividend) @StatementEquityComponentsAxis = none where $fact != 0}
$InvalidValueExists = ($DivdedndImpactRE == $DivdedndImpactSH );
$misingAPIC = list({nonils @@concept = list(DividendsCommonStockStock, StockIssuedDuringPeriodValueStockDividend)  @StatementEquityComponentsAxis in $additionPaidInCapital where $fact != 0}).length == 0

$InvalidValueExists and $misingAPIC

message
"The filing includes a value for dividends using the concept {$DivdedndImpactSH.concept.name} with a value of {$DivdedndImpactSH}. This value represents a dividend paid in stock, but no impact has been reported on the value of APIC. If this is an actual dividend paid in stock it should  include the retained earnings member and not have a value in the default. If it is a dividend paid fully in cash then the element DividendsCommonStockCash should be used.  If it is partial stock and cash then the element DividendsCommonStock should be used.

The properties of this  fact are:
Period :{$DivdedndImpactSH.period}
Dimensions : {$DivdedndImpactSH.dimensions.join(', ','=')}
Unit : {$DivdedndImpactSH.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $DivdedndImpactSH
status $status
effectiveDate $effective_dates[$rule_id]

/** Inappropriate use of members and dividend concepts**/
ASSERT US.0178.10138 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

for $memberToCheck in $INVALID_DIVIDEND_CONCEPTS_WITH_MEMBER.keys
           $divFact = {nonils @concept in $INVALID_DIVIDEND_CONCEPTS_WITH_MEMBER[$memberToCheck] @StatementEquityComponentsAxis = $memberToCheck where $fact != 0}
           exists($divFact)

message
"The filing includes a value for dividends using the concept {$divFact.concept.name} with a value of {$divFact}. This value is reported with the dimension member {$memberToCheck}.  This member should not be used with the concept {$divFact.concept.name}.  Please check the filing and select a more appropriate concept.

The properties of this  fact are:
Period :{$divFact.period}
Dimensions : {$divFact.dimensions.join(', ','=')}
Unit : {$divFact.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $divFact
status $status
effectiveDate $effective_dates[$rule_id]


ASSERT US.0178.10139 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$dividendEventConcepts = list(DividendsPayableAmountPerShare,DividendsPayableDateOfRecordDayMonthAndYear,DividendsPayableDateDeclaredDayMonthAndYear,DividendPayableDateToBePaidDayMonthAndYear,CommonStockDividendsPerShareDeclared, PreferredStockDividendsPerShareDeclared)

$badDividendAxis = list(
                        qname($EXTENSION_NS,'DividendDeclarationDateAxis'),
                        qname($EXTENSION_NS,'DividendDeclaredAxis'),
                        AwardTypeAxis,
                        srt:TitleOfIndividualAxis,
                        srt:CurrencyAxis,
                        srt:RangeAxis,
                        qname($EXTENSION_NS,'DividendsDeclaredTypeAxis'),
                        qname($EXTENSION_NS,'DistributionsPaymentDateAxis'),
                        AwardDateAxis,
                        StatementEquityComponentsAxis,
                        FairValueByAssetClassAxis,
                        RelatedPartyTransactionsByRelatedPartyAxis,
                        SubsidiarySaleOfStockAxis,
                        DebtInstrumentAxis,
                        LongtermDebtTypeAxis)

$validAxis = filter taxonomy().concepts where $item.name in $badDividendAxis returns $item.name;

for $axis in $validAxis
           
           $divFact = {@concept in $dividendEventConcepts @$axis = * }
           exists($divFact)

message
"The filing includes a value for dividends using the concept {$divFact.concept.name} with a value of {$divFact}. This value is reported with the dimension  {$axis}.  This dimension should not be used with the concept {$divFact.concept.name}.  Please check the filing and use the DividendsAxis with this value.

The properties of this  fact are:
Period :{$divFact.period}
Dimensions : {$divFact.dimensions.join(', ','=')}
Unit : {$divFact.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $divFact
status $status
effectiveDate $effective_dates[$rule_id]

ASSERT US.0178.10140 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$dividendDateConcepts = list(
                            list(DividendsPayableDateDeclaredDayMonthAndYear,DividendsPayableDateOfRecordDayMonthAndYear),
                            list(DividendsPayableDateDeclaredDayMonthAndYear,DividendPayableDateToBePaidDayMonthAndYear),
                            list(DividendsPayableDateOfRecordDayMonthAndYear,DividendPayableDateToBePaidDayMonthAndYear)
                            )

for $date in $dividendDateConcepts 
    $firstDate = {@concept = $date[1] @period=*} 
    $secondDate = {@concept = $date[2] @period=*}
    $firstDate >= $secondDate

message
"The date reported for {$firstDate.concept.name} of {$firstDate} is greater than or equal to the date reported for {$secondDate.concept.name} of {$secondDate}. The value of {$firstDate.concept.name} must be earlier than the date reported for {$secondDate.concept.name}. If this error occurs because seperate dividend events and associated values are distinguished by the context date, then each distinct dividend event should be identifed using the DividendsAxis and a discrete member for each dividend such as RegularDividendQ1Y2024Member.

The properties of  {$firstDate.concept.name} with a date of {$firstDate} are:
Period :{$firstDate.period}
Dimensions : {$firstDate.dimensions.join(', ','=')}

The properties of  {$secondDate.concept.name} with a date of {$secondDate}are:
Period :{$secondDate.period}
Dimensions : {$secondDate.dimensions.join(', ','=')}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $firstDate
status $status
effectiveDate $effective_dates[$rule_id]


ASSERT US.0178.10141 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$perShare = {@concept = CommonStockDividendsPerShareDeclared where $fact.inline-scale != none and $fact.inline-scale > 0}
true

message
"The concept CommonStockDividendsPerShareDeclared has been reported with a value of {$perShare}, with a inline-scale of {$perShare.inline-scale }. Generally the amount per share are not scaled when reported. This means the value in the XBRL report is overstated by the scale factor of {$perShare.inline-scale}.

The properties of  {$perShare.concept.name} are:
Period :{$perShare.period}
Dimensions : {$perShare.dimensions.join(', ','=')}
Scale: {$perShare.inline-scale }

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
status $status
effectiveDate $effective_dates[$rule_id]

/** Values before year end should be represeneted using the period it applies to for CommonStockDividendsPerShareDeclared.  DIvidend declared amount represents an expense and should be allocated to a period so that it can be compared to EPS which has periods. Really about getting the accounting alignment.  Identify reporting periods used in the filing and check dividends declared falls into one of them. Look for period durations with more than 4 durations as represntative.

Facts on the DividensAxis are excluded as they do not have date alignment as they represent the dividend event. Periods greater than a 13 months (398 days) are not considered valid periods to align with **/

constant $DICT_OF_DURATIONS = (filter list({covered @concept.period-type = duration where $fact.period.days < 398}) returns list($item.period)).agg-to-dict(1)
constant $DICT_OF_DURATION_FREQUENCY =  dict(
                                                for $key in $DICT_OF_DURATIONS.keys
                                                    list($key, $DICT_OF_DURATIONS[$key].length)
                                            )

ASSERT US.0178.10142 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$periodEnd = [covered @concept.local-name = 'DocumentPeriodEndDate']

$perShare = {@CommonStockDividendsPerShareDeclared @DividendsAxis = none where $fact.period.end < $periodEnd}
$start = $perShare.period.start;
$end = $perShare.period.end;

$validDurations = set(for $key in $DICT_OF_DURATIONS.keys
        if duration_contained($perShare.period, $key)
            $key
        else
            skip)

$frequencyOfDuration = $DICT_OF_DURATION_FREQUENCY[$perShare.period]

$perShareQuarterlyCheck = if $perShare.period.days > 85 and $perShare.period.days < 95
                                false
                          else
                                true

$frequencyOfDuration < 5 and $validDurations.length > 0  and $perShareQuarterlyCheck

message
"The concept {$perShare.concept.name} has been reported with a value of {$perShare}.  This value is reported with a duration starting on {$start} and ending on {$end}.  This period does not align with the reporting periods of the report.  Adjust the reporting period to reflect one of the following reporting durations {$validDurations.join(', ')}. This rule is flagging because the value reported for {$perShare.concept.name} is prior to the report year end and should have been expensed for a period. This period should be associated with this fact.  This makes it possible to compare the value with similar metrics such as earnings per share. If the dates are used to seperate two dividends within the same reporting period then the DividendsAxis should be used to seperate the values.

The properties of  {$perShare.concept.name} are:
Period :{$perShare.period}
Dimensions : {$perShare.dimensions.join(', ','=')}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $perShare
status $status
effectiveDate $effective_dates[$rule_id]


ASSERT US.0178.10143 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];


$factsPerDividendAxis = list({covered @DividendsAxis=*})

$dictOfConceptsUsedOnDivididendAxis = (filter $factsPerDividendAxis returns list(list($item.concept.name, $item.dimension(DividendsAxis).name))).agg-to-dict(1)

for $key in $dictOfConceptsUsedOnDivididendAxis.keys
            if $dictOfConceptsUsedOnDivididendAxis[$key].length > 1
                $factValues = list({@concept = $key[1] @DividendsAxis=$key[2] @period=*});
                true
            else
                false

message
"The filing has used the DividendsAxis with the member {$key[2]}, but has used the concept {$key[1]} multiple times on this axis with different period contexts. The concept {$key[1]} has been used mutiple times to capture the values {$factValues.join(' and ')} for the periods {$factValues.period.join(' and ')}.  The DividendAxis should use discrete members to represent different dividend events. The same concept should not be used more than once with the same dividend member {$key[2]}.

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $factValues.first
status $status
effectiveDate $effective_dates[$rule_id]

ASSERT US.0178.10144 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];


$divPerShareDeclared = {nonils @DividendsPayableAmountPerShare @period=* @unit=*}

$divPayDate = {nonils @DividendPayableDateToBePaidDayMonthAndYear @period=* @unit}

$divPayMonth = {nonils @DividendsPayableDateToBePaidMonthAndYear @period=* @unit} + '-01'

$divPayMonthEnd = if $divPayMonth  == '-01' 
                            skip 
                    else 
                        date($divPayMonth) + time-span('P1M')

$dividendPayDate = first-value($divPayDate, $divPayMonthEnd) 

$divPerShareDeclared.period.end > $dividendPayDate

message
"The element DividendsPayableAmountPerShare has a value of {$divPerShareDeclared} at {$divPerShareDeclared.period.end}. This represents the amount of dividends that are payable per share at {$divPerShareDeclared.period.end}. However, the payment date associated with the dividend has passed. The payment date associated with the dividend is {$dividendPayDate}. To resolve the issue the value of the context for DividendsPayableAmountPerShare should be changed to the declaration date or the value of DividendsPayableDateOfRecordDayMonthAndYear or DividendsPayableDateToBePaidMonthAndYear should be changed to reflect the date the dividend will be paid. If the payment date relates to a seperate dividened distribution unrelated to DividendsPayableAmountPerShare then the value of {$dividendPayDate} should be reported using the DividendsAxis.

The properties of  {$divPerShareDeclared.concept.name} are:
Period :{$divPerShareDeclared.period}
Dimensions : {$divPerShareDeclared.dimensions.join(', ','=')}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $divPerShareDeclared
status $status
effectiveDate $effective_dates[$rule_id]

/**------------ The following rules address issues in AdjustmentsToAdditionalPaidInCapitalDividendsInExcessOfRetainedEarnings ----------------------**/
ASSERT US.0178.10145 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$additionPaidInCapital = list(AdditionalPaidInCapitalMember, OtherAdditionalCapitalMember)

$dividendsInAPIC = {@concept = AdjustmentsToAdditionalPaidInCapitalDividendsInExcessOfRetainedEarnings @StatementEquityComponentsAxis in $additionPaidInCapital where $fact < 0}

exists($dividendsInAPIC)

message
"The filing includes a negative value for dividends using the concept {$dividendsInAPIC.concept.name} and the additional paid in capital member with a value of {$dividendsInAPIC}. The element AdjustmentsToAdditionalPaidInCapitalDividendsInExcessOfRetainedEarnings should not be reported with a negative value.  Check the disclosure and determine if the element DividendsCommonStockStock is more appropriate as this element is expected to increase APIC when a dividend distribution is made.

The properties of this  fact are:
Period :{$dividendsInAPIC.period}
Dimensions : {$dividendsInAPIC.dimensions.join(', ','=')}
Unit : {$dividendsInAPIC.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $dividendsInAPIC
status $status
effectiveDate $effective_dates[$rule_id]

/** 
**
** This specifically picks up the best buy case :
** https://www.sec.gov/Archives/edgar/data/764478/000076447823000041/bby-20230729x10q.htm 
** where they have used the incorrect sign
 **/

ASSERT US.0178.10146 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$dividendsInAPIC = {@concept = AdjustmentsToAdditionalPaidInCapitalDividendsInExcessOfRetainedEarnings @StatementEquityComponentsAxis = none where $fact < 0}

exists($dividendsInAPIC)

message
"The filing includes a negative value for dividends using the concept {$dividendsInAPIC.concept.name} in the default. The element AdjustmentsToAdditionalPaidInCapitalDividendsInExcessOfRetainedEarnings should not be reported with a negative value. 

The properties of this  fact are:
Period :{$dividendsInAPIC.period}
Dimensions : {$dividendsInAPIC.dimensions.join(', ','=')}
Unit : {$dividendsInAPIC.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $dividendsInAPIC
status $status
effectiveDate $effective_dates[$rule_id]

ASSERT US.0178.10147 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

for $presNetwork in $STATEMENT_PRES_NETWORKS.sort
		
		$presConceptItems = $presNetwork.concept-names;

		// Income Statement Processing 
		if $presConceptItems.contains(IncomeStatementAbstract) and $presConceptItems.contains(DividendsPayableAmountPerShare)
            set({covered @DividendsPayableAmountPerShare}).length > 0
		else 
			skip


message
"The company has included the concept DividendsPayableAmountPerShare as a component of the Income Statement using the group {$presNetwork.role.description}.  This is a an instant item and does not reflect the dividends applicable for the reporting period.  Filers should use the element CommonStockDividendsPerShareDeclared with the appropriate reporting period instead of the concept DividendsPayableAmountPerShare. The rule will only report an error if a value has been reported for the element DividendsPayableAmountPerShare.

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$rule_id]



/** This rule looks for dividends expense and dividends payment and if they are different checks that dividends declared per share is reported.  If dividends paid per share is reported this should be changed. **/

ASSERT US.0178.10148 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$dividendsPayments = {nonils @concept = PaymentsOfDividendsCommonStock @unit = *  where $fact < 0}

$dividendsExpense = {nonils @concept = DividendsCommonStockCash @unit = * where $fact < 0}

if $dividendsPayments != $dividendsExpense
    $divPaidElement  = {nonils @concept = CommonStockDividendsPerShareCashPaid @unit=*}
    $divPaidElementUsed = exists($divPaidElement)
    $messageIfDivPaidElementUsed = if $divPaidElementUsed
                                        "The filer has reported a value for CommonStockDividendsPerShareCashPaid of " + $divPaidElement  + ", but has not reported a value for CommonStockDividendsPerShareDeclared. If the cash paid amount and expense are different the value for  CommonStockDividendsPerShareDeclared should also be reported."
                                    else 
                                        ""
    not exists({nonils @concept = CommonStockDividendsPerShareDeclared @unit=*})
else
    skip


message
"The filing has reported a value for PaymentsOfDividendsCommonStock with a value of  {$dividendsPayments} and a value for  DividendsCommonStockCash with a value of {$dividendsExpense}. Given these values are not the same the value of CommonStockDividendsPerShareDeclared should be reported for the period.  If the values were the same the element CommonStockDividendsPerShareCashPaid could be used.  {$messageIfDivPaidElementUsed}

The properties of the PaymentsOfDividendsCommonStock fact are:
Period :{$dividendsPayments.period}
Dimensions : {$dividendsPayments.dimensions.join(', ','=')}
Unit : {$dividendsPayments.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $dividendsPayments
status $status
effectiveDate $effective_dates[$rule_id]

ASSERT US.0178.10149 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$dividendsPayments = {nonils @concept = PaymentsOfDividendsPreferredStockAndPreferenceStock @unit = *  where $fact < 0}

$dividendsExpense = {nonils @concept = DividendsPreferredStockCash @unit = * where $fact < 0}

if $dividendsPayments != $dividendsExpense
    $divPaidElement  = {nonils @concept = PreferredStockDividendsPerShareCashPaid @unit=*}
    $divPaidElementUsed = exists($divPaidElement)
    $messageIfDivPaidElementUsed = if $divPaidElementUsed
                                        "The filer has reported a value for PreferredStockDividendsPerShareDeclared of " + $divPaidElement  + ", but has not reported a value for PreferredStockDividendsPerShareDeclared. If the cash paid amount and expense are different the value for  PreferredStockDividendsPerShareDeclared should also be reported."
                                    else 
                                        ""
    not exists({nonils @concept = PreferredStockDividendsPerShareDeclared @unit=*})
else
    skip


message
"The filing has reported a value for PaymentsOfDividendsPreferredStockAndPreferenceStock with a value of  {$dividendsPayments} and a value for  DividendsPreferredStockCash with a value of {$dividendsExpense}. Given these values are not the same the value of PreferredStockDividendsPerShareDeclared should be reported for the period.  If the values were the same the element PreferredStockDividendsPerShareCashPaid could be used.  {$messageIfDivPaidElementUsed}

The properties of the PaymentsOfDividendsPreferredStockAndPreferenceStock fact are:
Period :{$dividendsPayments.period}
Dimensions : {$dividendsPayments.dimensions.join(', ','=')}
Unit : {$dividendsPayments.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $dividendsPayments
status $status
effectiveDate $effective_dates[$rule_id]

/** -----------------------------_RULE DQC.US.0178.10150 ------------------------------------------------**/
ASSERT US.0178.10150 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$StockWithInconsistentClass = {nonils @concept in list(CommonStockDividendsPerShareDeclared, PreferredStockDividendsPerShareDeclared) @StatementClassOfStockAxis in list(CommonStockMember, PreferredStockMember)}

exists($StockWithInconsistentClass)

message
"The filing includes a  value of {$StockWithInconsistentClass} for  dividends declared using the concept {$StockWithInconsistentClass.concept.name} and the dimension member of {$StockWithInconsistentClass.dimension(StatementClassOfStockAxis).name} on the axis StatementClassOfStockAxis. This member should not be used with this concept as the type of stock declared is already communicated with the concept name, and the dimensions provides no additional disaggregation. 

The properties of this  fact are:
Period :{$StockWithInconsistentClass.period}
Dimensions : {$StockWithInconsistentClass.dimensions.join(', ','=')}
Unit : {$StockWithInconsistentClass.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $StockWithInconsistentClass
status $status
effectiveDate $effective_dates[$rule_id]

/** -----------------------------_RULE DQC.US.0178.10151 ------------------------------------------------**/
ASSERT US.0178.10151 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$RetainedEarningsIsZero = {@concept in list(DividendsCommonStockCash,DividendsPreferredStockCash,DividendsCash, DividendsShareBasedCompensationCash) @StatementEquityComponentsAxis = RetainedEarningsMember where $fact == 0}

$positiveRetainedEarnings = {@concept = RetainedEarningsAccumulatedDeficit where $fact > 0}

$cumDivs = list({@concept in list(CumulativeDividends, AccumulatedDistributionsInExcessOfNetIncome, CumulativeEarningsDeficit) where $fact > 0})

exists($RetainedEarningsIsZero) and (exists($positiveRetainedEarnings) or $cumDivs.length == 0)

message
"The filing includes a  value of 0 for {$RetainedEarningsIsZero.concept.name}, but the value for {$positiveRetainedEarnings.concept.name} is {$positiveRetainedEarnings}.  Any dividends paid should reduce retained earnings if the retained earnings is positive. Either change the element, update the value for retained earnings or correct the balance for retained earnings.  If this is dividends paid out of APIC then use the element AdjustmentsToAdditionalPaidInCapitalDividendsInExcessOfRetainedEarnings.

The properties of this  fact are:
Period :{$RetainedEarningsIsZero.period}
Dimensions : {$RetainedEarningsIsZero.dimensions.join(', ','=')}
Unit : {$RetainedEarningsIsZero.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $RetainedEarningsIsZero
status $status
effectiveDate $effective_dates[$rule_id]


/** -----------------------------_RULE DQC.US.0178.10152 ------------------------------------------------**/
ASSERT US.0178.10152 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$StockDivsIsPositive = {@concept in list (StockIssuedDuringPeriodValueDividendReinvestmentPlan, StockIssuedDuringPeriodValueStockDividend) @StatementEquityComponentsAxis = RetainedEarningsMember where $fact > 0}

exists($StockDivsIsPositive) 
message
"The filing includes a  value of {$StockDivsIsPositive} for {$StockDivsIsPositive.concept.name} using the RetainedEarningsMember. This element is a credit element so, the value must be entered as a negative amount to reduce retained earnings.

The properties of this  fact are:
Period :{$StockDivsIsPositive.period}
Dimensions : {$StockDivsIsPositive.dimensions.join(', ','=')}
Unit : {$StockDivsIsPositive.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $StockDivsIsPositive
status $status
effectiveDate $effective_dates[$rule_id]
/**
TODO 
Pickup missing dividends declared.
**/