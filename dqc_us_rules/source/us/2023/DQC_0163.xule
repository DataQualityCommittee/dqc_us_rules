/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2023

**/

RULE-NAME-PREFIX DQC


Assert US.0163.10085 satisfied
$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

$AdditionalCapitalFact = {@SegmentExpenditureAdditionToLongLivedAssets}

for $x in list(PaymentsToAcquirePropertyPlantAndEquipment, PropertyPlantAndEquipmentAdditions)
    $PPEFacts = {@concept = $x}

    $PPEHypercubeLocation = navigate parent-child ancestors from $x where $relationship.source.name in list(ScheduleOfSegmentReportingInformationBySegmentTable, ScheduleOfRevenuesFromExternalCustomersAndLongLivedAssetsTable) returns set (target-name);

    if exists($PPEFacts) and $PPEHypercubeLocation.length > 0
        $locationCheck = navigate parent-child descendants from list(ScheduleOfSegmentReportingInformationBySegmentTable, ScheduleOfRevenuesFromExternalCustomersAndLongLivedAssetsTable) where $relationship.target-name == SegmentExpenditureAdditionToLongLivedAssets returns set (target-name)
        if $locationCheck.length == 0
            true
        else 
            false
    else
        false

message
"A value of {$PPEFacts} has been reported for the concept {$PPEFacts.concept}. This represents additions to PPE and is used in the segment note as a descendant of a segment reporting table {$PPEHypercubeLocation.join(', ')}.  When reporting additions to long lived assets in the segment disclosure the element SegmentExpenditureAdditionToLongLivedAssets should be used even if the value is the same as the additions to PPE.

Concept : {$PPEFacts.concept}
Value : {$PPEFacts} 
Total period : {$PPEFacts.period} 
Dimensions : {$PPEFacts.dimensions.join(', ','=')}
Decimals : {$PPEFacts.decimals} 

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]