/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024
**/


rule-name-prefix DQC

assert US.0214.10741 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else


/** Identify where Equity disclosures are made in the financial statements but do not appear in the equity section of the balance sheet **/

/** Identify the roles that are the balance sheet **/
$balance_sheet_roles = (filter taxonomy().networks(parent-child) where (($item.concept-names.contains(StatementOfFinancialPositionAbstract) or $item.role.uri.lower-case.contains('balancesheet') or  $item.role.description.lower-case.contains('balance sheet')) and  $item.role.description.contains('- Statement ') and not $item.role.uri.lower-case.contains('parenthetical')) returns $item.role).to-list;

if ($balance_sheet_roles.length == 0)
	skip
else
	/** Sort so we always get the first  statement as this is most likely to be the actual BS**/
	$bs_roles_sorted = (filter $balance_sheet_roles returns list($item.description, $item.uri, $item )).sort;
	
	/** Get the first balance sheet role  and return the role object at 3**/
	$balance_sheet_role = $bs_roles_sorted[1][3]

	/** Get the balance sheet calculation target items**/
	$balance_sheet_calc_target_items = navigate summation-item descendants role $balance_sheet_role returns set (target-name)

	/** Identify if key items are reported in the financial STatements but not in the balance sheet **/

	/** Treasury Items  https://www.sec.gov/ix?doc=/Archives/edgar/data/1874097/000121390024041350/ea0204007-10q_cyngn.htm#fact-identifier-401 **/

	$TreasuryValue = [nonils @concept = TreasuryStockValue where $fact != 0];
	$CommonStock = [nonils @concept = CommonStockValue where $fact != 0];
	$AdditionalPaidInCapital = [nonils @concept = AdditionalPaidInCapital where $fact != 0];
	$RetainedEarnings = [nonils @concept = RetainedEarningsAccumulatedDeficit where $fact != 0];
	$AccumulatedOtherComprehensiveIncomeLoss = [nonils @concept = AccumulatedOtherComprehensiveIncomeLossNetOfTax where $fact != 0];
	$NoncontrollingInterest = [nonils @concept = MinorityInterest where $fact != 0];
	$PreferredStockValue = [nonils @concept = PreferredStockValue where $fact != 0];


	$items_to_check = list($TreasuryValue, $CommonStock, $AdditionalPaidInCapital, $RetainedEarnings, $AccumulatedOtherComprehensiveIncomeLoss, $NoncontrollingInterest, $PreferredStockValue);

	for $item in $items_to_check
		$item_in_balance_sheet = $balance_sheet_calc_target_items.contains($item.name);
		if not $item_in_balance_sheet
			true
		else
			false

message
"A value of {$item} has been reported for the concept {$item.name.local-name} in the financial statements but it does not appear in the calculation relationships comprising equity  of the balance sheet defined in  {$balance_sheet_role.description}. If {$item.name.local-name}  is reported in the financial statements then it should also be reported in the balance sheet.  

The properties of this {$item.name} fact are:

Period: {$item.period}
Unit: {$item.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


severity error  

effectiveDate $effective_dates[$rule_id]