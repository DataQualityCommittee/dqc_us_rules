/**
DQC Rules
 (c) Copyright 2017 - 2024, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP All

WARNING : THIS IS A SINGLE FILE ON CHANGE TO THIS AFFECTS 5 files 2024, 2023, 2022, 2021, 2020
This was created using a hardlink ln {original} {new}
**/

RULE-NAME-PREFIX DQC

ASSERT US.0185.10165 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** REQUESTED BY FASB (See Github repo https://github.com/TaxonomyFASB/Proposed-DQC-Rules/issues/37)**/
$tax_increase_decrease_elements = set(
    UnrecognizedTaxBenefitsDecreasesResultingFromCurrentPeriodTaxPositions,
    UnrecognizedTaxBenefitsDecreasesResultingFromForeignCurrencyTranslation,
    UnrecognizedTaxBenefitsDecreasesResultingFromPriorPeriodTaxPositions,
    UnrecognizedTaxBenefitsDecreasesResultingFromSettlementsWithTaxingAuthorities,
    UnrecognizedTaxBenefitsIncreasesResultingFromCurrentPeriodTaxPositions,
    UnrecognizedTaxBenefitsIncreasesResultingFromForeignCurrencyTranslation,
    UnrecognizedTaxBenefitsIncreasesResultingFromPriorPeriodTaxPositions,
    UnrecognizedTaxBenefitsIncreasesResultingFromSettlementsWithTaxingAuthorities,
    UnrecognizedTaxBenefitsPeriodIncreaseDecrease,
    UnrecognizedTaxBenefitsReductionsResultingFromLapseOfApplicableStatuteOfLimitations
)

for $tax_item in $tax_increase_decrease_elements

        $taxFact = {@concept = $tax_item}
        $ancestors = $taxFact.inline-ancestors
        if $ancestors.length == 0
            skip 
        else
        $ancestorConcepts = filter $ancestors returns $item.concept.name 
        if ScheduleOfUnrecognizedTaxBenefitsRollForwardTableTextBlock in $ancestorConcepts
            false
        else
            true

message
"The concept {$tax_item} with a value of {$taxFact} is reported in the disclosure within the text blocks item {$ancestorConcepts.join(', ')}. The element {$tax_item} should be included in the text block disclosure ScheduleOfUnrecognizedTaxBenefitsRollForwardTableTextBlock instead.

The properties of this {$taxFact} fact are:
Period :{$taxFact.period}
Dimensions : {$taxFact.dimensions.join(', ','=')}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}
"

severity error
rule-focus $taxFact
effectiveDate $effective_dates[$rule_id]