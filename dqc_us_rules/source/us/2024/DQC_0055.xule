/**

DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024

**/

rule-name-prefix DQC

assert US.0055.7493 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else


$listmembers = navigate parent-child descendants from RetirementPlanSponsorLocationAxis returns by network set (target-name)

for ($network in $listmembers.keys)
	$nrole = $network.role.uri;
	if not ($listmembers[$network].contains(DomesticPlanMember) or  $listmembers[$network].contains(ForeignPlanMember))
		if ($listmembers[$network].length > 1)
			true
		else 
			false
	else
		false

message
"Neither the  Domestic Plan [Member] NOR Foreign Plan [Member] has been used on the Retirement Plan Sponsor Location [Axis] in the presentation linkbase  {$nrole}. The FASB requires that either one or both of these members is present on the Retirement Plan Sponsor Location [Axis]. Please review the axis and add one of these members or assess the need to use the Retirement Plan Sponsor Location [Axis].

The axis RetirementPlanSponsorLocationAxis has the following members listed under it:
{$listmembers[$network].join(', 
')}


Rule Element Id:7493
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]


/** 1. Add rule above but use network path rather than network  **/
/** 2. Check that  either DomesticPlanMember or ForeignPlanMember are not leaf items **/
/** 3. Check that only Geography, extension and existing members are permitted  **/

assert US.0055.9842 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else


$networks = navigate parent-child children from RetirementPlanSponsorLocationAxis returns set (role)

for ($network in $networks)
	$paths = navigate parent-child descendants from RetirementPlanSponsorLocationAxis role $network returns paths (target-name)
	for $pathElements in $paths 
		if not ($pathElements.contains(DomesticPlanMember) or  $pathElements.contains(ForeignPlanMember))
			if ($pathElements.length > 1)
				true
			else 
				false
		else
			false

message
"For the path including the elements  {$pathElements.join(', ')} neither the  Domestic Plan [Member] NOR Foreign Plan [Member] has been included in the path of the descendants of the Retirement Plan Sponsor Location [Axis] in the presentation linkbase  {$network.uri}. The FASB requires that either one or both of these members is present on the Retirement Plan Sponsor Location [Axis]. Please review the axis and add one of these members or assess the need to use the Retirement Plan Sponsor Location [Axis].

The axis RetirementPlanSponsorLocationAxis has the following members listed under it for this path:
{$pathElements.join(', 
')}


Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]

assert US.0055.9843 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else


$networks = navigate parent-child children from RetirementPlanSponsorLocationAxis returns set (role)

for ($network in $networks)
	$paths = navigate parent-child descendants from RetirementPlanSponsorLocationAxis role $network returns paths (target-name)
	for $pathElements in $paths 
		if ($pathElements.last == DomesticPlanMember)
			true
		else 
			false
		

message
"The Domestic Plan [Member] has been included as a descendant of the Retirement Plan Sponsor Location [Axis] in the presentation linkbase {$network.uri}. The FASB requires that this member includes children elements representing the Domestic Geography. In this case the Domestic Plan [Member] has no children. Please add a member representing the domestic location or remove the Domestic Plan [Member].

The axis RetirementPlanSponsorLocationAxis has the following members listed under it for this path:
{$pathElements.join(', 
')}


Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]

assert US.0055.9844 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else


$networks = navigate parent-child children from RetirementPlanSponsorLocationAxis returns set (role)

for ($network in $networks)
	$paths = navigate parent-child descendants from RetirementPlanSponsorLocationAxis role $network returns paths (target-name)
	for $pathElements in $paths 
		if ($pathElements.last == ForeignPlanMember)
			list({covered @RetirementPlanSponsorLocationAxis = ForeignPlanMember}).length == 0
		else 
			false
		

message
"The Foreign Plan [Member] has been included as a descendant of the Retirement Plan Sponsor Location [Axis] in the presentation linkbase {$network.uri}. The FASB requires this member includes children elements representing the Foreign Geography if no value is reported for the foreign plan member. In this case the Foreign Plan [Member] has no children. Please add a member representing the foreign location or remove the Foreign Plan [Member].

The axis RetirementPlanSponsorLocationAxis has the following members listed under it for this path:
{$pathElements.join(', 
')}


Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]