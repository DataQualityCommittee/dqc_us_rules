/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024
**/


rule-name-prefix DQC

assert US.0033.2 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else


$exceptions_to_dei = set(qname($DEI_NAMESPACE,'EntityCommonStockSharesOutstanding')
						, qname($DEI_NAMESPACE,'EntityPublicFloat')
						, qname($DEI_NAMESPACE,'DocumentPeriodEndDate')
						, qname($DEI_NAMESPACE,'EntityNumberOfEmployees')
						, qname($DEI_NAMESPACE,'AuditorLocation')
						, qname($DEI_NAMESPACE,'AuditorName')
						, qname($DEI_NAMESPACE,'AuditorFirmId')
						, qname($DEI_NAMESPACE,'EntityListingDepositoryReceiptRatio'));
$document_period = first(list({covered @concept.local-name = 'DocumentPeriodEndDate'}));
$document_period_end_date = $document_period.period.end;

/** The  first part of the expession checks if the document period end date is within three days if not the rule does not execute as rule DQC.US.0036 will have executed**/
not($document_period > ($document_period_end_date + (time-span("P4D"))) or $document_period < ($document_period_end_date - (time-span("P4D"))))
and 
exists({@concept.namespace-uri = $DEI_NAMESPACE @period  @SubsequentEventTypeAxis = none where $fact.concept.name not in $exceptions_to_dei and  $fact.period.end != $document_period_end_date}#fact1)
and 
/** The rule will not run if it is an 8-K that has the amendment flag flagged as true**/
not (any(set(exists({covered @concept.local-name ='DocumentType' where $fact in set('8-K')}))) and (exists({covered @concept.local-name ='AmendmentFlag' where $fact == true})))



message
"The ending date of {$fact1.period.end} used for {$fact1.concept.label.text} must be the same as the Document Period End Date,  {$document_period_end_date}. Revise either the date of the {$fact1.concept.label.text} or the date of the Document Period End Date.

The properties of this {$fact1.concept} fact are:
Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]

rule-focus $fact1

