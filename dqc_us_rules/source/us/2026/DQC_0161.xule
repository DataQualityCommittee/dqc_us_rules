/**
DQC Rules
 (c) Copyright 2017 - 2026, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024

**/

RULE-NAME-PREFIX DQC


ASSERT US.0161.10083 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

$EBPReports = set('11-K','11-K/A','11-KT').contains([covered @concept.local-name ='DocumentType']);

if $EBPReports
    skip
else
    $axisExtensibleEnumDict = dict(
                                    list(RetirementPlanFundingStatusAxis, "DefinedBenefitPlanFundingStatusExtensibleList"),
                                    list(InvestmentTypeAxis, "InvestmentTypeExtensibleEnumeration"),
                                    list(RetirementPlanTaxStatusAxis,"DefinedBenefitPlanTaxStatusExtensibleList or DefinedContributionPlanTaxStatusExtensibleList" )
                                    )

    $all_cubes = filter taxonomy().cubes;

    for $cube in $all_cubes
            for $dim in $axisExtensibleEnumDict.keys
                if $cube.dimensions().concept().name.contains($dim)
                        $domain = navigate dimensions dimension-domain descendants from $dim drs-role $cube.drs-role returns set target-name;
                        if count(navigate dimensions descendants from $domain drs-role $cube.drs-role cube $cube.cube-concept()) == 1
                            $count_values = count(list({@cube = $cube @$dim = *}#PrimaryConcept));
                        
                            $count_values == 1 and (not exists({@cube = $cube @$dim = none}))
                        else
                            skip
                else
                        skip


message
"The filing has  reported a value for the element {$PrimaryConcept.concept} with a value of {$PrimaryConcept} using the axis {$dim} in the role {$cube.drs-role.description} on the table {$cube.cube-concept()}. However, the only member used to disaggregate the total for {$PrimaryConcept.concept} was the member {$PrimaryConcept.dimension($dim)}. If only one member is used on this axis then the filer should either:

1. Retag the filing using the element {$axisExtensibleEnumDict[$dim]} with a value of {$PrimaryConcept.dimension($dim)}.  The member {$PrimaryConcept.dimension($dim)} should be removed from the value as it represents the aggregate value of {$PrimaryConcept.concept}.
 
2. Tag the aggregate value of {$PrimaryConcept.concept} with a value of {$PrimaryConcept} without the {$dim} axis. This rule will not generate an error if the aggregate value of {$PrimaryConcept.concept} is reported.

The properties of this {$PrimaryConcept.concept} fact are:
Period :{$PrimaryConcept.period}
Dimensions : {$PrimaryConcept.dimensions.join(', ','=')}
Unit : {$PrimaryConcept.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}
"

SEVERITY error

effectiveDate $effective_dates[$rule_id]