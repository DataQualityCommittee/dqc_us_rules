/**
DQC Rules
 (c) Copyright 2017 - 2026, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024
**/


rule-name-prefix DQC

assert US.0087.9369 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

/** If operating lease liability amount is reported but is not included in the Financial statement presentation then the extensible list element OperatingLeaseLiabilityStatementOfFinancialPositionExtensibleList must be tagged with a value or the Balance Sheet Location Axis should be used. **/

/** Does the Operating Lease Liability exist and is the report a 10-K **/

if exists([@concept = OperatingLeaseLiability @BalanceSheetLocationAxis @unit=* where $fact != 0]#leaseLiability) and set('10-K','10-K/A','10-KT','20-F','20-F/A','40-F','40-F/A').contains([covered @concept.local-name ='DocumentType'])
	
	/** Identify all calculation networks in the company extension **/

	$all_networks = $PRES_NETWORKS;

	/** eleminate reports with no calculations such as an 8-K with no financial data**/
	if $all_networks == none
			false
	else
	
		/** Get those networks that are the financial Statements **/
		$FS_Networks = filter $all_networks where $item.role.description.contains('- Statement ');

		/** Get all concepts in FS Networks **/
		$FS_Concepts = sum(set(for ($network in $FS_Networks)
								$network.concepts));
								
		$FS_Concepts_qname = filter $FS_Concepts returns $item.name;

		/** Identify if OperatingLeaseLiability is contained in the financial statements**/

		if $FS_Concepts_qname.contains(OperatingLeaseLiability) 
			false
			/** Rule will not run if the company is using current and non current operating leases as these will be in the balance sheet **/
		else
			if ($FS_Concepts_qname.contains(OperatingLeaseLiabilityCurrent) or $FS_Concepts_qname.contains(OperatingLeaseLiabilityNoncurrent))
		 		false
			else
		
				/** Determine if any the extensible list element has been used with a value or if the balance sheet location axis has been used with the operating lease liability **/
				if exists({@OperatingLeaseLiabilityStatementOfFinancialPositionExtensibleList @unit}) or exists({@OperatingLeaseLiabilityCurrentStatementOfFinancialPositionExtensibleList @unit}) or exists({@OperatingLeaseLiabilityNoncurrentStatementOfFinancialPositionExtensibleList @unit}) or exists([@OperatingLeaseLiabilityCurrent @BalanceSheetLocationAxis = * @unit=*]) or exists([@OperatingLeaseLiabilityNoncurrent @BalanceSheetLocationAxis = * @unit=*])
				 	false
				else 
					/** If using the balance sheet location axis the value must be seperated across 2 or more financial statement captions otherwise the element OperatingLeaseLiabilityStatementOfFinancialPositionExtensibleList should have been used. **/
					
					if count(list({@concept = OperatingLeaseLiability @BalanceSheetLocationAxis = * @unit=*})) > 1
						false
					else
						if count(list({@concept = OperatingLeaseLiability @BalanceSheetLocationAxis = * @unit=*})) == 1
							$OLL = list({@concept = OperatingLeaseLiability @unit=*})
							({@concept = OperatingLeaseLiability @BalanceSheetLocationAxis = * @unit=*} == {@concept = OperatingLeaseLiability @unit=*}) or $OLL.length == 0
						else
							true
else
 	false
 


message

"The element {$leaseLiability.concept.name} with a value of {$leaseLiability} has been used in the filing but does not appear in the financial statements of the filing. This disclosure should appear in the face financials unless it is included in another account caption. If this is the case either:
1. The element OperatingLeaseLiabilityStatementOfFinancialPositionExtensibleList should be used to identify the account where the Operating Lease liability is included; or
2. The Balance Sheet location axis should be used with two or more account members to identify into which financial statement captions the Operating Lease liability has been apportioned.
 
Period :{$leaseLiability.period}
Dimensions : {$leaseLiability.dimensions.join(', ','=')}
Unit : {$leaseLiability.unit}
Decimals: {$leaseLiability.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$rule_id]

assert US.0087.9370 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

/** If operating lease liability Current amount is reported but is not included in the Financial statement calculation then the extensible list element OperatingLeaseLiabilityCurrentStatementOfFinancialPositionExtensibleList must be tagged with a value or the Balance Sheet Location Axis should be used. **/

/** Does the Operating Lease Liability exist and is the report a 10-K **/

if exists([@concept = OperatingLeaseLiabilityCurrent @BalanceSheetLocationAxis @unit=* where $fact != 0]#leaseLiability) and  set('10-K','10-K/A','10-KT','20-F','20-F/A','40-F','40-F/A').contains([covered @concept.local-name ='DocumentType']) 
	
	/** Identify all calculation networks in the company extension **/

	$all_networks = $PRES_NETWORKS;

	/** eleminate reports with no calculations such as an 8-K with no financial data**/
	if $all_networks == none
			false
	else
	
		/** Get those networks that are the financial Statements **/
		$FS_Networks = filter $all_networks where $item.role.description.contains('- Statement ');

		/** Get all concepts in FS Networks **/
		$FS_Concepts = sum(set(for ($network in $FS_Networks)
								$network.concepts));
								
		$FS_Concepts_qname = filter $FS_Concepts returns $item.name;

		/** Identify if OperatingLeaseLiability is contained in the financial statements**/

		if $FS_Concepts_qname.contains(OperatingLeaseLiabilityCurrent) 
			false
		else
			/** If the operating Lease Liability is on the BAlanace Sheet then dont flag an error**/
			if ($FS_Concepts_qname.contains(OperatingLeaseLiability))
		 		false
			else
		
			/** Determine if either the extensible list element has been used with a value or if the balance sheet location axis has been used with the operating lease liability **/
			if exists({@OperatingLeaseLiabilityCurrentStatementOfFinancialPositionExtensibleList @unit}) or exists({@OperatingLeaseLiabilityStatementOfFinancialPositionExtensibleList @unit})
			 	false
			else 
				/** If using the balance sheet location axis the value must be seperated across 2 or more financial statement captions otherwise the element OperatingLeaseLiabilityCurrentStatementOfFinancialPositionExtensibleList should have been used. **/ 
				if count(list({@concept = OperatingLeaseLiabilityCurrent @BalanceSheetLocationAxis = * @unit=*})) > 1
					false
				else
					/** Check that the values on the balance sheet location axis are not a subcomponent of the total. If they are a sub component then the rule will not flag an error. **/
					if count(list({@concept = OperatingLeaseLiabilityCurrent @BalanceSheetLocationAxis = * @unit=*})) == 1
						$fact_item = {@concept = OperatingLeaseLiabilityCurrent @BalanceSheetLocationAxis = * @unit=*}
						$specific_error = "The element OperatingLeaseLiabilityCurrent with a value of " + $fact_item.string + " and the Balance Sheet Location Axis with the member " + $fact_item.dimension(BalanceSheetLocationAxis).string + " only appears in the balance sheet line item represented by the member. The Balance Sheet Location Axis should be used when the OperatingLeaseLiabilityCurrent amount is split across multiple Balance Sheet line items. When the amount is not spread across multiple line items then the OperatingLeaseLiabilityCurrentStatementOfFinancialPositionExtensibleList item should be used with a value of the element name of the Balance Sheet line item.";
						({@concept = OperatingLeaseLiabilityCurrent @BalanceSheetLocationAxis = * @unit=*} == {@concept = OperatingLeaseLiabilityCurrent @unit=*}) or list({@concept = OperatingLeaseLiabilityCurrent @unit=*}).length == 0
					else
						$fact_item = [@concept = OperatingLeaseLiabilityCurrent @unit=*]
						$specific_error = "The element OperatingLeaseLiabilityCurrent with a value of " + $fact_item.string + " has been used in the filing but does not appear in the financial statements of the filing. This disclosure should appear in the face financials unless it is included in another account caption. If this is the case either:
1. The element OperatingLeaseLiabilityCurrentStatementOfFinancialPositionExtensibleList should be used to identify the account where the Operating Lease liability Current is included; or
2. The Balance Sheet location axis should be used with two or more account members to identify into which financial statement captions the Operating Lease liability Current has been apportioned.";
						true
else
	false
 
message

"{$specific_error}
 
Period :{$fact_item.period}
Dimensions : {$fact_item.dimensions.join(', ','=')}
Unit : {$fact_item.unit}
Decimals: {$fact_item.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$rule_id]

rule-focus $fact_item 


assert US.0087.9371 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

/** If operating lease liability Non Current amount is reported but is not included in the Financial statement calculation then the extensible list element OperatingLeaseLiabilityNoncurrentStatementOfFinancialPositionExtensibleList must be tagged with a value or the Balance Sheet Location Axis should be used. **/

/** Does the Operating Lease Liability exist and is the report a 10-K **/

if exists([@concept = OperatingLeaseLiabilityNoncurrent @BalanceSheetLocationAxis @unit=* where $fact != 0]#leaseLiability ) and  set('10-K','10-KT','20-F').contains([covered @concept.local-name ='DocumentType']) 
	
	/** Identify all calculation networks in the company extension **/

	$all_networks = $PRES_NETWORKS;

	/** eleminate reports with no calculations such as an 8-K with no financial data**/
	if $all_networks == none
			false
	else
	
		/** Get those networks that are the financial Statements **/
		$FS_Networks = filter $all_networks where $item.role.description.contains('- Statement ');

		/** Get all concepts in FS Networks **/
		$FS_Concepts = sum(set(for ($network in $FS_Networks)
								$network.concepts));
								
		$FS_Concepts_qname = filter $FS_Concepts returns $item.name;

		/** Identify if OperatingLeaseLiability is contained in the financial statements**/

		if $FS_Concepts_qname.contains(OperatingLeaseLiabilityNoncurrent)
			false
		else
			/** If the operating Lease Liability is on the BAlanace Sheet then dont flag an error**/
			if ($FS_Concepts_qname.contains(OperatingLeaseLiability))
		 		false
			else
		
			/** Determine if either the extensible list element has been used with a value or if the balance sheet location axis has been used with the operating lease liability **/
			if exists({@OperatingLeaseLiabilityNoncurrentStatementOfFinancialPositionExtensibleList @unit}) or exists({@OperatingLeaseLiabilityStatementOfFinancialPositionExtensibleList @unit})
			 	false
			else 
				/** If using the balance sheet location axis the value must be seperated across 2 or more financial statement captions otherwise the element OperatingLeaseLiabilityCurrentStatementOfFinancialPositionExtensibleList should have been used. **/ 
				$checkValue = count(list({@concept = OperatingLeaseLiabilityNoncurrent @BalanceSheetLocationAxis = * @unit=*}))

				if count(list({@concept = OperatingLeaseLiabilityNoncurrent @BalanceSheetLocationAxis = * @unit=*})) > 1
					false
				else
					/** Check that the values on the balance sheet location axis are not a subcomponent of the total. If they are a sub component then the rule will not flag an error. **/
					if count(list({@concept = OperatingLeaseLiabilityNoncurrent @BalanceSheetLocationAxis = * @unit=*})) == 1
						$fact_item = {@concept = OperatingLeaseLiabilityNoncurrent @BalanceSheetLocationAxis = * @unit=*}
						$specific_error = "The element OperatingLeaseLiabilityNoncurrent with a value of " + $fact_item.string + " and the Balance Sheet Location Axis with the member " + $fact_item.dimension(BalanceSheetLocationAxis).string + " only appears in the balance sheet line item represented by the member. The Balance Sheet Location Axis should be used when the OperatingLeaseLiabilityNoncurrent amount is split across multiple Balance Sheet line items. When the amount is not spread across multiple line items then the OperatingLeaseLiabilityNoncurrentStatementOfFinancialPositionExtensibleList item should be used with a value of the element name of the Balance Sheet line item.";
						({@concept = OperatingLeaseLiabilityNoncurrent @BalanceSheetLocationAxis = * @unit=*} == {@concept = OperatingLeaseLiabilityNoncurrent @unit=*}) or list({@concept = OperatingLeaseLiabilityNoncurrent @unit=*}).length == 0
					else
						$fact_item = [@concept = OperatingLeaseLiabilityNoncurrent @unit=*]
						$specific_error = "The element OperatingLeaseLiabilityNoncurrent with a value of " + $fact_item.string + " has been used in the filing but does not appear in the financial statements of the filing. This disclosure should appear in the face financials unless it is included in another account caption. If this is the case either:
1. The element OperatingLeaseLiabilityNoncurrentStatementOfFinancialPositionExtensibleList should be used to identify the account where the Operating Lease liability Noncurrent is included; or
2. The Balance Sheet location axis should be used with two or more account members to identify into which financial statement captions the Operating Lease liability Noncurrent has been apportioned.";
						true
else
	false
 
message

"{$specific_error}
 
Period :{$fact_item.period}
Dimensions : {$fact_item.dimensions.join(', ','=')}
Unit : {$fact_item.unit}
Decimals: {$fact_item.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$rule_id]

rule-focus $fact_item 