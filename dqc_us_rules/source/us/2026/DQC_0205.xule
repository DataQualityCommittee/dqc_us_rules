/**
DQC Rules
 (c) Copyright 2017 - 2026, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024
**/


rule-name-prefix DQC


constant $BS_MEMBER_LOOKUP = dict(
							list("OtherNoncurrentAssetsMember", set("OtherAssetsNoncurrent")),
							list("OtherNoncurrentLiabilitiesMember", set("OtherLiabilitiesNoncurrent")),
							list("OtherCurrentLiabilitiesMember", set("OtherLiabilitiesCurrent")),
							list("OtherCurrentAssetsMember", set("OtherAssetsCurrent")),
							list("OtherAssetsMember", set("OtherAssets")),
							list("OtherLiabilitiesMember", set("OtherLiabilities")),
							list("PrepaidExpensesAndOtherCurrentAssetsMember", set("PrepaidExpenseAndOtherAssetsCurrent")),
							list("OtherInvestmentsMember", set("OtherInvestments")),
							list("ContractHolderFundsMember", set("ContractHolderFunds")),
							list("AccountsPayableAndAccruedLiabilitiesMember", set("AccountsPayableAndAccruedLiabilitiesCurrentAndNoncurrent")),
							list("AccountsPayableMember", set("AccountsPayableCurrentAndNoncurrent")), 
							list("CashAndCashEquivalents", set("CashAndCashEquivalentsAtCarryingValue")),
							list("CashMember", set("Cash")),
							list("CashEquivalentsMember", set("CashEquivalentsAtCarryingValue")),
							list("AccountsReceivableMember", set("AccountsReceivableNet", "AccountsReceivableGross", "AccountsReceivableFairValueDisclosure")),
							list("NotesReceivableMember", set("NotesReceivableNet", "NotesReceivableGross")),
							list("PremiumsReceivableMember", set("PremiumsReceivableAtCarryingValue", "PremiumsReceivableGross")),
							list("InvestmentsMember", set("Investments")),
							list("EquityMethodInvestmentsMember", set("EquityMethodInvestments")),
							list("DerivativeFinancialInstrumentsAssetsMember", set("DerivativeAssets")),
							list("LoansReceivableMember", set("LoansReceivableFairValueDisclosure", "NotesAndLoansReceivableNetCurrent", "NotesAndLoansReceivableGrossCurrent", "NotesAndLoansReceivableNetNoncurrent", "NotesAndLoansReceivableGrossNoncurrent")),
							list("CashSurrenderValueMember", set("CashSurrenderValueOfLifeInsurance")),
							list("RetainedInterestMember", set("RetainedInterestFairValueDisclosure")),
							list("RetainedInvestmentInSubsidiaryMember", set("InvestmentsInAffiliatesSubsidiariesAssociatesAndJointVenturesFairValueDisclosure")),
							list("RetainedInvestmentInBusinessMember", set("InvestmentsInAffiliatesSubsidiariesAssociatesAndJointVenturesFairValueDisclosure")),
							list("InvestmentInFederalHomeLoanBankStockMember", set("FederalHomeLoanBankStock")),
							list("FederalFundsSoldAndSecuritiesBorrowedOrPurchasedUnderAgreementsToResellMember", set("FederalFundsSoldAndSecuritiesPurchasedUnderAgreementsToResell"))	
)


ASSERT US.0205.10720 SATISFIED

/** This rule identifies if an extended link role has a valid value. **/

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

/** Identify all presentation networks in the company extension **/

	$allNetworks = $PRES_NETWORKS;

	/** eliminate reports with no presentations containing a statement **/

	$fsNetworks = filter $allNetworks where $item.role.description.contains('- Statement ');
	
	if $fsNetworks.length == 0
			skip
	else
		/** Get all concepts in FS Networks **/
		$fsConcepts = sum(set(for ($network in $fsNetworks)
								$network.concepts));

		/** Get those concepts that are monetary items **/					
		$fsConceptsLocal-name = (filter $fsConcepts where $item.is-monetary returns $item.name.local-name).to-set


		/** Get Balance SHeet Location Axis Members **/

		$facts_using_BS_location_axis = list({covered nonils @BalanceSheetLocationAxis = *})
		$bs_location_members = (filter $facts_using_BS_location_axis returns $item.dimension(BalanceSheetLocationAxis).name.local-name).to-set
		for $member in $bs_location_members
			$calc_local-name = if $member in $BS_MEMBER_LOOKUP.keys
									$BS_MEMBER_LOOKUP[$member]
								else
									$start_of_member = last-index-of($member,'Member') - 1
									set(substring($member, 1, $start_of_member))
			if ($fsConceptsLocal-name.intersect($calc_local-name)).length > 0
				false
			else
				true

	message
	"The filer has used the BalanceSheetLocationAxis member {$member} in the filing. This member is used to identify the location of the line item in the balance sheet. However, the concept {sum($calc_local-name)} does not appear in the face financial statements. Either update the value of the BalanceSheetLocationAxis member {sum($calc_local-name)} with a Member that has the same name as the line item with the suffix 'Member' or add the line item {sum($calc_local-name)} to the presentation and calculation linkbase in the balance sheet.

	Rule Element Id:{$rule_id}
	Rule version: {$ruleVersion}"

	severity error  
status $status  

	effectiveDate $effective_dates[$rule_id]

