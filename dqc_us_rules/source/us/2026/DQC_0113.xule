/**
DQC Rules
 (c) Copyright 2017 - 2026, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024
Release Version 18
**/

RULE-NAME-PREFIX DQC

/** 
**This rule checks where the gross payment for the acquisition is less than the amount net of cash acquired
**The filing https://www.sec.gov/ix?doc=/Archives/edgar/data/47518/000004751820000129/hrc-20200930.htm has this incorrect 
**/

ASSERT US.0113.9562 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

$grossPaid = {nonils @PaymentsToAcquireBusinessesGross};
$netPaid = {nonils @PaymentsToAcquireBusinessesNetOfCashAcquired};

$fact_values = list($grossPaid , $netPaid);

$minDecimals = min(list(for $x in $fact_values
                if $x == none 
                    skip
                else
                    $x.decimals));

tolerance_for_decimals_greater_than($netPaid, $grossPaid, $minDecimals, 0)

message
"
The company has reported a value of  {$grossPaid} for the concept {$grossPaid.concept.name} which is less than the value of {$netPaid.concept.name} with a value of {$netPaid}. It is expected that the value of {$grossPaid.concept.name} will always be greater than or equal to {$netPaid.concept.name} 

The properties of this {$grossPaid.concept.name} fact are:
Period :{$grossPaid.period}
Dimensions : {$grossPaid.dimensions.join(', ','=')}
Unit : {$grossPaid.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$rule_id]

rule-focus $grossPaid
