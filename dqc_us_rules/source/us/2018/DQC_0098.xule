/**
DQC Rules
 (c) Copyright 2017 - 2020, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2019
**/


rule-name-prefix DQC

assert US.0098.9531 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$value_recognized = {@concept = ContractWithCustomerLiabilityRevenueRecognized};
$openingDate = $value_recognized.period.start;
$openingBalanceOfValueRecognized = {@concept = ContractWithCustomerLiability @period = $openingDate};

$value_recognized > $openingBalanceOfValueRecognized

message
"The element ContractWithCustomerLiabilityRevenueRecognized with a value of {$value_recognized} is greater than the opening balance of ContractWithCustomerLiability with an opening balance of {$openingBalanceOfValueRecognized} on {$openingDate}. The element ContractWithCustomerLiabilityRevenueRecognized represents the amount from the opening balance that was recognized during the period. For this reason it cannot be greater than the opening balance.

The properties of this {$value_recognized.concept.name} fact are:
Period :{$value_recognized.period}
Dimensions : {$value_recognized.dimensions.join(', ','=')}
Unit : {$value_recognized.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]


assert US.0098.9532 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$value_recognized = {@concept = ContractWithCustomerLiabilityRevenueRecognized};
$openingDate = $value_recognized.period.start;
$openingBalanceOfCurrentValueRecognized = {@concept = ContractWithCustomerLiabilityCurrent @period = $openingDate};
$openingBalanceOfNonCurrentValueRecognized = {@concept = ContractWithCustomerLiabilityNoncurrent @period = $openingDate};
$openingBalanceOfValueRecognized = $openingBalanceOfNonCurrentValueRecognized + $openingBalanceOfCurrentValueRecognized

$value_recognized > $openingBalanceOfValueRecognized

message
"The element ContractWithCustomerLiabilityRevenueRecognized with a value of {$value_recognized} is greater than the opening balances of ContractWithCustomerLiabilityCurrent and ContractWithCustomerLiabilityNoncurrent combined which have an aggregate opening balance of {$openingBalanceOfValueRecognized} on {$openingDate}. The element ContractWithCustomerLiabilityRevenueRecognized represents the amount from the opening balance that was recognized during the period. For this reason it cannot be greater than the opening balance.

The properties of this {$value_recognized.concept.name} fact are:
Period :{$value_recognized.period}
Dimensions : {$value_recognized.dimensions.join(', ','=')}
Unit : {$value_recognized.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]