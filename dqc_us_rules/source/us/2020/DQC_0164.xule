/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2020

**/

RULE-NAME-PREFIX DQC


Assert US.0164.10086 satisfied
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

/** Balance SHeet location and others should not be in any statement cubes role **/

/**Sometimes companies have a section at the bottom of the income statement that gives an alternative representation of the allocation of expense
In these cases companies include the supplemental section showing where those amounts appear in the financials. This means that the location axis gets added
to the financial statement.  The excluded concepts set below lists those elements that can have the location axis on the financials. These facts will be excluded
from the execution of the rule. **/

$excluded_concepts = set(AllocatedShareBasedCompensationExpense)

for $cube in $STATEMENT_CUBES  // ANALYZE EACH OF THE STATEMENT CUBES
    $dimensionsSet = filter $cube.dimensions returns $item.concept.name //GET THE DIMENSIONS ON THE CUBE
    for $abstract in set(BalanceSheetLocationAxis, IncomeStatementLocationAxis, OtherComprehensiveIncomeLocationAxis) //LOOP THROUGH EACH LOCATION AXIS AND COMPARE TO THE CUBES DIMS
        if $dimensionsSet.contains($abstract) //IF THE CUBE DOES NOT HAVE A LOCATION AXIS EXIT THE RULE ELSE CONTINUE
            $facts_with_excluded_concept = list({covered @cube=$cube @$abstract = * where $fact.concept.name in $excluded_concepts}) //GET FACTS WITH ELEMENTS THAT ARE OK TO USE WITH AXIS
            $facts_with_bad_axis = list({covered @cube=$cube @$abstract = * where $fact.concept.name not in $excluded_concepts}) // GET FACTS THAT ARE NOT OK TO USE
            if $facts_with_bad_axis.length == 0 and $facts_with_excluded_concept.length == 0 //REPORT ERROR IF NO FACTS ARE PRESENT
                $concepts_with_facts = "";
                true
            else 
                $concepts_with_facts = "The following concepts were used with the axis" + $abstract.local-name + " : " + ((filter $facts_with_bad_axis returns $item.name.local-name).to-set).join(', ') + "."
                $facts_with_bad_axis.length > 0 //REPORT AN ERROR IF BAD FACTS ARE USED IN THE FILING.
        else
            false

message
"The filer has used the axis {$abstract} in the financial statement presentation cube {$cube.drs-role.description}. The axis {$abstract}  should not be used in a financial statement.
{$concepts_with_facts}
Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$ruleId]


Assert US.0164.10087 satisfied
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

/** Balance SHeet location axis has bad elements **/

$HighLevelElements = set(Assets, Liabilities, AssetsCurrent, AssetsNoncurrent, LiabilitiesCurrent, LiabilitiesNoncurrent, LiabilitiesAndStockholdersEquity, StockholdersEquity, StockholdersEquityIncludingPortionAttributableToNoncontrollingInterest)

exists({@concept in $HighLevelElements @BalanceSheetLocationAxis = *}#fact2)
message
"A value of {$fact2} is reported for the concept {$fact2.concept.name} with the BalanceSheetLocationAxis. This concept is an aggregation concept and should not be disaggregated using the BalanceSheetLocationAxis and the member {$fact2.dimension(BalanceSheetLocationAxis)}. Use a more specific line item from the fact.

The properties of this {$fact2.concept.name} fact are:

Period :{$fact2.period}
Dimensions : {$fact2.dimensions.join(', ','=')}
Unit : {$fact2.unit}

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$ruleId]


Assert US.0164.10088 satisfied
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

/** Balance SHeet location axis has bad elements **/

$HighLevelElements = set(ProfitLoss, NetIncomeLoss, IncomeLossFromContinuingOperationsIncludingPortionAttributableToNoncontrollingInterest, IncomeLossFromContinuingOperationsBeforeIncomeTaxesExtraordinaryItemsNoncontrollingInterest, IncomeLossFromContinuingOperationsBeforeIncomeTaxesMinorityInterestAndIncomeLossFromEquityMethodInvestments, OperatingIncomeLoss, GrossProfit, OperatingExpenses, Revenues, CostOfRevenue, OperatingCostsAndExpenses, OtherOperatingIncomeExpenseNet, NonoperatingIncomeExpense, OtherNonoperatingIncomeExpense, IncomeTaxExpenseBenefit)

exists({@concept in $HighLevelElements @IncomeStatementLocationAxis = *}#fact2)
message
"A value of {$fact2} is reported for the concept {$fact2.concept.name} with the IncomeStatementLocationAxis. This concept is an aggregation concept and should not be disaggregated using the IncomeStatementLocationAxis and the member {$fact2.dimension(IncomeStatementLocationAxis)}. Use a more specific line item to report the fact value or remove the IncomeStatementLocationAxis.

The properties of this {$fact2.concept.name} fact are:

Period :{$fact2.period}
Dimensions : {$fact2.dimensions.join(', ','=')}
Unit : {$fact2.unit}

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$ruleId]


/**REMOVED RULES BASED ON FILER FEEDBACK Sometimes this is a valid case See Shelly Wavrin email**/

/**
Assert US.0164.10089 satisfied
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];


$assetsElements = navigate parent-child descendants from FairValueAssetsMeasuredOnRecurringBasisUnobservableInputReconciliationLineItems returns target 


exists({@concept in $assetsElements @BalanceSheetLocationAxis = *}#fact2)
message
"A value of {$fact2} is reported for the concept {$fact2.concept.name} with the BalanceSheetLocationAxis. This concept should not be disaggregated using the BalanceSheetLocationAxis. Instead the FairValueByLiabilityAssetClassAxis should be used with the member  {$fact2.dimension(BalanceSheetLocationAxis)}. 

The properties of this {$fact2.concept.name} fact are:

Period :{$fact2.period}
Dimensions : {$fact2.dimensions.join(', ','=')}
Unit : {$fact2.unit}

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$ruleId]


Assert US.0164.10090 satisfied
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];


$assetsElements = navigate parent-child descendants from FairValueLiabilitiesMeasuredOnRecurringBasisUnobservableInputReconciliationLineItems returns target 


exists({@concept in $assetsElements @BalanceSheetLocationAxis = *}#fact2)
message
"A value of {$fact2} is reported for the concept {$fact2.concept.name} with the BalanceSheetLocationAxis. This concept should not be disaggregated using the BalanceSheetLocationAxis. Instead the FairValueByLiabilityClassAxis should be used with the member  {$fact2.dimension(BalanceSheetLocationAxis)}.

The properties of this {$fact2.concept.name} fact are:

Period :{$fact2.period}
Dimensions : {$fact2.dimensions.join(', ','=')}
Unit : {$fact2.unit}

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$ruleId]

**/