/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2020
Release Version 18
**/

RULE-NAME-PREFIX DQC

ASSERT US.0119.9576 SATISFIED

$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

exists([@concept=IncomeLossFromContinuingOperationsBeforeIncomeTaxesMinorityInterestAndIncomeLossFromEquityMethodInvestments]#beforeTax) and 
count(list({covered @concept in list(IncomeLossFromEquityMethodInvestments, IncomeLossFromEquityMethodInvestmentsNetOfDividendsOrDistributions, IncomeLossFromAffordableHousingProjectsEquityMethodInvestments)})) == 0

message
"The company has reported a value for the concept representing Income before income tax, Non Controlling Interest and Equity Method Investments with a value of {$beforeTax}. However the company has not reported a value for equity method investments. If the company has no equity method investments then the filer should use the element IncomeLossFromContinuingOperationsBeforeIncomeTaxesExtraordinaryItemsNoncontrollingInterest.

Element : {$beforeTax.concept.name}
Value : {$beforeTax} 
Period : {$beforeTax.period} 
Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$ruleId]

rule-focus $beforeTax


ASSERT US.0119.9577 SATISFIED

$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

/** Equity method investments is excluded from rule 0081, so this rule addresses this **/

$componentsOfIncome = navigate summation-item descendants from  IncomeLossFromContinuingOperationsBeforeIncomeTaxesMinorityInterestAndIncomeLossFromEquityMethodInvestments returns set (target-name);

if $componentsOfIncome.contains(IncomeLossFromEquityMethodInvestments)
	$equityIncomeElement = IncomeLossFromEquityMethodInvestments;
	true
else
	if $componentsOfIncome.contains(IncomeLossFromEquityMethodInvestmentsNetOfDividendsOrDistributions)
		$equityIncomeElement = IncomeLossFromEquityMethodInvestmentsNetOfDividendsOrDistributions;
		true
	else
		false

message
"The company has included {$equityIncomeElement} as a component of the concept \"Income Loss From Continuing Operations Before Income Taxes Minority Interest And Income Loss From Equity Method Investments\". The element {$equityIncomeElement} cannot be included as a component of \"Income Loss From Continuing Operations Before Income Taxes Minority Interest And Income Loss From Equity Method Investments\" as this element excludes equity method investments.  The filer should use the concept IncomeLossFromContinuingOperationsBeforeIncomeTaxesExtraordinaryItemsNoncontrollingInterest instead.

Element : {$equityIncomeElement}
Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$ruleId]

rule-focus taxonomy().concept($equityIncomeElement)




