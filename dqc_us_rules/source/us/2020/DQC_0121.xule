/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2020
**/

RULE-NAME-PREFIX DQC


assert US.0121.9580 satisfied

$ruleId = (rule-name().split('.'))[rule-name().split('.').length];
$document_period = first(list({covered @concept.local-name = 'DocumentType'}));
$document_period_end = $document_period.period.end;

$transitionElements = navigate parent-child descendants from set(Asu201601TransitionAbstract, Asu201618TransitionAbstract)taxonomy $us-gaap where not $relationship.target.is-abstract returns (target-name);

for $transitionElement in $transitionElements

	$fact1 = if set('S-1','S-1/A').contains([covered @concept.local-name ='DocumentType'])
				/** Private companies do not have to adopt ASU's until after 2021-12-15 **/
				{@concept = $transitionElement where ($fact.period.start > date('2021-12-15') and $fact.concept.period-type == duration) or ($fact.period.start > date('2022-12-15') and $fact.concept.period-type == instant) and ($fact.period.end == $document_period_end)}
			else 
				{@concept = $transitionElement where $fact.period.end >= date('2020-01-01') and ($fact.period.end == $document_period_end)}
	
	
	$factIsTextBlock = $fact1.is-type(nonnum:textBlockItemType);
	
	if exists($fact1)
		true
	else
		skip

message

"The element {$fact1.concept.name} with a value of {if $factIsTextBlock $fact1.string.substring(1,200) + '....' else $fact1} has been used in the filing.  This element is a transition element and is intended to be used in periods before adoption of amendments from Accounting Standards Update 2016-01 or Accounting Standards Update 2016-18. The element should no longer be used for filings with fiscal years beginning after December 15th 2017 for ASU 2016-01 - Financial Instruments and December 15th 2018 for ASU 2016-18 - Restricted Cash. 

Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}
Decimals: {$fact1.decimals}

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$ruleId]
rule-focus $fact1 

assert US.0121.9581 satisfied

$ruleId = (rule-name().split('.'))[rule-name().split('.').length];
if (any(set(exists({covered @concept.local-name ='DocumentType' where $fact in set('8-K','6-K')}))))
	skip
else
	$document_period = first(list({covered @concept.local-name = 'DocumentType'}));
	$document_period_end = $document_period.period.end;

	$EGCFlag = if list([covered @concept.local-name ='EntityEmergingGrowthCompany']).length > 0
					first(list([covered @concept.local-name ='EntityEmergingGrowthCompany']))
				else 
					false

	/** Added for situation when an EGC goes non EGC.  Prior elements included in presentation group with text priortoadoptionoftopic842 will be excluded from the rule. **/

	$presDisclosureNetworks = FILTER taxonomy().networks(parent-child) where $item.role.description.contains('- Disclosure -') 
	and ($item.role.uri.lower-case.contains('priortoadoptionoftopic842'))

	$priorAdoptionItems = sum(set(for $presNetwork in $presDisclosureNetworks 
								$presNetwork.concept-names
								)
							)
					
	if (any(set(exists({covered @concept.local-name ='DocumentType' where $fact in $report_type_exclusions})))) OR $EGCFlag
		skip
	else
		$transitionElements = navigate parent-child descendants from set(ASU201602TransitionAbstract, Asu201712TransitionAbstract, ASU201807TransitionAbstract)taxonomy $us-gaap where not $relationship.target.is-abstract returns set (target-name);

		$exceptionTransitionElements = set(LongTermDebtAndCapitalLeaseObligationsMaturitiesRepaymentsOfPrincipalRemainderOfFiscalYear
											,LongTermDebtAndCapitalLeaseObligationsRepaymentsOfPrincipalInNextTwelveMonths
											,LongTermDebtAndCapitalLeaseObligationsMaturitiesRepaymentsOfPrincipalInYearTwo
											,LongTermDebtAndCapitalLeaseObligationsMaturitiesRepaymentsOfPrincipalInYearThree
											,LongTermDebtAndCapitalLeaseObligationsMaturitiesRepaymentsOfPrincipalInYearsTwoAndThree
											,LongTermDebtAndCapitalLeaseObligationsMaturitiesRepaymentsOfPrincipalInYearFour
											,LongTermDebtAndCapitalLeaseObligationsMaturitiesRepaymentsOfPrincipalInYearFive
											,LongtermDebtAndCapitalLeaseObligationsMaturitiesRepaymentsOfPrincipalInYearsFourAndFive
											,LongTermDebtAndCapitalLeaseObligationsMaturitiesRepaymentsOfPrincipalAfterYearFive) + $priorAdoptionItems

		for $transitionElement in ($transitionElements - $exceptionTransitionElements)

			$fact1 = if set('S-1','S-1/A').contains([covered @concept.local-name ='DocumentType'])
					/** Private companies do not have to adopt ASU's until after 2021-12-15 **/
					{@concept = $transitionElement where ($fact.period.start > date('2021-12-15') and $fact.concept.period-type == duration) or ($fact.period.start > date('2022-12-15') and $fact.concept.period-type == instant) and ($fact.period.end == $document_period_end)}
				else 
					{@concept = $transitionElement where $fact.period.end >= date('2020-01-01') and ($fact.period.end == $document_period_end)}

			$factIsTextBlock = $fact1.is-type(nonnum:textBlockItemType);
			
			if exists($fact1)
				true
			else
				skip

message

"The element {$fact1.concept.name} with a value of {if $factIsTextBlock $fact1.string.substring(1,200) + '....' else $fact1} has been used in the filing.  This element is a transition element and is intended to be used in periods before adoption of amendments from Accounting Standards Update 2016-02,  Accounting Standards Update 2017-12, or Accounting Standards Update 2018-07. The element should no longer be used for filings with fiscal years beginning after:

December 15, 2018 for ASU 2016-02 - Leases
December 15, 2018 for ASU 2017-12 - Derivatives and Hedging
December 15, 2019 for ASU 2018-07 - Compensation - Stock Compensation  

Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}
Decimals: {$fact1.decimals}

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$ruleId]
rule-focus $fact1 