/**
DQC Rules
 (c) Copyright 2017 - 2021, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2020
Release Version 16
**/

RULE-NAME-PREFIX DQC


assert US.0122.9582 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** Check Components of Shareholders Equity is not used on the Balance Sheet **/

$balance_sheet_roles = (filter taxonomy().networks(parent-child) where (($item.concept-names.contains(StatementOfFinancialPositionAbstract) or $item.role.uri.lower-case.contains('balancesheet') or $item.role.uri.lower-case.contains('financialposition')) and  $item.role.description.contains('- Statement ') and not $item.role.uri.lower-case.contains('parenthetical') )returns $item.role).to-list

if $balance_sheet_roles.length == 0
	skip
else
	/** Sort so we always get the first  statement **/
	$bs_roles_sorted = (filter $balance_sheet_roles returns list($item.description, $item.uri, $item )).sort

	$bs_role = $bs_roles_sorted[1][3]

	$bs_role_uri = $bs_roles_sorted[1][2]

	$bsRoleDesc = $bs_roles_sorted[1][1]

	$BSCubes = FILTER taxonomy().cubes where $item.drs-role.uri == $bs_role_uri

	for $cube in $BSCubes.sort
		for $dimension in $cube.dimensions
			$dimConcept = $dimension.concept
			$dim = $dimension.concept.name;
			if $dim.local-name in list('StatementEquityComponentsAxis', 'PartnerCapitalComponentsAxis')
				$cubeFacts1 = set({covered @cube.drs-role = $cube.drs-role @StatementEquityComponentsAxis = * })
				$cubeFacts2 = set({covered @cube.drs-role = $cube.drs-role @PartnerCapitalComponentsAxis = * })
				$cubeFacts = $cubeFacts1  + $cubeFacts2
				$cubeConcepts = (filter $cubeFacts returns $item.concept.name).to-set.to-list.sort
				$countOfFacts = count($cubeFacts)
				true
			else
				false

	message

	"The dimension {$dim.local-name} has been defined as part of the Balance Sheet (Statement of Financial Position) structure in the companies filing. The dimension is included in the financial statement  {$bsRoleDesc}.  The inclusion of the dimension {$dim.local-name} in the Balance Sheet structure implies that these values comprise the numbers reported on the Balance Sheet.  This will cause facts that are reported on the Statement of Shareholders Equity or Capital to also appear on the Balance Sheet. (These can also be seen on the SEC renderer view of the Balance Sheet). There are {$countOfFacts} facts reported in the filing using the axis {$dim.local-name} that end up being included in the Balance Sheet using the line items:
		{$cubeConcepts.join(',
		')}

	To correct the issue the dimension {$dim.local-name} should be removed from the the financial statement  {$bsRoleDesc} and the values using the dimension {$dim.local-name} on the Balance Sheet should either have the dimension removed and potentially be updated with a specific line item that reflects the nature of the member that was removed.

	Rule Element Id:{$rule_id}
	Rule version: {$ruleVersion}"

	SEVERITY error
	
	effectiveDate $effective_dates[$rule_id]
	rule-focus $dimConcept