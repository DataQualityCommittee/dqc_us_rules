/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2021
**/


rule-name-prefix DQC


assert US.0145.9867 satisfied

$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

$NonFinancing = set(NetCashProvidedByUsedInInvestingActivitiesAbstract, NetCashProvidedByUsedInOperatingActivitiesAbstract, EffectOfExchangeRateOnCashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsIncludingDisposalGroupAndDiscontinuedOperationsAbstract);

$allowableFinancingItems = navigate parent-child descendants from NetCashProvidedByUsedInFinancingActivitiesAbstract taxonomy $us-gaap  returns set (target-name) + set(ProceedsFromAdvancesForConstruction, RepaymentsOfAdvancesForConstruction, ProceedsFromContributionInAidOfConstruction)

$taxonomyItemsNotInExtensions = navigate parent-child descendants from $NonFinancing taxonomy $us-gaap   returns set (target-name) - $allowableFinancingItems

$extensionNames = navigate parent-child descendants from NetCashProvidedByUsedInFinancingActivitiesAbstract where $relationship.target.name in $taxonomyItemsNotInExtensions returns set (target-name)

for $invalidConcept in $extensionNames
	true

message
"The company has reported the concept {$invalidConcept} as a descendant of the presentation concept NetCashProvidedByUsedInFinancingActivitiesAbstract.  This concept is not a component of financing activities and should be presented outside of this group in the presentation linkbase.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$ruleId]

rule-focus taxonomy().concept($invalidConcept)

//-----------DQC.US.0145.9868 ---------------//
assert US.0145.9868 satisfied

$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

$NonInvesting = set(NetCashProvidedByUsedInFinancingActivitiesAbstract, NetCashProvidedByUsedInOperatingActivitiesAbstract, EffectOfExchangeRateOnCashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsIncludingDisposalGroupAndDiscontinuedOperationsAbstract);

$taxonomyItemsNotInExtensions = (navigate parent-child descendants from $NonInvesting taxonomy $us-gaap  returns set (target-name)) - set(PaymentsForProceedsFromOtherDeposits, IncreaseDecreaseOfRestrictedInvestments, ProceedsFromPaymentsForInSecuritiesSoldUnderAgreementsToRepurchase) 

$extensionNames = navigate parent-child descendants from NetCashProvidedByUsedInInvestingActivitiesAbstract where $relationship.target.name in $taxonomyItemsNotInExtensions returns set (target-name)

for $invalidConcept in $extensionNames
	true

message
"The company has reported the concept {$invalidConcept} as a descendant of the presentation concept NetCashProvidedByUsedInInvestingActivitiesAbstract.  This concept is not a component of investing activities and should be presented outside of this group in the presentation linkbase.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$ruleId]

rule-focus taxonomy().concept($invalidConcept)

//-----------DQC.US.0145.9869 ---------------//

assert US.0145.9869  satisfied
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

$taxonomyChangeInOperatingCapital = navigate parent-child descendants from IncreaseDecreaseInOperatingCapitalAbstract taxonomy $us-gaap returns set (target-name) 

$filinComponentsOfNetIncomeLoss = navigate parent-child descendants from list(IncomeStatementAbstract, StatementOfIncomeAndComprehensiveIncomeAbstract) where $relationship.target.is-monetary == true returns set (target-name)

$misplacedConcepts = $filinComponentsOfNetIncomeLoss.intersect($taxonomyChangeInOperatingCapital)

for $invalidConcept in $misplacedConcepts
	true

message
"The company has reported the concept {$invalidConcept} as a child of the presentation concept IncomeStatementAbstract or StatementOfIncomeAndComprehensiveIncomeAbstract.  This concept is  a component of the cash flow statement and should not be presented within the income statement.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$ruleId]

rule-focus taxonomy().concept($invalidConcept)