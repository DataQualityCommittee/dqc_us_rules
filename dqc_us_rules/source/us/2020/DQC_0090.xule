/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2020
**/


rule-name-prefix DQC

assert US.0090.9374 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** Rule only applies to an annual filing **/

if set('10-K','10-KT','20-F').contains([covered @concept.local-name ='DocumentType']) 

/** Identifies that the concept LesseeOperatingLeaseLiabilityPaymentsDue is the sum of components OperatingLeaseLiability and LesseeOperatingLeaseLiabilityUndiscountedExcessAmount if either one is used. **/

	$ReconciliationOptIn = length(list({covered @concept = qname($extension_ns,'LesseeOperatingLeaseLiabilityUndiscountedExcessAmountNotDisclosed')})) == 0

	$sum1 = [@LesseeOperatingLeaseLiabilityPaymentsDue];

	$addend = [@OperatingLeaseLiability] <+ [@LesseeOperatingLeaseLiabilityUndiscountedExcessAmount];

	$Decimals = list([nonils @LesseeOperatingLeaseLiabilityPaymentsDue], [nonils @OperatingLeaseLiability], [nonils @LesseeOperatingLeaseLiabilityUndiscountedExcessAmount] )

	$minDecimals = min(list(for $x in $Decimals
				if $x == none or $x.decimals == none
					skip
				else
					$x.decimals));

	if tolerance_for_decimals_comp($sum1, $addend, $minDecimals, 2) and $ReconciliationOptIn
		$paymentsDueCalc =  navigate summation-item descendants from LesseeOperatingLeaseLiabilityPaymentsDue returns set (target-name);
		$extInCalc = $paymentsDueCalc intersect $extension_qname;
		$liabilityInCalcBool = OperatingLeaseLiability in $paymentsDueCalc;
		if $extInCalc.length > 0 and $liabilityInCalcBool
			false
		else
			true
	else
		false
else
	false

message

"The element {$sum1.concept.name} with a value of {$sum1} should be equal to the sum of OperatingLeaseLiability plus LesseeOperatingLeaseLiabilityUndiscountedExcessAmount. The value of these adds to {$addend}.  A difference of {$sum1 - $addend}. In an annual filing this reconciliation is required and all elements should have a value in the current reporting period.

If the difference is due to an extension reconciliation item, then the filer should add a calculation relationship. The calculation tree should show the parent element as LesseeOperatingLeaseLiabilityPaymentsDue with children of LesseeOperatingLeaseLiabilityUndiscountedExcessAmount + OperatingLeaseLiability + any extension items. There should already be a calculation tree defined for the components of LesseeOperatingLeaseLiabilityPaymentsDue comprised of LesseeOperatingLeaseLiabilityPaymentsDueNextTwelveMonths +  LesseeOperatingLeaseLiabilityPaymentsDueYearTwo etc.  These two beakdowns of LesseeOperatingLeaseLiabilityPaymentsDue should be in seperate calculation groups. If an extension item appears as a child calculation of LesseeOperatingLeaseLiabilityPaymentsDue then no error is produced.
 
Period :{$sum1.period}
Dimensions : {$sum1.dimensions.join(', ','=')}
Unit : {$sum1.unit}
Decimals: {$sum1.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$rule_id]


assert US.0090.9375 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** Rule only applies to an annual filing **/

if set('10-K','10-KT','20-F').contains([covered @concept.local-name ='DocumentType']) 
	
/** Identifies that the concept FinanceLeaseLiabilityPaymentsDue is the sum of components FinanceLeaseLiability and FinanceLeaseLiabilityUndiscountedExcessAmount if either one is used **/
/** Rule only works at the default level **/

	$ReconciliationOptIn = length(list({covered @concept = qname($extension_ns,'FinanceLeaseLiabilityUndiscountedExcessAmountNotDisclosed')})) == 0

	$sum1 = [@FinanceLeaseLiabilityPaymentsDue];

	$addend = [@FinanceLeaseLiability] <+ [@FinanceLeaseLiabilityUndiscountedExcessAmount];

	$Decimals = list([nonils @FinanceLeaseLiabilityPaymentsDue], [nonils @FinanceLeaseLiability], [nonils @FinanceLeaseLiabilityUndiscountedExcessAmount] )

	$minDecimals = min(list(for $x in $Decimals
				if $x == none or $x.decimals == none
					skip
				else
					$x.decimals));

	if tolerance_for_decimals_comp($sum1, $addend, $minDecimals, 2) and $ReconciliationOptIn
		$paymentsDueCalc =  navigate summation-item descendants from FinanceLeaseLiabilityPaymentsDue returns set (target-name);
		$extInCalc = $paymentsDueCalc intersect $extension_qname
		if $extInCalc.length == 0
			true
		else
			false
	else
		false
else
	false
message

"The element {$sum1.concept.name} with a value of {$sum1} should be equal to the sum of FinanceLeaseLiability plus FinanceLeaseLiabilityUndiscountedExcessAmount. The value of these adds to {$addend}.  A difference of {$sum1 - $addend}. In an annual filing this reconciliation is required and all elements should have a value in the current reporting period.

If the difference is due to an extension reconciliation item, then the filer should add a calculation relationship. The calculation tree should show the parent element as FinanceLeaseLiabilityPaymentsDue with children of FinanceLeaseLiabilityUndiscountedExcessAmount + FinanceLeaseLiability + any extension items. There should already be a calculation tree defined for the components of FinanceLeaseLiabilityPaymentsDue comprised of FinanceLeaseLiabilityPaymentsDueNextTwelveMonths +  FinanceLeaseLiabilityPaymentsDueYearTwo etc.  These two beakdowns of FinanceLeaseLiabilityPaymentsDue should be in seperate calculation groups. If an extension item appears as a child calculation of FinanceLeaseLiabilityPaymentsDue then no error is produced.
 
Period :{$sum1.period}
Unit : {$sum1.unit}
Decimals: {$sum1.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$rule_id]