/**
DQC Rules
 (c) Copyright 2017 - 2020, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2020
Release Version 15
**/

RULE-NAME-PREFIX DQC

ASSERT US.0107.9557 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];
exists({@ScheduleOfAcquiredFiniteLivedIntangibleAssetsByMajorClassTextBlock @BusinessAcquisitionAxis = *}#invalidTextBlock)

message
"
The instance contains a text block using the concept {$invalidTextBlock.concept.name} with a value of {$invalidTextBlock} which uses the axis BusinessAcquisitionAxis. This axis means that the acquired Intangible assets were acquired as part of a business combination. The filer should use the text block ScheduleOfFiniteLivedIntangibleAssetsAcquiredAsPartOfBusinessCombinationTextBlock.

The properties of this {$invalidTextBlock.concept.name} fact are:
Period :{$invalidTextBlock.period}
Dimensions : {$invalidTextBlock.dimensions.join(', ','=')}
Unit : {$invalidTextBlock.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]


ASSERT US.0107.9558 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** This rule also needs to account for NCI **/
({@BusinessCombinationConsiderationTransferred1 @@BusinessAcquisitionAxis=*}#paid < {@BusinessCombinationRecognizedIdentifiableAssetsAcquiredGoodwillAndLiabilitiesAssumedNet @@BusinessAcquisitionAxis=*}#valueRec) and missing({@BusinessCombinationBargainPurchaseGainRecognizedAmount @@BusinessAcquisitionAxis=*}#gain)

message
"
The company has paid a value of {$paid} but has recognized a value of {$valueRec}. This represents a gain on the acquisition and the value for the purchase gain should appear in the instance. Please check that value for the gain has been tagged with the element BusinessCombinationBargainPurchaseGainRecognizedAmount.

The properties of this {$paid.concept.name} fact are:
Period :{$paid.period}
Dimensions : {$paid.dimensions.join(', ','=')}
Unit : {$paid.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]

rule-focus $paid

ASSERT US.0107.9559 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** WHere company has used PAID AMOUNT incorrectly **/

/** The amount paid for acquisitions is a duration item and should reflect the amount paid for the acquisition.  Generally this should be reported as 
** by each individual acquisition.  However some companies report it with no members and a very specific date. This tends to indicate it is the amount for a specific
** purchase and as such should have an acqusition member. 

** This rule looks at the total price paid and determines if the amount as an associated acquisition member and if it doesnt then determines that the period matches
** the current reporting period.
** An example filing that is detected is:
** https://www.sec.gov/ix?doc=/Archives/edgar/data/859737/000085973720000018/holxq2-2020.htm or https://www.sec.gov/Archives/edgar/data/859737/000085973720000018/holxq2-2020.htm 
** Entered value for purchase but was as default.
**/

$basePeriod = first(list({@concept.local-name ='DocumentPeriodEndDate'})).period.days
$paid = [@BusinessCombinationConsiderationTransferred1];
$paidDuration = $paid.period.days;

$paidDuration < ($basePeriod - 20 )

message
"
The company has reported a value of  {$paid} as the default value paid for acquisitions. This represents all acquisitions during the period.  However the period is for {$paidDuration} days, which is less than the reporting period, based on the document period end date.  If the amount represents a specific acquisition then the business acquisition axis must be used.

The properties of this {$paid.concept.name} fact are:
Period :{$paid.period}
Dimensions : {$paid.dimensions.join(', ','=')}
Unit : {$paid.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]

rule-focus $paid


ASSERT US.0107.9560 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** WHere company has used the percentage acquired incorrectly**/

/** When a company makes an acquisition that is less than 100% they should report the value of the non controlling interest reported. This rule checks that that if the percentage
** acquired is less than 100% that there is a corresponding amount reported for the value of non controlling interest. In some cases companies get the dimensions mixed up or create an extension element. This rule covers those situations.
**/

if ({@BusinessAcquisitionPercentageOfVotingInterestsAcquired}#percentage < 1)
    {@BusinessAcquisitionPercentageOfVotingInterestsAcquired @unit} and missing({@BusinessCombinationAcquisitionOfLessThan100PercentNoncontrollingInterestFairValue @unit})
else
    false

message
"
The company has reported a value of  {$percentage} as the percentage acquired. However, the company has not reported the value of the Non Controlling interest value. Check that the two values have the same dimension members, or that the concept BusinessCombinationAcquisitionOfLessThan100PercentNoncontrollingInterestFairValue has been used to identify the non controlling interest of the acquisition amount.

The properties of this {$percentage.concept.name} fact are:
Period :{$percentage.period}
Dimensions : {$percentage.dimensions.join(', ','=')}
Unit : {$percentage.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]

rule-focus $percentage


ASSERT US.0107.9561 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** WHere company has used the percentage acquired incorrectly**/

/** This rule checks that the following elements have been used with a business acquisition axis:
BusinessAcquisitionNameOfAcquiredEntity
BusinessAcquisitionPercentageOfVotingInterestsAcquired
BusinessAcquisitionDescriptionOfAcquiredEntity
BusinessAcquisitionSharePrice
BusinessCombinationReasonForBusinessCombination
BusinessAcquisitionRevenueReportedByAcquiredEntityForLastAnnualPeriod
BusinessAcquisitionDateOfAcquisitionAgreement1
BusinessAcquisitionEffectiveDateOfAcquisition1
BusinessAcquisitionPeriodResultsIncludedInCombinedEntity1

BusinessCombinationIndemnificationAssetsDescription
BusinessCombinationIndemnificationAssetsBasisForAmount
BusinessCombinationIndemnificationAssetsRangeOfOutcomesValueHigh
BusinessCombinationIndemnificationAssetsRangeOfOutcomesValueLow
BusinessCombinationIndemnificationAssetsRangeOfOutcomesInestimable
BusinessCombinationIndemnificationAssetsRangeOfOutcomesMaximumUnlimited
BusinessCombinationIndemnificationAssetsAmountAsOfAcquisitionDate

BusinessCombinationBargainPurchaseGainRecognizedAmount
BusinessCombinationBargainPurchaseGainRecognizedDescription
BusinessCombinationStepAcquisitionEquityInterestInAcquireeDescription
BusinessCombinationStepAcquisitionEquityInterestInAcquireeFairValue1
BusinessCombinationStepAcquisitionEquityInterestInAcquireeIncludingSubsequentAcquisitionPercentage
BusinessCombinationStepAcquisitionEquityInterestInAcquireePercentage

BusinessAcquisitionEquityInterestIssuedOrIssuableDescription
BusinessAcquisitionEquityInterestIssuedOrIssuableBasisForDeterminingValue
BusinessAcquisitionEquityInterestsIssuedOrIssuableNumberOfSharesIssued
BusinessAcquisitionEquityInterestIssuedOrIssuableValueAssigned

BusinessCombinationContingentConsiderationArrangementsDescription
BusinessCombinationContingentConsiderationArrangementsBasisForAmount
BusinessCombinationContingentConsiderationArrangementsRangeOfOutcomesValueHigh
BusinessCombinationContingentConsiderationArrangementsRangeOfOutcomesValueLow
BusinessCombinationContingentConsiderationArrangementsRangeOfOutcomesInestimable
BusinessCombinationContingentConsiderationArrangementsRangeOfOutcomesMaximumUnlimited

**/


$conceptsNeedingBusinessAcquisitionAxis = set(BusinessAcquisitionNameOfAcquiredEntity
                                            ,BusinessAcquisitionPercentageOfVotingInterestsAcquired
                                            ,BusinessAcquisitionDescriptionOfAcquiredEntity
                                            ,BusinessAcquisitionSharePrice
                                            ,BusinessCombinationReasonForBusinessCombination
                                            ,BusinessAcquisitionRevenueReportedByAcquiredEntityForLastAnnualPeriod
                                            ,BusinessAcquisitionDateOfAcquisitionAgreement1
                                            ,BusinessAcquisitionEffectiveDateOfAcquisition1
                                            ,BusinessAcquisitionPeriodResultsIncludedInCombinedEntity1
                                            ,BusinessCombinationIndemnificationAssetsDescription
                                            ,BusinessCombinationIndemnificationAssetsBasisForAmount
                                            ,BusinessCombinationIndemnificationAssetsRangeOfOutcomesValueHigh
                                            ,BusinessCombinationIndemnificationAssetsRangeOfOutcomesValueLow
                                            ,BusinessCombinationIndemnificationAssetsRangeOfOutcomesInestimable
                                            ,BusinessCombinationIndemnificationAssetsRangeOfOutcomesMaximumUnlimited
                                            ,BusinessCombinationIndemnificationAssetsAmountAsOfAcquisitionDate
                                            ,BusinessCombinationBargainPurchaseGainRecognizedAmount
                                            ,BusinessCombinationBargainPurchaseGainRecognizedDescription
                                            ,BusinessCombinationStepAcquisitionEquityInterestInAcquireeDescription
                                            ,BusinessCombinationStepAcquisitionEquityInterestInAcquireeFairValue1
                                            ,BusinessCombinationStepAcquisitionEquityInterestInAcquireeIncludingSubsequentAcquisitionPercentage
                                            ,BusinessCombinationStepAcquisitionEquityInterestInAcquireePercentage
                                            ,BusinessAcquisitionEquityInterestIssuedOrIssuableDescription
                                            ,BusinessAcquisitionEquityInterestIssuedOrIssuableBasisForDeterminingValue
                                            ,BusinessAcquisitionEquityInterestsIssuedOrIssuableNumberOfSharesIssued
                                            ,BusinessAcquisitionEquityInterestIssuedOrIssuableValueAssigned
                                            ,BusinessCombinationContingentConsiderationArrangementsDescription
                                            ,BusinessCombinationContingentConsiderationArrangementsBasisForAmount
                                            ,BusinessCombinationContingentConsiderationArrangementsRangeOfOutcomesValueHigh
                                            ,BusinessCombinationContingentConsiderationArrangementsRangeOfOutcomesValueLow
                                            ,BusinessCombinationContingentConsiderationArrangementsRangeOfOutcomesInestimable
                                            ,BusinessCombinationContingentConsiderationArrangementsRangeOfOutcomesMaximumUnlimited);


    exists({@concept in $conceptsNeedingBusinessAcquisitionAxis @BusinessAcquisitionAxis=none}#missingMember)

message
"
The company has reported a value of  {$missingMember} for the concept {$missingMember.concept.name}. It is expected that this fact also uses the BusinessAcquisitionAxis with a member indicating the business acqusition that the fact was related to.  This fact must inlude the business acquistion axis (BusinessAcquisitionAxis).

The properties of this {$missingMember.concept.name} fact are:
Period :{$missingMember.period}
Dimensions : {$missingMember.dimensions.join(', ','=')}
Unit : {$missingMember.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]

rule-focus $missingMember


ASSERT US.0107.9562 SATISFIED
/** The filing https://www.sec.gov/ix?doc=/Archives/edgar/data/47518/000004751820000129/hrc-20200930.htm has this incorrect **/

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

{@PaymentsToAcquireBusinessesGross}#grossPaid < {@PaymentsToAcquireBusinessesNetOfCashAcquired}#NetPaid

message
"
The company has reported a value of  {$grossPaid} for the concept {$grossPaid.concept.name} which is less than the value paid for cash paid net cash acquired with a value of {$NetPaid}. It is expected that the value of {$grossPaid.concept.name} will always be greater than or equal to {$NetPaid.concept.name} 

The properties of this {$grossPaid.concept.name} fact are:
Period :{$grossPaid.period}
Dimensions : {$grossPaid.dimensions.join(', ','=')}
Unit : {$grossPaid.unit}\n

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error

status $status

effectiveDate $effective_dates[$rule_id]

rule-focus $grossPaid

ASSERT US.0107.9563 SATISFIED
/** The filing https://www.sec.gov/Archives/edgar/data/14707/000001470718000013/cal-20180203.xml has this incorrect as does https://www.sec.gov/Archives/edgar/data/6281/000000628119000144/adi-20191102.htm **/

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$CalcNetValueAcquired = {@BusinessCombinationRecognizedIdentifiableAssetsAcquiredAndLiabilitiesAssumedNet}#nanl <+> {@Goodwill}#Goodwill;
$NetValueAcquired = {@BusinessCombinationRecognizedIdentifiableAssetsAcquiredGoodwillAndLiabilitiesAssumedNet}


$Decimals = list({@BusinessCombinationRecognizedIdentifiableAssetsAcquiredAndLiabilitiesAssumedNet},{@Goodwill},{@BusinessCombinationRecognizedIdentifiableAssetsAcquiredGoodwillAndLiabilitiesAssumedNet});

$minDecimals = min(list(for $x in $Decimals
				if $x == none 
					skip
				else
					$x.decimals));

$decimal_tolerance_factor = 2;

if $CalcNetValueAcquired == none
	skip
else	
	tolerance_for_decimals_comp($NetValueAcquired, $CalcNetValueAcquired, $minDecimals, $decimal_tolerance_factor)

message

"{$NetValueAcquired.concept.label.text} with a value of {$NetValueAcquired} is not equal to the combined total of Assets and Liabilities acquired and Goodwill ( {$nanl} + {$Goodwill} ) with a value of {$NetValueAcquired}. 

These values should be equal.
The properties of this {$NetValueAcquired.concept} fact are:
Period :{$NetValueAcquired.period}
Dimensions :  {$NetValueAcquired.dimensions.join(', ','=')}
Unit : {$NetValueAcquired.unit}
Decimals of Net Value Acquired : {$NetValueAcquired.decimals}
Decimals of Net Assets and Net Liabilities : {$nanl.decimals}
Decimals of Non Current Liabilities : {$Goodwill.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


severity error

effectiveDate $effective_dates[$rule_id]


