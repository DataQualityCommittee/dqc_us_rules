/**
DQC Rules
 (c) Copyright 2017 - 2021, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2020
Release Version 16
**/

RULE-NAME-PREFIX DQC

ASSERT US.0120.9578 SATISFIED

$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

$balanceSheetRoles = (filter taxonomy().networks(parent-child) where ($item.concept-names.contains(StatementOfFinancialPositionAbstract) or $item.role.uri.lower-case.contains('balancesheet') or $item.role.uri.lower-case.contains('financialposition')) and  $item.role.description.contains('- Statement ') and not $item.role.uri.lower-case.contains('parenthetical') returns $item.role).to-list

if $balanceSheetRoles.length == 0
	skip
else
	/** Sort so we always get the first  statement **/
	$balanceSheetRolesSorted = (filter $balanceSheetRoles returns list($item.description, $item.uri, $item )).sort

	$balanceSheetRole = $balanceSheetRolesSorted[1][3]

	$balanceSheetRoleUri = $balanceSheetRolesSorted[1][2]

	$balanceSheetItems = 
				navigate parent-child descendants from list(StatementOfFinancialPositionAbstract, AssetsAbstract, LiabilitiesAbstract, LiabilitiesAndStockholdersEquityAbstract) role $balanceSheetRole where $relationship.target.is-monetary == true returns set (target-name)

	$bsCalcItems = navigate summation-item descendants from $balanceSheetItems role $balanceSheetRole returns set (target-name);

	$bsCalcItemsLocal = filter $bsCalcItems returns $item.local-name;

	for $y in list(FinanceLeaseLiabilityCurrentStatementOfFinancialPositionExtensibleList, FinanceLeaseLiabilityNoncurrentStatementOfFinancialPositionExtensibleList, FinanceLeaseRightOfUseAssetStatementOfFinancialPositionExtensibleList, OperatingLeaseLiabilityNoncurrentStatementOfFinancialPositionExtensibleList, OperatingLeaseLiabilityCurrentStatementOfFinancialPositionExtensibleList, OperatingLeaseRightOfUseAssetStatementOfFinancialPositionExtensibleList)
		$listItem = set({@concept = $y})
		for $x in $listItem
			$colanLoc = $x.index-of(':') + 1;
			$extLocalName = $x.substring($colanLoc)
			not $bsCalcItemsLocal.contains($extLocalName)
			

	message
	"The extensible list item {$y} has a local value of {$extLocalName}. This item refers to the location of this item in the financial statements.  However, the concept {$x} does not appear in the calculation role {$balanceSheetRole.description}. Either update the value of the extensible list item {$y} with the correct balance sheet element or add the balance sheet line item {$x} to the calculation linkbase in the balance sheet role.

	Rule Element Id:{$ruleId}
	Rule version: {$ruleVersion}"

	severity error
	
	effectiveDate $effective_dates[$ruleId]