/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024

**/

RULE-NAME-PREFIX DQC
/** UPDATE RULE TO INCLUDE CHECK FOR VariableLeaseCost **/

ASSERT US.0088.9372 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

$Operating_Lease_Expense = {covered-dims @concept = OperatingLeaseExpense};
$VariableLeaseCost = {covered-dims @concept = VariableLeaseCost};
$Operating_Lease_Expense_Exists = exists($Operating_Lease_Expense);
$Variable_Lease_Cost_Exists = exists($VariableLeaseCost)

$Operating_Lease_Cost =  ({covered-dims @concept = OperatingLeaseCost});

$Operating_Lease_Cost_exists = exists($Operating_Lease_Cost);

if $Operating_Lease_Expense_Exists and $Variable_Lease_Cost_Exists and not $Operating_Lease_Cost_exists
    true
else
    false


message
"The element us-gaap:OperatingLeaseExpense with a value of  {$Operating_Lease_Expense} has been reported in the filing but the element OperatingLeaseCost is missing from the filing. Please check that the value for OperatingLeaseCost has not been tagged with another element or is missing from the filing.

Period :{$Operating_Lease_Expense.period}
Dimensions : {$Operating_Lease_Expense.dimensions.join(', ','=')}
Unit : {$Operating_Lease_Expense.unit}
Decimals: {$Operating_Lease_Expense.decimals}

Rule Element Id:{$rule_id }
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$rule_id ]

ASSERT US.0088.10094 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

$calculationChildrenOfLeaseCost = navigate summation-item descendants from LeaseCost returns target-name

$calculationChildrenOfLeaseCost.contains(OperatingLeaseExpense)

message
"The calculation children of LeaseCost includes the element OperatingLeaseExpense. If the filer has operating lease expenses rolling up into LeaseCost the element OperatingLeaseCost should be used instead of OperatingLeaseExpense.  The element OperatingLeaseCost is a required disclosure and should be used to report components of LeaseCost.

Rule Element Id:{$rule_id }
Rule version: {$ruleVersion}"

SEVERITY error

rule-focus  taxonomy().concept(OperatingLeaseExpense)

effectiveDate $effective_dates[$rule_id ]