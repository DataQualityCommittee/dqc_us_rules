/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024
**/


rule-name-prefix DQC


assert US.0145.9867 satisfied

$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else


$allowableFinancingItems = $ALLOWABLE_FINANCING_ITEMS

$taxonomyItemsNotInExtensions = $NON_FINANCING_DESCENDANTS - $allowableFinancingItems

$extensionNames = navigate parent-child descendants from NetCashProvidedByUsedInFinancingActivitiesAbstract where $relationship.target.name in $taxonomyItemsNotInExtensions returns set (target-name)

for $invalidConcept in $extensionNames
	true

message
"The company has reported the concept {$invalidConcept} as a descendant of the presentation concept NetCashProvidedByUsedInFinancingActivitiesAbstract.  This concept is not a component of financing activities and should be presented outside of this group in the presentation linkbase.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$ruleId]

rule-focus taxonomy().concept($invalidConcept)

//-----------DQC.US.0145.9868 ---------------//
assert US.0145.9868 satisfied

$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else


$taxonomyItemsNotInExtensions = ($NON_INVESTING_DESCENDANTS) - set(PaymentsForProceedsFromOtherDeposits, IncreaseDecreaseOfRestrictedInvestments, ProceedsFromPaymentsForInSecuritiesSoldUnderAgreementsToRepurchase) 

$extensionNames = navigate parent-child descendants from NetCashProvidedByUsedInInvestingActivitiesAbstract where $relationship.target.name in $taxonomyItemsNotInExtensions returns set (target-name)

for $invalidConcept in $extensionNames
	true

message
"The company has reported the concept {$invalidConcept} as a descendant of the presentation concept NetCashProvidedByUsedInInvestingActivitiesAbstract.  This concept is not a component of investing activities and should be presented outside of this group in the presentation linkbase.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$ruleId]

rule-focus taxonomy().concept($invalidConcept)

//-----------DQC.US.0145.9869 ---------------//

assert US.0145.9869  satisfied
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

$taxonomyChangeInOperatingCapital = $CHG_IN_OP_CAPTL 

$filinComponentsOfNetIncomeLoss = $FILIN_COMPONENTS_OF_NET_INC_LOSS

$misplacedConcepts = $filinComponentsOfNetIncomeLoss.intersect($taxonomyChangeInOperatingCapital)

for $invalidConcept in $misplacedConcepts
	true

message
"The company has reported the concept {$invalidConcept} as a child of the presentation concept IncomeStatementAbstract or StatementOfIncomeAndComprehensiveIncomeAbstract.  This concept is  a component of the cash flow statement and should not be presented within the Income Statement.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$ruleId]

rule-focus taxonomy().concept($invalidConcept)