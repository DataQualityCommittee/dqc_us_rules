/**

DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024

**/

rule-name-prefix DQC

assert US.0047.7481 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

for ( $cashOperating in set(NetCashProvidedByUsedInOperatingActivitiesContinuingOperations,NetCashProvidedByUsedInOperatingActivities))

	$misplaced_concept = navigate summation-item descendants from ($cashOperating)
	/** tests if the element has no balance type **/ 
	where $relationship.target.balance == none 
	/** Excludes as this is a known exception **/
    and $relationship.target.name  != NetCashProvidedByUsedInOperatingActivitiesContinuingOperations
	and $relationship.target.name  != InvestmentCompanyNetAssetsFromOperationsIncreaseDecrease
	/** DOes not flag an error if the element is an extension element **/
	and is_base($relationship.target, $ALL_BASE_TAXONOMIES_ELTS);

	for $x in $misplaced_concept.to-set 
		true

message
"In the company's extension taxonomy the concept {taxonomy().concept($cashOperating).label.text} includes {$x} as a summation-child. The concept {$x}  should not appear as a child of {$cashOperating} because it does not have a balance type. Increase (Decrease) items without balance attributes are used in a roll forward and should not be used in the cash flow statement as they represent the impact on the balance sheet item which is the opposite of the impact on cash.

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


severity error
effectiveDate $effective_dates[$rule_id]