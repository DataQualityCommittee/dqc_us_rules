/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP All

**/

RULE-NAME-PREFIX DQC

/** This rule tries to identify scaling issues in filings. Spcifically where the scale on an entire financial statement has gone wrong
To do this we look at the size of the company and look at the balance sheet and check that there is at least one number in the default is greater than 1 million dollars.

What can occur is that companies forget to add the sacale when preparing so the numbers are understated.

The other issue appears in the nots where the scale is not applied pro
**/

ASSERT US.0192.10620 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

/**Get the filere size if accelerated or large accelerated then check if values over a milliion **/

if set('Large Accelerated Filer','Accelerated Filer').contains([covered @concept.local-name ='EntityFilerCategory'])

    /** Go get the balance sheet values *by looking up the values on the balance sheet*/

    $balanceSheetRoles = (filter $PRES_NETWORKS where ($item.concept-names.contains(StatementOfFinancialPositionAbstract) or $item.role.uri.lower-case.contains('balancesheet') or $item.role.uri.lower-case.contains('financialposition')) and  $item.role.description.contains('- Statement ') and not $item.role.uri.lower-case.contains('parenthetical') returns $item.role).to-list

    if $balanceSheetRoles.length == 0
        skip
    else
        /** Sort so we always get the first  statement **/
        $balanceSheetRolesSorted = (filter $balanceSheetRoles returns list($item.description, $item.uri, $item )).sort

        $balanceSheetRole = $balanceSheetRolesSorted[1][3]

        $balanceSheetRoleUri = $balanceSheetRolesSorted[1][2]

        $balanceSheetItems = navigate parent-child descendants from list(StatementOfFinancialPositionAbstract, AssetsAbstract, LiabilitiesAbstract, LiabilitiesAndStockholdersEquityAbstract) role $balanceSheetRole where $relationship.target.is-monetary == true returns set (target-name)

        /** Check if any of the values is over 1 million dollars.  If yes then pass rule, if not fail**/

        $AllValues = (list([covered @concept in $balanceSheetItems]))

        $MaxValue = max(list(for $x in $AllValues
                            if $x == none 
                                skip 
                            else
                                $x))

        if $MaxValue == none 
            skip
        else
            $MaxValue < 1000000
        
else
    false

message
"The filer is a Large Accelerated Filer or an Accelerated filer, based on the value provided for EntityFilerCategory.  The balance sheet of the filing does not contain a single value that is greater than 1 million {$MaxValue.unit}.  The largest value reported on the balance sheet is for {$MaxValue.concept.name} with a a value of {$MaxValue} for the period {$MaxValue.period}. The inline scale used for this value is {$MaxValue.inline-scale}.

Please review the filing to ensure that the appropriate scale has been included for the values used in the balance sheet and remainder of the filing.

The properties of this  fact are:
Concept :{$MaxValue.concept.name}
Period :{$MaxValue.period}
Unit : {$MaxValue.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


severity error

effectiveDate $effective_dates[$rule_id]