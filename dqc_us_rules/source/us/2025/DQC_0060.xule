/**

DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024

**/

rule-name-prefix DQC

assert US.0060.7495 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

count(list([@NetCashProvidedByUsedInFinancingActivities])) == 0 and 
  
count(list([@NetCashProvidedByUsedInFinancingActivitiesContinuingOperations]#fact1))!= 0 and
  
count(list([@CashProvidedByUsedInFinancingActivitiesDiscontinuedOperations]))== 0 

message
"This rule reflects updated guidance from the Data Quality Committee which contradicts with historical rules. This is a change based on the changes in the cashflow ASU published by FASB.
 The element {$fact1.name}  has been reported with a value of {$fact1}. However no value has been reported for either the element(s) NetCashProvidedByUsedInFinancingActivities and CashProvidedByUsedInFinancingActivitiesDiscontinuedOperations. 
It is expected that if the element {$fact1.name}  is reported then values would be reported for these corresponding elements. If the value of {$fact1} represents the value of NetCashProvidedByUsedInFinancingActivities then this more general element should be used as it is a required disclosure.

The properties of this {$fact1.concept} fact are:
Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]

assert US.0060.7496 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

count(list([@NetCashProvidedByUsedInInvestingActivities])) == 0 and 
count(list([@NetCashProvidedByUsedInInvestingActivitiesContinuingOperations]#fact1))!= 0 and
count(list([@CashProvidedByUsedInInvestingActivitiesDiscontinuedOperations]))== 0 


message
"This rule reflects updated guidance from the Data Quality Committee which contradicts with historical rules. This is a change based on the changes in the cashflow ASU published by FASB.
 The element {$fact1.name}  has been reported with a value of {$fact1}. However no value has been reported for either the element(s) NetCashProvidedByUsedInInvestingActivities and CashProvidedByUsedInInvestingActivitiesDiscontinuedOperations. 
It is expected that if the element {$fact1.name}  is reported then values would be reported for these corresponding elements. If the value of {$fact1} represents the value of NetCashProvidedByUsedInInvestingActivities then this more general element should be used as it is a required disclosure.

The properties of this {$fact1.concept} fact are:
Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]


assert US.0060.7497 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

/** The discOpsMaterialityFlag is added to address those cases where the value has used cashflow from continuing ops, but the filer has no disc ops from cash flow
 because it is immaterial. We asses this situation if the filer reported discontinued operations from income and the value of this is less than 5% of 
 cash flow from continuing ops. If the filer has used continuing ops by mistake  this will get picked up if they have no Disc ops income and if disc ops 
 income is more than 5% of cash flow from continuing ops. **/

$discOpsValue = (list([nonils @concept in $DISCONTINUED_INCOME_ITEMS]))
$continuingOpsValue =  list([nonils @concept = NetCashProvidedByUsedInOperatingActivitiesContinuingOperations])

if $continuingOpsValue.length == 0
    skip
else
    $MaxValueOfDiscOps = if $discOpsValue.length > 0
            max($discOpsValue)
        else
            if $continuingOpsValue.length > 0
                max($continuingOpsValue)
            else
                skip

    $discOpsMaterialityFlag = if  (abs($MaxValueOfDiscOps)  >  abs(max($continuingOpsValue)) *  0.05)
                                    true
                            else
                                    false              
     if set('10-K','10-KT','10-K/A','20-F').contains([covered @concept.local-name ='DocumentType'])
        /** USed to exclude quarterly financial data that is disclosed in a 10-K but is not complete **/
        count(list([@concept = NetCashProvidedByUsedInOperatingActivities where $fact.period.days > 290])) == 0 and 
        count(list([@concept = NetCashProvidedByUsedInOperatingActivitiesContinuingOperations where $fact.period.days > 290]#fact1))!= 0 and  
        count(list([@concept = CashProvidedByUsedInOperatingActivitiesDiscontinuedOperations where $fact.period.days > 290]))== 0  and
        $discOpsMaterialityFlag

    else
        count(list([@NetCashProvidedByUsedInOperatingActivities])) == 0 and 
        count(list([@NetCashProvidedByUsedInOperatingActivitiesContinuingOperations]#fact1))!= 0 and  
        count(list([@CashProvidedByUsedInOperatingActivitiesDiscontinuedOperations]))== 0  and
        $discOpsMaterialityFlag

message
"This rule reflects updated guidance from the Data Quality Committee which contradicts with historical rules. This is a change based on the changes in the cashflow ASU published by FASB.

 The element {$fact1.name}  has been reported with a value of {$fact1}. However no value has been reported for either the element(s) NetCashProvidedByUsedInOperatingActivities and CashProvidedByUsedInOperatingActivitiesDiscontinuedOperations. 
It is expected that if the element {$fact1.name}  is reported then values would be reported for these corresponding elements. If the value of {$fact1} represents the value of NetCashProvidedByUsedInOperatingActivities then this more general element should be used as it is a required disclosure.

The properties of this {$fact1.concept} fact are:
Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]