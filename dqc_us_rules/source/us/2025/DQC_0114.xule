/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024
Release Version 18
**/

RULE-NAME-PREFIX DQC

/** This rule checks that the price net of goowill equals the value inclusive of goodwill. This does not operate on the default.
The filing https://www.sec.gov/Archives/edgar/data/14707/000001470718000013/cal-20180203.xml has this incorrect as does https://www.sec.gov/Archives/edgar/data/6281/000000628119000144/adi-20191102.htm **/

ASSERT US.0114.9563 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

$CalcNetValueAcquired = {@BusinessCombinationRecognizedIdentifiableAssetsAcquiredAndLiabilitiesAssumedNet @@BusinessAcquisitionAxis=*}#nanl <+> {@Goodwill @@BusinessAcquisitionAxis=*}#Goodwill;
$NetValueAcquired = {@BusinessCombinationRecognizedIdentifiableAssetsAcquiredGoodwillAndLiabilitiesAssumedNet @@BusinessAcquisitionAxis=*}


$Decimals = list({@BusinessCombinationRecognizedIdentifiableAssetsAcquiredAndLiabilitiesAssumedNet @@BusinessAcquisitionAxis=*},{@Goodwill @@BusinessAcquisitionAxis=*},{@BusinessCombinationRecognizedIdentifiableAssetsAcquiredGoodwillAndLiabilitiesAssumedNet @@BusinessAcquisitionAxis=*});

$minDecimals = min(list(for $x in $Decimals
				if $x == none 
					skip
				else
					$x.decimals));

$decimal_tolerance_factor = 2;

if $CalcNetValueAcquired == none
	skip
else	
	tolerance_for_decimals_comp($NetValueAcquired, $CalcNetValueAcquired, $minDecimals, $decimal_tolerance_factor)

message

"{$NetValueAcquired.concept.label.text} with a value of {$NetValueAcquired} is not equal to the combined total of Assets and Liabilities acquired excluding Goodwill and Goodwill ( {$nanl} + {$Goodwill} ) with a value of {$CalcNetValueAcquired}. 

These values should be equal.
The properties of this {$NetValueAcquired.concept} fact are:
Period :{$NetValueAcquired.period}
Dimensions :  {$NetValueAcquired.dimensions.join(', ','=')}
Unit : {$NetValueAcquired.unit}
Decimals of Net Value Acquired : {$NetValueAcquired.decimals}
Decimals of Net Assets and Net Liabilities : {$nanl.decimals}
Decimals of Goodwill : {$Goodwill.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


severity error

effectiveDate $effective_dates[$rule_id]

rule-focus $NetValueAcquired


