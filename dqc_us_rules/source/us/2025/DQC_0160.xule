/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024

**/

RULE-NAME-PREFIX DQC

ASSERT US.0160.10082 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

/**Check that the non related party member has no descendants **/

$NonRelatedPartyMemberDescendants = navigate domain-member descendants from NonrelatedPartyMember returns (target-name)

$NonRelatedPartyMemberDescendants.length > 0

message
"The filer has reported descendant members of the  member NonrelatedPartyMember. This member cannot have any descendants and is only to be used to show the non related party portion of a balance containing related party components. The descendant members are as follows: {$NonRelatedPartyMemberDescendants.join(', ')}.


Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}
"

SEVERITY error

effectiveDate $effective_dates[$rule_id]

ASSERT US.0160.10092 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

/**Check that the non related party member has no descendants **/

$NonRelatedPartyMemberSiblings = navigate domain-member siblings from NonrelatedPartyMember returns (target-name)

if $NonRelatedPartyMemberSiblings.length > 0 and $NonRelatedPartyMemberSiblings.contains(RelatedPartyMember)
    false
else
    if $NonRelatedPartyMemberSiblings.length > 0
        true
    else
        false

message
"The filer has defined the element NonrelatedPartyMember in the filing and includes the sibling members of {$NonRelatedPartyMemberSiblings.join(", ")}. When using the member NonrelatedPartyMember the filing must also have the sibling member RelatedPartyMember. The elements {$NonRelatedPartyMemberSiblings.join(", ")} should be defined as children of the RelatedPartyMember. 


Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}
"

SEVERITY error

effectiveDate $effective_dates[$rule_id]