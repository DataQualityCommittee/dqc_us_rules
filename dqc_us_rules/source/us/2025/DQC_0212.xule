/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2025
**/


rule-name-prefix DQC

CONSTANT $DOCUMENT_PERIOD = first(list({covered @concept.local-name = 'DocumentPeriodEndDate'}))
CONSTANT $DOCUMENT_PERIOD_END_DATE = $DOCUMENT_PERIOD.period.end - time-span("P1D")

assert US.0212.10736 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

    $present_fact = [@CommonStockSharesOutstanding @@period.end = $DOCUMENT_PERIOD_END_DATE];
    $not_present_fact1 = [@CommonStockSharesIssued @@period.end  = $DOCUMENT_PERIOD_END_DATE];
	$not_present_fact2 = {@SharesIssued @StatementClassOfStockAxis = * @@period.end  = $DOCUMENT_PERIOD_END_DATE};
	$not_present_fact3 = [@SharesIssued @@period.end  = $DOCUMENT_PERIOD_END_DATE];
    $not_present_fact5 = [@SharesIssued @StatementEquityComponentsAxis = CommonStockMember @@period.end  = $DOCUMENT_PERIOD_END_DATE];
    $not_present_fact6 = [@CommonStockSharesIssued @StatementEquityComponentsAxis = CommonStockMember @@period.end  = $DOCUMENT_PERIOD_END_DATE];
    $not_disclosed_on_purpose = list({covered @concept.local-name ='CommonStockSharesIssuedNotDisclosed'}).length > 0

    exists($present_fact) and 
    missing($not_present_fact1) 
    and missing($not_present_fact2) 
    and missing($not_present_fact3) 
    and missing($not_present_fact5) 
    and missing($not_present_fact6)
    and not $not_disclosed_on_purpose

message

"A value was reported for {$present_fact.name} with a value of {$present_fact}. A value was not reported to represent shares issued. If shares outstanding and shares issued are the same, then the value should be tagged with the concept {$present_fact.name} and the concept CommonStockSharesIssued. 

If it is not reported use the extension boolean concept called CommonStockSharesIssuedNotDisclosed with value true in any context. 

The properties of this {$present_fact.name} fact are:

Period: {$present_fact.period}
Unit: {$present_fact.unit}
Rule version: {$ruleVersion}"

severity error  
rule-focus $present_fact
status $status  

effectiveDate $effective_dates[$rule_id]


assert US.0212.10737 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

     $present_fact = [@SharesOutstanding @@period.end  = $DOCUMENT_PERIOD_END_DATE];
    $not_present_fact1 = [@CommonStockSharesIssued @@period.end  = $DOCUMENT_PERIOD_END_DATE];
	$not_present_fact2 = {@SharesIssued @StatementClassOfStockAxis = * @@period.end  = $DOCUMENT_PERIOD_END_DATE};
	$not_present_fact3 = [@SharesIssued @@period.end  = $DOCUMENT_PERIOD_END_DATE];
    $not_present_fact4 = [@SharesIssued @StatementEquityComponentsAxis = CommonStockMember @@period.end  = $DOCUMENT_PERIOD_END_DATE];
    $not_present_fact5 = [@CommonStockSharesIssued @StatementEquityComponentsAxis = CommonStockMember @@period.end  = $DOCUMENT_PERIOD_END_DATE];
    $not_disclosed_on_purpose = list({covered @concept.local-name ='SharesIssuedNotDisclosed'}).length > 0

    exists($present_fact) 
    and missing($not_present_fact1) 
    and missing($not_present_fact2) 
    and missing($not_present_fact3) 
    and missing($not_present_fact4) 
    and missing($not_present_fact5) 
    and not $not_disclosed_on_purpose

message

"A value was reported for {$present_fact.name} with a value of {$present_fact}. A value was not reported to represent shares issued. If shares outstanding and shares issued are the same, then the value should be tagged with the concept {$present_fact.name} and the concept CommonStockSharesIssued.

If it is not reported use the extension boolean concept called SharesIssuedNotDisclosed with value true in any context.

The properties of this {$present_fact.name} fact are:

Period: {$present_fact.period}
Unit: {$present_fact.unit}
Rule version: {$ruleVersion}"

severity error  
rule-focus $present_fact
status $status  

effectiveDate $effective_dates[$rule_id]


assert US.0212.10738 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name()) 
    or list({covered @concept.local-name ='TradingSymbol'}).length == 0
    or (true in list({covered @concept.local-name ='NoTradingSymbolFlag'}))
    or list({covered @concept.local-name ='NumberOfAuthorizedSharesNotDisclosed'}).length > 0
	skip
else

    $present_fact = [@CommonStockSharesOutstanding @unit @period.end  = $DOCUMENT_PERIOD_END_DATE];
    $not_present_fact1 = [@CommonStockSharesAuthorized @StatementClassOfStockAxis @unit @period.end  = $DOCUMENT_PERIOD_END_DATE];
	$not_present_fact2 = [@CommonStockSharesAuthorizedUnlimited @StatementClassOfStockAxis @unit @period.end = $DOCUMENT_PERIOD_END_DATE];

    exists($present_fact) and missing($not_present_fact1) and missing($not_present_fact2)

message

"A value was reported for {$present_fact.name} with a value of {$present_fact}. A value was not reported to represent shares authorized. If shares outstanding and shares authorized are the same, then the value should be tagged with the concept {$present_fact.name} and the concept CommonStockSharesAuthorized.  If the authorized stock is unlimited it should be tagged with the concept CommonStockSharesAuthorizedUnlimited.  In those cases where the authorized shares are not disclosed, the filer should tag a boolean value to an extension concept called NumberOfAuthorizedSharesNotDisclosed with a value of true in any context. This can occur for companies that are not publicly listed.

The properties of this {$present_fact.name} fact are:

Period: {$present_fact.period}
Unit: {$present_fact.unit}
Rule version: {$ruleVersion}"

severity error
rule-focus $present_fact
status $status  

effectiveDate $effective_dates[$rule_id]


assert US.0212.10739 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
    or list({covered @concept.local-name ='TradingSymbol'}).length == 0
    or (true in list({covered @concept.local-name ='NoTradingSymbolFlag'}))
    or list({covered @concept.local-name ='NumberOfAuthorizedSharesNotDisclosed'}).length > 0
	skip
else

    $present_fact = [@SharesOutstanding @unit @period.end = $DOCUMENT_PERIOD_END_DATE];
    $not_present_fact1 = [@CommonStockSharesAuthorized @unit @period.end = $DOCUMENT_PERIOD_END_DATE];
	$not_present_fact2 = [@CommonStockSharesAuthorizedUnlimited @unit @period.end = $DOCUMENT_PERIOD_END_DATE];
	$not_present_fact3 = [@CommonStockSharesAuthorized @StatementClassOfStockAxis = * @unit @period.end = $DOCUMENT_PERIOD_END_DATE];

    exists($present_fact) and missing($not_present_fact1) and missing($not_present_fact2) and missing($not_present_fact3)

message

"A value was reported for {$present_fact.name} with a value of {$present_fact}. A value was not reported to represent shares authorized. If shares outstanding and shares authorized are the same, then the value should be tagged with the concept {$present_fact.name} and the concept CommonStockSharesAuthorized.  If the authorized stock is unlimited it should be tagged with the concept CommonStockSharesAuthorizedUnlimited.  In those cases where the authorized shares are not disclosed, the filer should tag a boolean value to an extension concept called NumberOfAuthorizedSharesNotDisclosed with a value of true in any context. This can occur for companies that are not publicly listed.

The properties of this {$present_fact.name} fact are:

Period: {$present_fact.period}
Unit: {$present_fact.unit}
Rule version: {$ruleVersion}"

severity error  
rule-focus $present_fact
status $status  

effectiveDate $effective_dates[$rule_id]