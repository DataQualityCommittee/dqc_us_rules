/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024
**/


rule-name-prefix DQC

ASSERT US.0208.10727 SATISFIED

/** Check to make sure an extensible enumeration value for the investment type is used as a member on the investment type axis.
Get the value of each extensible enumeration value and check that it is used on the investment type axis if it is not used on the investment type axis then it is a violation of this rule.**/


$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

/** GEt SOI cubes **/
$SOI_CUBES = FILTER taxonomy().cubes where $item.drs-role.description.contains('- Statement -')
and $item.cube-concept.name in set(ScheduleOfInvestmentsTable)

$subTotal = list({covered @concept = InvestmentOwnedAtFairValue @InvestmentTypeAxis = * })

if $subTotal.length == 0
    skip
else

    $members = sum(filter  $subTotal returns set($item.dimension(InvestmentTypeAxis)))

    $Get_Domain_Members = navigate domain-member descendants from $members returns set(target-name) + $members.name ;

    $BDC_Ext_enum = sum(list([covered @concept = InvestmentTypeExtensibleEnumeration @InvestmentIdentifierAxis = *]))

    $intersectingDebt = $BDC_Ext_enum.intersect($Get_Domain_Members)

    $NoTotalForInvestmentType = $BDC_Ext_enum - $intersectingDebt

    $NoTotalForInvestmentType.length > 0


message
"The filer has reported a schedule of investments and has provided subtotals for the InvestmentTypeAxis.  The filer has also reported values for the InvestmentTypeExtensibleEnumeration concept.  The values reported for the InvestmentTypeExtensibleEnumeration concept should be used as members on the InvestmentTypeAxis or should roll up in a domain-member hierarchy to the member used as a subtotal.  The following extensible enumeration values have been reported but are not being used as members on the InvestmentTypeAxis or can be reconciled to this axis. 

- {$NoTotalForInvestmentType.join(", 
- ")}.  

Please ensure that these values are used as members on the InvestmentTypeAxis or are descendants of one of the following members {$members.join(", ")}, that are used to define subtotals on the InvestmentTypeAxis.


Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error  
status $status  

effectiveDate $effective_dates[$rule_id]


ASSERT US.0208.10728 SATISFIED

/** Check to make sure an extensible enumeration value for the Industry  is used as a member on the industry type axis.
Get the value of each extensible enumeration value and check that it is used on the industry  axis if it is not used on the industry axis then it is a violation of this rule.**/


$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

/** GEt SOI cubes **/
$SOI_CUBES = FILTER taxonomy().cubes where $item.drs-role.description.contains('- Statement -')
and $item.cube-concept.name in set(ScheduleOfInvestmentsTable)

$subTotal = list({covered @concept = InvestmentOwnedAtFairValue @EquitySecuritiesByIndustryAxis = * })
if $subTotal.length == 0
    skip
else
    $members = sum(filter  $subTotal returns set($item.dimension(EquitySecuritiesByIndustryAxis)))

    $Get_Domain_Members = navigate domain-member descendants from $members returns set(target-name) + $members.name ;

    $BDC_Ext_enum = sum(list([covered @concept = InvestmentIndustrySectorExtensibleEnumeration @InvestmentIdentifierAxis = *]))

    $intersectingDebt = $BDC_Ext_enum.intersect($Get_Domain_Members)

    $NoTotalForInvestmentType = $BDC_Ext_enum - $intersectingDebt

    $NoTotalForInvestmentType.length > 0


message
"The filer has reported a schedule of investments and has provided subtotals for the EquitySecuritiesByIndustryAxis.  The filer has also reported values for the InvestmentIndustrySectorExtensibleEnumeration concept.  The values reported for the InvestmentIndustrySectorExtensibleEnumeration concept should be used as members on the EquitySecuritiesByIndustryAxis or should roll up in a domain-member hierarchy to the member used as a subtotal.  The following extensible enumeration values have been reported but are not being used as members on the EquitySecuritiesByIndustryAxis or can be reconciled to this axis. 

- {$NoTotalForInvestmentType.join(", 
- ")}.  

Please ensure that these values are used as members on the EquitySecuritiesByIndustryAxis or are descendants of one of the following members {$members.join(", ")}, that are used to define subtotals on the EquitySecuritiesByIndustryAxis.


Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error  
status $status  

effectiveDate $effective_dates[$rule_id]
