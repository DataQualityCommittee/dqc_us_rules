/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP ALL

**/

/** Rule Documentation 

# Income Sstatement Items used on The Changes in Shareholders Equity
Rule ID: DQC_0197
Status: Final  
Release date: Oct 15, 2024  
Effective date: Nov 30, 2024

## Rule Function
The rule checks that the filer has not used income statement items in the Statement of Changes in Equity. The rule identifies income statement items using those items that are inflows and outflows to retained earnings defined in the US-GAAP meta taxonomy. The rule excludes the elements NetIncomeLoss and ProfitLoss from the list of income statement items. 

## Problem Solved by the Rule
 The purpose of the rule is to identify those cases where filers have used an incrrect element in the roll-forward of stockholders equity. 

## Example Rule Message
Revenues has a value of 234,000 and has been used with an inappropriate member on the StatementEquityComponentsAxis. This fact value has been used with the StatementEquityComponentsAxis. This element should not be used to represent movements in equity.  Please remove the member from this fact or consider using a different element to represent the movement of Equity.

The properties of this Revenue fact are:
Period :2022-12-31 to 2023-12-31
Dimensions : us-gaap:StatementEquityComponentsAxis=us-gaap:CommonStockMember
Unit : USD
Rule Element Id:10634
Rule version: 26.0.0RC1

## Rule element ID index  
The rule element id is used to identify unique elements or combinations of elements tested in the rule.

|Rule Element ID|Element|
|--- |--- |
| DQC.US.0197.10634 |StatementEquityComponentsAxis = * and Income Statement Items|


Â© Copyright 2016 - 2025, XBRL US Inc. All rights reserved.   
See [License](https://xbrl.us/dqc-license) for license information.  
See [Patent Notice](https://xbrl.us/dqc-patent) for patent infringement notice. 

 **/

RULE-NAME-PREFIX DQC

ASSERT US.0197.10634 satisfied

/** Therule check where the commonstock member is used on the StatementEquityComponentsAxis with an income statement element that is not an expected element.}**/

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name()) or $BDC_FLAG
	skip
else

    $Pref_stock_items = navigate summation-item descendants from PreferredStockDividendsAndOtherAdjustments taxonomy $US-GAAP_ELTS_CALCS returns set (target-name)

    $Pref_stock_items_allowed = navigate summation-item descendants from StockholdersEquityPeriodIncreaseDecrease taxonomy $US-GAAP_ELTS_CALCS returns set (target-name)

    $income_statement_items = $INCOME_STATEMENT_CONCEPTS + ($Pref_stock_items - $Pref_stock_items_allowed) - set(NetIncomeLoss, ProfitLoss, NetIncomeLossAttributableToNoncontrollingInterest, InvestmentCompanyDividendDistribution, OtherComprehensiveIncomeAvailableforsaleSecuritiesAdjustmentNetOfTaxPortionAttributableToParent)

    $fact1 = {nonils @concept in $income_statement_items  @StatementEquityComponentsAxis = * @ReclassificationOutOfAccumulatedOtherComprehensiveIncomeAxis = none}
    exists($fact1)

message
"{$fact1.concept} has a value of {$fact1} and has been used with an inappropriate member on the StatementEquityComponentsAxis. This fact value has been used with the StatementEquityComponentsAxis. This element should not be used to represent movements in equity.  Please remove the member from this fact or consider using a different element to represent the movement of Equity.

The properties of this {$fact1.concept} fact are:
Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}
Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $fact1
effectiveDate $effective_dates[$rule_id]