/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2024
**/

rule-name-prefix DQC

/** DQC0 0149.9943 **/
assert US.0149.9943  satisfied
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

/**Noncurrent Assets used incorrectly  Rule checks that if the element appears in a statement presentation linkbase then an error is recorded.**/

$invalidUseOfElement = set(NoncurrentAssets) intersect (filter $STATEMENT_PRES_NETWORKS.concepts returns $item.name)

if $invalidUseOfElement.length > 0
    true
else
    false

message
"The element NoncurrentAssets should not appear in the face financial statements of a filing. This element represents long lived assets that are reported in the segment reporting section of the financial statements. Use the element AssetsNoncurrent on the Balance Sheet.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}
"

severity error
effectiveDate $effective_dates[$ruleId]


assert US.0149.10075 satisfied
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

/** DQCRT RULE **/
if not applicable_form(rule-name())
	skip
else

/**Noncurrent Assets used incorrectly  Rule checks that  the element appears as a descendant ofScheduleOfRevenuesFromExternalCustomersAndLongLivedAssetsTable **/

$NonCurrantAssetFact = {@NoncurrentAssets}
$actualLocation = navigate parent-child ancestors from NoncurrentAssets where $relationship.source.substitution == xbrldt:hypercubeItem returns set (target-name);
$additionalMessage = if $actualLocation.length > 0 "The element appears as a descendant of the table " +  $actualLocation.join(', ') + ".  Update the table to ScheduleOfRevenuesFromExternalCustomersAndLongLivedAssetsTable." else "";
if exists($NonCurrantAssetFact)
    $locationCheck = navigate parent-child descendants from ScheduleOfRevenuesFromExternalCustomersAndLongLivedAssetsTable where $relationship.target-name == NoncurrentAssets returns set (target-name)
    if $locationCheck.length == 0
        true
    else 
        false
else
    false

message
"A value of {$NonCurrantAssetFact} has been reported for the concept NoncurrentAssets. This element is used to represent the value of Long Lived Assets by Geographic region.  This element must be included as a presentation descendant of the table ScheduleOfRevenuesFromExternalCustomersAndLongLivedAssetsTable. {$additionalMessage}

Concept : {$NonCurrantAssetFact.concept}
Value : {$NonCurrantAssetFact} 
Total period : {$NonCurrantAssetFact.period} 
Dimensions : {$NonCurrantAssetFact.dimensions.join(', ','=')}
Decimals : {$NonCurrantAssetFact.decimals} 

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$ruleId]