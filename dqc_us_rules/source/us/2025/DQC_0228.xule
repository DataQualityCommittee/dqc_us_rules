/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2025
**/


rule-name-prefix DQC

ASSERT US.0228.10801 satisfied

/** 
Check that other is not greater than its non other partner
**/

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];
if not applicable_form(rule-name())
	skip
else
  for $item in $OTHER_AGGREGATE
      $source = $item.first
      $target = $item.last

      if $source in $NON_NEG_ITEMS.keys
          $sourceFact = {nonils @concept = $source where $fact > 0 and ($fact.dimensions.length == 0 or 
                            (not match_set_substring($fact, $EXCLUDE_NON_NEG_STRING_MEMBERS)
                            and (to-set($fact.dimensions-explicit.values.name) intersect $EXCLUDE_NON_NEG_MEMBERS).length == 0
                            and ($fact.dimensions.keys.name intersect $EXCLUDE_NON_NEG_AXIS).length == 0
                            and not_elim_adjust($fact, $EXCLUDE_NON_NEG_AXIS_MEMBERS)))
                            }
          $targetFact = {nonils @concept = $target where $fact > 0 and ($fact.dimensions.length == 0 or 
                            (not match_set_substring($fact, $EXCLUDE_NON_NEG_STRING_MEMBERS)
                            and (to-set($fact.dimensions-explicit.values.name) intersect $EXCLUDE_NON_NEG_MEMBERS).length == 0
                            and ($fact.dimensions.keys.name intersect $EXCLUDE_NON_NEG_AXIS).length == 0
                            and not_elim_adjust($fact, $EXCLUDE_NON_NEG_AXIS_MEMBERS)))
                            }

              if ($sourceFact < $targetFact)
                  true
              else
                  if ($sourceFact == $targetFact) and not $sourceFact.concept.name in list(Assets, Liabilities, AssetsCurrent, LiabilitiesCurrent, AssetsNoncurrent, LiabilitiesNoncurrent)
                        true
                  else
                        false
      else
          skip

message
"The value of the {$source} with a value of {$sourceFact} should be greater than the concept {$target} with a value of {$targetFact}.  Other items should role up into the value of the aggregate item {$source}.


Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]

