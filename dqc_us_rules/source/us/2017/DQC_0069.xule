/**

DQC Rules
 (c) Copyright 2017 - 2018, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2017

**/

rule-name-prefix DQC

assert US.0069.1236 satisfied

$childMember = {@concept = DefinedBenefitPlanPlanAssetsInvestmentWithinPlanAssetCategoryAmount}.dimension(DefinedBenefitPlanByPlanAssetCategoriesAxis);

if $childMember == none
	skip
else
	$parentMember = navigate dimensions descendants from DefinedBenefitPlanByPlanAssetCategoriesAxis to $childMember where $relationship.target == $childMember returns source-name;

	{@concept = DefinedBenefitPlanPlanAssetsInvestmentWithinPlanAssetCategoryAmount @DefinedBenefitPlanByPlanAssetCategoriesAxis = $childMember}#LessAmount >= {@concept = DefinedBenefitPlanFairValueOfPlanAssets 	@DefinedBenefitPlanByPlanAssetCategoriesAxis in $parentMember}#GreaterAmount

message
"It is expected that the concept {$LessAmount.concept} with a value of {$LessAmount} should not use the same member element  on the DefinedBenefitPlanByPlanAssetCategoriesAxis as the  element {$GreaterAmount.concept} with a value of {$GreaterAmount}.  The element {$LessAmount.concept} is supposed to be used as a  component of {$GreaterAmount.concept} with a more specific member.

The properties of this {$LessAmount.concept} fact are:
Period :{$LessAmount.period}
Dimensions : {$LessAmount.dimensions.join(', ','=')}
Unit : {$LessAmount.unit}

Rule Element Id:xxx
Rule version: {$ruleVersion}"


SEVERITY error


assert US.0069.1237 satisfied

$LessAmount = {@concept = DefinedBenefitPlanAmountOfEmployerAndRelatedPartySecuritiesIncludedInPlanAssets};
$GreaterAmount = {@concept = DefinedBenefitPlanFairValueOfPlanAssets};

$childMember = $LessAmount.dimension(DefinedBenefitPlanByPlanAssetCategoriesAxis);

if $childMember == none
	$message_related_party = "The element DefinedBenefitPlanAmountOfEmployerAndRelatedPartySecuritiesIncludedInPlanAssets with a value of {$LessAmount} must be used with the dimension DefinedBenefitPlanByPlanAssetCategoriesAxis.";
	true 
else
	$parentMember = navigate dimensions descendants from DefinedBenefitPlanByPlanAssetCategoriesAxis to $childMember where $relationship.target == $childMember returns source-name;
	
	$message_related_party = "It is expected that the concept DefinedBenefitPlanAmountOfEmployerAndRelatedPartySecuritiesIncludedInPlanAssets with a value of {$LessAmount} should not use the same member element  on the DefinedBenefitPlanByPlanAssetCategoriesAxis as the  element DefinedBenefitPlanFairValueOfPlanAssets with a value of {$GreaterAmount}.  The element {$LessAmount.concept} is supposed to be used as a  component of {$GreaterAmount.concept} with a more specific member.";

	{@concept = DefinedBenefitPlanAmountOfEmployerAndRelatedPartySecuritiesIncludedInPlanAssets @DefinedBenefitPlanByPlanAssetCategoriesAxis = $childMember} > {@concept = DefinedBenefitPlanFairValueOfPlanAssets @DefinedBenefitPlanByPlanAssetCategoriesAxis in $parentMember}

message
"{$message_related_party}

The properties of this {$LessAmount.concept} fact are:
Period :{$LessAmount.period}
Dimensions : {$LessAmount.dimensions.join(', ','=')}
Unit : {$LessAmount.unit}

Rule Element Id:xxx
Rule version: {$ruleVersion}"


SEVERITY error




