/**
DQC Rules
 (c) Copyright 2017 - 2022, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2022
**/


rule-name-prefix DQC

//-----------DQC.US.0146.9870 ---------------//
assert US.0146.9870  satisfied
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

$document_period = first(list({covered @concept.local-name = 'DocumentPeriodEndDate'}));
$document_period_end_date = $document_period.period.end;
$document_period_start_date = $document_period.period.start + time-span("P0D")

if  not set('10-K','10-KT','20-F').contains([covered @concept.local-name ='DocumentType'])
	skip
else
	$presNetworks = FILTER $PRES_NETWORKS where $item.role.description.contains('- Statement -') 
	and (not $item.role.uri.lower-case.contains('parenthetical')) 
	and (not $item.role.uri.lower-case.contains('equity')) 
	and (not $item.role.uri.lower-case.contains('deficit')) 
	and (not $item.role.uri.lower-case.contains('stockholders')) 
	and (not $item.role.uri.lower-case.contains('capital')) 
	and (not $item.role.uri.lower-case.contains('investment')) 
	and (not $item.role.uri.lower-case.contains('changes')) 
	and (not $item.role.uri.lower-case.contains('shareholder')) 
	and (not $item.role.uri.lower-case.contains('convertible')) 
	and (not $item.role.uri.lower-case.contains('preferred'))  
	and (not $item.role.uri.lower-case.contains('temporaryequity')) 
	and (not $item.role.uri.lower-case.contains('redeemable'))
	and (not $item.role.uri.lower-case.contains('netproceedsfromallsources'))
	and (not $item.role.uri.lower-case.contains('membersinterest'))
	and (not $item.role.uri.lower-case.contains('highlights'))
                        
	for $presNetwork in $presNetworks.sort
		
		$presConceptItems = $presNetwork.concept-names;

		// Income Statement Processing 
		if $presConceptItems.contains(IncomeStatementAbstract)
			$presNumericItems = filter $presConceptItems where taxonomy().concept($item).is-monetary and taxonomy().concept($item).period-type == duration;
			$periodDuration = duration($document_period_start_date, $document_period_end_date);
			$totalYearItems = list([covered @concept in $presNumericItems @period in list($periodDuration)])#YearFacts.count
			$totalNonYearItems = list([covered @concept in $presNumericItems @period.end = $document_period_end_date - time-span("P1D") @period.start != $document_period_start_date])#facts.count
			$totalYearItems < $totalNonYearItems

		else 
			skip


message
"The company has reported a 10K but the majority of the values reported on the Income Statement are for a period other than a year. The document period for the report starts on {$document_period_start_date} and ends on {$document_period_end_date}. Facts reported for this duration were {$totalYearItems} and the total for non years were {$totalNonYearItems}. Please check that the values used on the Income Statement represent a full year for the required reporting period.

Facts with values not reported for a year are as follows: {$facts.join(' | ')}

Facts with values reported for a year are as follows: {$YearFacts.join(' | ')}

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$ruleId]

rule-focus first($facts.sort)