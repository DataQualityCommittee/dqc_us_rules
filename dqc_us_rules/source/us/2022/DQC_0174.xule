/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2023

**/

RULE-NAME-PREFIX DQC

/** Add rule for FinancingReceivableAllowanceForCreditLossesIndividuallyEvaluatedForImpairment1 being negative but does not flag if FinancingReceivableAllowanceForCreditLossesCollectivelyEvaluatedForImpairment exceeds the negative value.  See JPM case where they have neeting between th classes https://www.sec.gov/ix?doc=/Archives/edgar/data/19617/000001961723000432/jpm-20230630.htm **/

ASSERT US.0174.10115 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

if not applicable_form(rule-name())
	skip
else

$individualLoss = {@concept = FinancingReceivableAllowanceForCreditLossesIndividuallyEvaluatedForImpairment1 where $fact < 0}

$CollectiveLoss =  first-value({@concept = FinancingReceivableAllowanceForCreditLossesCollectivelyEvaluatedForImpairment}, {@concept = FinancingReceivableAllowanceForCreditLossExcludingAccruedInterest}, 0)

abs($individualLoss) > $CollectiveLoss

message
"The concept {$individualLoss.concept} has a value of {$individualLoss} which is less than zero. This element should not have a negative value. The preparer should consider if the value is input correctly for this assertion and, after considering the appropriateness of the input, if incorrect, input the amount as a positive (i.e., absolute) value and provide a negated label.

The properties of this {$individualLoss.concept} fact are:
Period :{$individualLoss.period}
Dimensions : {$individualLoss.dimensions.join(', ','=')}
Unit : {$individualLoss.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


severity error

effectiveDate $effective_dates[$rule_id]
