/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2022
**/


rule-name-prefix DQC

assert US.0142.9864 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

if not applicable_form(rule-name())
	skip
else

/** Check that the following axis have a single dimension **/

/** Check if the BDC taxonomy is used **/

$srttaxonomy = any(set(for $link in dts-document-locations(taxonomy())
                            if $link.contains('https://xbrl.fasb.org/us-gaap/2022q3/us-gaap-sup-2022q3.xsd')
                                true
                            else
                                false
                                    ));
if $srttaxonomy

    $BDCTypedDimensionsToCheck = list(us-gaap-supplement:InvestmentIdentifierAxis, 
                                        us-gaap-supplement:SecuritySoldShortIdentifierAxis,
                                        us-gaap-supplement:OpenOptionContractIdentifierAxis,
                                        us-gaap-supplement:OpenFuturesContractIdentifierAxis,
                                        us-gaap-supplement:OpenForwardForeignCurrencyContractIdentifierAxis,
                                        us-gaap-supplement:OpenSwapContractIdentifierAxis);

    for $typedDimension in $BDCTypedDimensionsToCheck

            $BDCInvestmentFact = {@$typedDimension = *}

            $Legitimateimensions = $BDCInvestmentFact.dimensions.keys.name - set(us-gaap-supplement:InvestmentCompanyNonconsolidatedSubsidiaryAxis, srt:RangeAxis, to-qname("dei:LegalEntityAxis"));
            $Legitimateimensions.length > 1
else
    false

message
"The filing reports a value of {$BDCInvestmentFact} for the concept {$BDCInvestmentFact.concept.name} using the {$typedDimension} axis.

If the {$typedDimension} is used no other axis should be used. Remove any additional axis on this fact.

Period :{($BDCInvestmentFact).period}
Dimensions : {($BDCInvestmentFact).dimensions.join(', ','=')}
Unit : {($BDCInvestmentFact).unit}\n
Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$rule_id]