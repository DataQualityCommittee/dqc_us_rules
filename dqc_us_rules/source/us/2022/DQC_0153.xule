/**
DQC Rules
 (c) Copyright 2017 - 2023, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2022
**/

rule-name-prefix DQC

/**DQC 0153.9879 **/
/** Check that the statement presentation has a table at the top and all line items as children. The rule checks if any monetary line items are siblings of the table element in the financial statements.
**/

assert US.0153.9879  satisfied
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];


for $presNetwork in $STATEMENT_PRES_NETWORKS.sort

/** get the presentation items aligned with the pres role.  We assume that the same role is used for the cube and the dimension**/
			
        $hypercubeItems = navigate parent-child descendants  role $presNetwork.role.uri where $relationship.target.substitution == xbrldt:hypercubeItem returns set (target-name)

        for $hypercubeItem in $hypercubeItems

        $siblingItems = navigate parent-child siblings from $hypercubeItem role $presNetwork.role.uri returns set (target)
        $hypercubeSiblings = filter $siblingItems where $item.is-monetary returns $item.name

        if $hypercubeSiblings.length == 0
            false
        else
            true

message
"The hypercube {$hypercubeItem} should not appear in the statement {$presNetwork.role.description} with the monetary sibling concepts of {$hypercubeSiblings.join(", ")}. The hypercube should be a child of an abstract concept and should have the primary monetary concepts as decendants.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}
"