/**

DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2022

**/


rule-name-prefix DQC

assert US.0079.7657 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

if not applicable_form(rule-name())
	skip
else
$acceptable_dimensions = set(StatementBusinessSegmentsAxis, IncomeTaxAuthorityNameAxis);

 for ($duplicate_concept in $EXTENSION_QNAME)
  if ($EXTENSION_ITEMS.keys.contains($duplicate_concept.local-name.lower-case))
     $dims = get_member_dims($duplicate_concept);
	 $dim_names_set = set(for $dim in $dims
	    if $acceptable_dimensions.contains($dim['dimension'])
	  		skip
	    else 
			$dim['dimension'].local-name);
	if $dim_names_set.length > 0
	   $replacement = $EXTENSION_ITEMS[$duplicate_concept.local-name.lower-case];
	   true
	else
	    false	  
  else
     false
	 
message
"{$duplicate_concept} is an extension member on the axis {$dim_names_set.join(', ')} that was reported in the extension taxonomy, but duplicates a concept that exists in the SEC Non-GAAP taxonomies. Consider replacing {$duplicate_concept} with the existing taxonomy concept {$replacement.local-name} with the taxonomy namespace {$replacement.namespace-uri}.

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}
"

SEVERITY error

effectiveDate $effective_dates[$rule_id]

