/**
DQC Rules
 (c) Copyright 2017 - 2022, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2022
**/


rule-name-prefix DQC


assert US.0137.9838 satisfied

/** THIS RULE ONLY CHECKS THOSE FACTS THAT HAVE USED Ta single value for tHE BALANCE SHEET AND INCOME STATEMENT LOCATION AXIS **/
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

/** Is this report a 10-K or 10-Q**/
if set('10-K','10-KT','10-K/A','20-F', '10-Q', '10-Q/A', '20-F/A','40-F','40-F/A').contains([covered @concept.local-name ='DocumentType'])

		/** Get the list of items to check **/
		for $ext_pairs in $EXT_ENUM
			$FS_concept = $ext_pairs[1]
			$related_ext_enum = $ext_pairs[2]

				/** Get a list of elements **/

				if taxonomy().concept($FS_concept).period-type == instant

					$FS_Concept_Items = list({@concept = $FS_concept @BalanceSheetLocationAxis = * @unit = *});
				    $locationAxis = "BalanceSheetLocationAxis";
					if $FS_Concept_Items.length == 1
						
							true
					else 
						
							false
				else
					$FS_Concept_Items = list({@concept = $FS_concept @IncomeStatementLocationAxis = * @unit = *});
					
					$locationAxis = "IncomeStatementLocationAxis";
					if $FS_Concept_Items.length == 1
						
							true
					else 
						
							false
else
 false
 
message

"The element {$FS_concept} with a value of {first($FS_Concept_Items)} has been used in the filing with the axis {$locationAxis}.  This is the only fact reported with this element.  Rather than using the {$locationAxis} the filer should use the extensible enumeration element to indicate the location of the item in the financial statements. In this case the element {$related_ext_enum} should be used to identify the account where the element {$FS_concept} is included. 

 
Period :{first($FS_Concept_Items).period}
Dimensions : {first($FS_Concept_Items).dimensions.join(', ','=')}
Unit : {first($FS_Concept_Items).unit}
Decimals: {first($FS_Concept_Items).decimals}

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

SEVERITY error
status $status
effectiveDate $effective_dates[$ruleId]

rule-focus first($FS_Concept_Items)

