/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2022
**/


rule-name-prefix DQC

ASSERT US.0230.10803 satisfied

/** 
Check that other is not greater than its non other partner
**/

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];
if not applicable_form(rule-name())
	skip
else
     $DICT_OTHER_AGGREGATE = $OTHER_AGGREGATE.agg-to-dict(1)
    $ALL_OTHER_ITEMS = (filter $OTHER_AGGREGATE returns $item.last).to-set

    for $item in $DICT_OTHER_AGGREGATE.keys
    
        $networks-dict = navigate summation-item children from $item returns by network set (target-name)

        for $network in $networks-dict.keys
                $dupTargetsAndBase = sum($DICT_OTHER_AGGREGATE[$item]).to-set
                $dupTargets = $dupTargetsAndBase - set($item)
                $networkValues = $networks-dict[$network]
                $unallowed_other_items = $ALL_OTHER_ITEMS - $dupTargets
                $unallowedOtherUsedInNetwork = $networkValues intersect $unallowed_other_items
                if  $unallowedOtherUsedInNetwork.length > 0
                    $ancestors = navigate summation-item ancestors from $unallowedOtherUsedInNetwork taxonomy $US-GAAP returns set (target-name)
                    if $item in $ancestors
                        false
                    else
                        true
                else
                    skip

message
"The calculation network {$network.role.description} has used the 'other' concept {$unallowedOtherUsedInNetwork.join(', ')}.  The calculation relationship has a calculation concept total of  {$item}.  The 'other' concept in this calculation is expected to be only {$dupTargets.join(', ')}. Each calculated concept should have only one 'other' concept comprising it.

Replace the other concept {$unallowedOtherUsedInNetwork.join(', ')} with {$dupTargets.join(', ')}.
   
Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]