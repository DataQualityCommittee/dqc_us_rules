/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2022
**/

rule-name-prefix DQC

//-----------DQC.US.0148.9874 ---------------//

assert US.0148.9874  satisfied
$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

if not applicable_form(rule-name())
	skip
else

$Calc_Descendants_of_NetIncome = navigate summation-item descendants from list(NetIncomeLoss, ProfitLoss, NetIncomeLossAvailableToCommonStockholdersBasic, NetIncomeLossAvailableToCommonStockholdersDiluted) returns set (target-name)

$invalidConcepts = $Calc_Descendants_of_NetIncome.intersect($OCI_CONCEPTS)

for $invalidConcept in $invalidConcepts
	true

message
"The company has reported the concept {$invalidConcept} as a descendant of Income Losss in the calculation linkbase of the filing.  This element is an OCI item and should not be included as a component of Net Income Loss.

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error
effectiveDate $effective_dates[$rule_id]

rule-focus taxonomy().concept($invalidConcept)

//-----------DQC.US.0148.10073 ---------------//

/** 
Example filing that is incorrect: http://www.sec.gov/Archives/edgar/data/1132509/000147793222006042/nnax_10q.htm
**/

assert US.0148.10073  satisfied
$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

if not applicable_form(rule-name())
	skip
else

$Calc_Descendants_of_OCI = navigate summation-item descendants from list(OtherComprehensiveIncomeLossNetOfTaxPortionAttributableToParent, OtherComprehensiveIncomeLossTaxPortionAttributableToParent1, OtherComprehensiveIncomeLossBeforeTaxPortionAttributableToParent, OtherComprehensiveIncomeLossNetOfTax, OtherComprehensiveIncomeLossNetOfTaxPortionAttributableToNoncontrollingInterest) returns set (target-name)

$invalidConcepts = $Calc_Descendants_of_OCI.intersect($NI_CONCEPTS)

for $invalidConcept in $invalidConcepts
	true

message
"The company has reported the concept {$invalidConcept} as a descendant of Other Comprehensive Income in the calculation linkbase of the filing.  This element is a Net Income item and should not be included as a component of Other Comprehensive Income.

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

SEVERITY error
effectiveDate $effective_dates[$rule_id]

rule-focus taxonomy().concept($invalidConcept)