/**

DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2022
Release Version 17
**/

rule-name-prefix DQC

assert US.0048.7482 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

if not applicable_form(rule-name())
	skip
else
/* Identify the cashflow statement by StatementOfCashFlowsAbstract */

$networkPresRole = filter $PRES_NETWORKS where ($item.concept-names.contains(StatementOfCashFlowsAbstract) or $item.role.uri.lower-case.contains('cashflow')) and  $item.role.description.contains('- Statement ') and not $item.role.uri.lower-case.contains('parenthetical') returns $item.role;

$EGCFlag = if list([covered @concept.local-name ='EntityEmergingGrowthCompany']).length > 0
				first(list([covered @concept.local-name ='EntityEmergingGrowthCompany']))
			else 
				false
				
if (any(set(exists({covered @concept.local-name ='DocumentType' where $fact in $REPORT_TYPE_EXCLUSIONS}))))
	skip
else
	if ($networkPresRole.length == 0)
		skip
	else

	/* This uses  navigation. It finds the root relationships and returns the networks. Since it  returns a set, the dups will be eliminated. */

		if (filter taxonomy().networks(summation-item) where $item.role in $networkPresRole).length == 0
			$root_string = "There are no calculations defined for the role(s)  " + $networkPresRole.join(', ');
			$network_role = $networkPresRole.join(', ');
			true
		else
			/** Test if all the calculations dont have the change in cash.  If one does then the rule will not fire. If all do not have the element then the rule will fire for each occurence.  If just one is fixed then the rule will stop running.  The DQC agreed to this because of examples of utilities having 5 cash flow statements and not wanting to duplicate the calcs because of the SEC rule prohibiting the occurence of duplicate calculation rules.  When this rule is changed this rule should be amended to flag every occurence.
			**/
			if
				all(list(for ($calcNetwork in filter taxonomy().networks(summation-item) where $item.role in $networkPresRole)

				$roots = set(for $root in $calcNetwork.roots 
										$root.name.local-name
									);

				not ($roots.contains('CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseIncludingExchangeRateEffect') or
				$roots.contains('CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseExcludingExchangeRateEffect') or
				$roots.contains('CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseIncludingExchangeRateEffectContinuingOperations') or
				$roots.contains('CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseIncludingExchangeRateEffectDisposalGroupIncludingDiscontinuedOperations') or
				$EGCFlag) and
				not ($roots.contains('NetCashProvidedByUsedInContinuingOperations') and $roots.contains('NetCashProvidedByUsedInDiscontinuedOperations'))
				))
				/** Generate an error for every occurence of a cash flow that doesnt conform.  This wont run if just one role does **/
				for ($calcNetwork in 
					filter taxonomy().networks(summation-item) where $item.role in $networkPresRole)
								
					$network_role = $calcNetwork.role.uri;

					$roots = set(for $root in $calcNetwork.roots 
										$root.name.local-name
									);
								
					$root_string = "The calculation linkbase in the filing has the following root elements defined: " + $roots.join(', ');
		
					not ($roots.contains('CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseIncludingExchangeRateEffect') or
					$roots.contains('CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseExcludingExchangeRateEffect') or
					$roots.contains('CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseIncludingExchangeRateEffectContinuingOperations') or
					$roots.contains('CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseIncludingExchangeRateEffectDisposalGroupIncludingDiscontinuedOperations') or
					$EGCFlag) and
					not ($roots.contains('NetCashProvidedByUsedInContinuingOperations') and $roots.contains('NetCashProvidedByUsedInDiscontinuedOperations'))
			else
				false

message
"The filing should include in the cash flow statement calculation linkbase(s) {$network_role} one of the following elements as the root(first element) of the calculation tree to represent the aggregate change in cash for the period:

CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseIncludingExchangeRateEffect,
CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseExcludingExchangeRateEffect, 
NetCashProvidedByUsedInContinuingOperations (As long as the element NetCashProvidedByUsedInDiscontinuedOperations is also included the reconciliation between opening and closing cash).

In some cases the following extension elements can be used when the change in cash is broken up into continuing and discontinued operations:
CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseIncludingExchangeRateEffectContinuingOperations
CashCashEquivalentsRestrictedCashAndRestrictedCashEquivalentsPeriodIncreaseDecreaseIncludingExchangeRateEffectDisposalGroupIncludingDiscontinuedOperations

{$root_string}.

Rule Element Id: {$rule_id}
Rule version: {$ruleVersion}"


severity error
effectiveDate $effective_dates[$rule_id]
rule-focus taxonomy().concept(StatementOfCashFlowsAbstract)
