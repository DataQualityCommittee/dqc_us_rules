/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: IFRS 2020

**/

RULE-NAME-PREFIX DQC

ASSERT IFRS.0105.9556 SATISFIED

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];
$presNetworks = FILTER taxonomy().networks(parent-child) where $item.role.description.contains('- Statement -')
and (not $item.role.description.lower-case.contains('parenthetical'))
and (not $item.role.uri.lower-case.contains('parenthetical')) 
and (not $item.role.uri.lower-case.contains('equity')) 
and (not $item.role.uri.lower-case.contains('deficit')) 
and (not $item.role.uri.lower-case.contains('stockholders')) 
and (not $item.role.uri.lower-case.contains('capital')) 
and (not $item.role.uri.lower-case.contains('investment')) 
and (not $item.role.uri.lower-case.contains('changes')) 
and (not $item.role.uri.lower-case.contains('shareholder')) 
and (not $item.role.uri.lower-case.contains('convertible')) 
and (not $item.role.uri.lower-case.contains('preferred'))  
and (not $item.role.uri.lower-case.contains('temporaryequity')) 
and (not $item.role.uri.lower-case.contains('redeemable'))
and (not $item.role.uri.lower-case.contains('netproceedsfromallsources'))
and (not $item.role.uri.lower-case.contains('membersinterest'))
and (not $item.role.uri.lower-case.contains('highlights'))

$calcNetworks = taxonomy().networks(summation-item);

$calcRelationshipItems = if $calcNetworks == set()
                        set()
                    else
                        sum(set(for $network in $calcNetworks
                                $network.relationships))                        

$CF_Exclusions = set(EffectOfExchangeRateChangesOnCashAndCashEquivalents, IncreaseDecreaseInCashAndCashEquivalentsDiscontinuedOperations)

$Non_CF_Items = navigate parent-child descendants from list(DisclosureOfCashFlowStatementExplanatory) taxonomy $ifrs-gaap where $relationship.target.is-monetary == true returns set (target-name);
                        

for $presNetwork in $presNetworks
    $presConceptNoCalculation =  if $presNetwork.role.description.lower-case.contains("cash")
                                        $presConceptItems = $presNetwork.concept-names;
                                                                               
                                        /** GET ANY SUPPLEMENTAL ABSTRACTS **/
                                        $supplementalAbstracts = filter $presConceptItems where $item.local-name.lower-case.contains('supplement') and taxonomy().concept($item).is-abstract;

                                        /** GET ANY noncash ABSTRACTS **/

                                        $noncashAbstracts = filter $presConceptItems where $item.local-name.lower-case.contains('noncash') and taxonomy().concept($item).is-abstract;

                                        /** GET monetary children of root elements **/
                                        $levelOneItemsPreExemptions = navigate parent-child children from $presNetwork.roots role $presNetwork.role where $relationship.target.is-monetary == true returns set (target-name);
                                        $levelOneItems = $levelOneItemsPreExemptions - set(IncreaseDecreaseInCashAndCashEquivalents)

                                        /** Get monetary items from presentation **/
                                        $presNumericItems = filter $presConceptItems where taxonomy().concept($item).is-monetary and taxonomy().concept($item).period-type == duration;
                                        $calcspresentSet = set(for $calc in $calcRelationshipItems 
                                                                if $calc.target-name in $presNumericItems and $calc.source-name in $presNumericItems 
                                                                    set($calc.target-name, $calc.source-name)
                                                                else
                                                                    skip);
                                        $calcItems = sum($calcspresentSet);
                                        
                                        $supplementalCashItems = navigate parent-child descendants from ($supplementalAbstracts.to-set + set(IncomeTaxesPaidRefundAbstract) + $noncashAbstracts.to-set) role $presNetwork.role where $relationship.target.is-monetary == true returns set (target-name);

                                        $presNumericItems - ($calcItems + $CF_Exclusions + $supplementalCashItems + $Non_CF_Items + $levelOneItems)
                                else 
                                        $presConceptItems = $presNetwork.concept-names;

                                        /** GET ANY commitments ABSTRACTS **/
                                        $commitItems = filter $presConceptItems where $item.local-name.lower-case.contains('commit') and taxonomy().concept($item).is-monetary;

                                        /** GET ANY contingent ABSTRACTS **/
                                        $contingenItems = filter $presConceptItems where $item.local-name.lower-case.contains('contingen') and taxonomy().concept($item).is-monetary;

                                        $presNumericItems = filter $presConceptItems where taxonomy().concept($item).is-monetary;
                                        $calcspresentSet = set(for $calc in $calcRelationshipItems 
                                                                if $calc.target-name in $presNumericItems and $calc.source-name in $presNumericItems 
                                                                    set($calc.target-name, $calc.source-name)
                                                                else
                                                                    skip);
                                        $calcItems = sum($calcspresentSet);
                                        
                                         /** Revenue can be excluded because under IFRS the Income Statement does not require that COS is reported so a calculation canot be defined in these cases - Arghh **/
                                        $RevenueException = if $presNumericItems.contains(CostOfSales) 
                                                                set()
                                                            else
                                                                set(Revenue)
                                        $presNumericItems - ($calcItems + set(ProfitLossAttributableToOwnersOfParent) + $commitItems.to-set + $contingenItems.to-set + $RevenueException)
    
        $cash_flow_message =    if $presNetwork.role.description.lower-case.contains("cash")
                                "
If the item is an additional disclosure in the cash flow statement then make sure that is included as a child of DisclosureOfCashFlowStatementExplanatory or the extension abstracts  SupplementalCashFlowInformationAbstract, CashFlowNoncashInvestingAndFinancingActivitiesDisclosureAbstract, NoncashInvestingAndFinancingItemsAbstract, NonCashOperatingAndFinancingActivitiesAbstract, NonCashOperatingAndInvestingActivitiesAbstract, NoncashFinancingItemsAbstract or NoncashInvestingItemsAbstract."
                            else
                                "";
                                

    $presConceptNoCalculation.length > 0
        

MESSAGE
"
The following elements are in the presentation linkbase of {$presNetwork.role.description} but are not included in any calculation relationship in the filing.
{$presConceptNoCalculation.join(", 
")}
All elements appearing in the face of the financial statements should be included in a calculation relationship somewhere in the filing. 

This rule will produce an error irrespective of a fact value being entered for the item(s) listed above.
{$cash_flow_message}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


SEVERITY error

effectiveDate $effective_dates[$rule_id]