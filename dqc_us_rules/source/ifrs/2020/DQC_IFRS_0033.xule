/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: IFRS 2020
Original Release: Version 9
**/


rule-name-prefix DQC

assert IFRS.0033.2 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$exceptions_to_dei = set(qname($dei_namespace,'EntityCommonStockSharesOutstanding')
						, qname($dei_namespace,'EntityPublicFloat')
						, qname($dei_namespace,'DocumentPeriodEndDate')
						, qname($dei_namespace,'EntityNumberOfEmployees')
						, qname($dei_namespace,'AuditorLocation')
						, qname($dei_namespace,'AuditorName')
						, qname($dei_namespace,'AuditorFirmId')
						, qname($dei_namespace,'EntityListingDepositoryReceiptRatio'));
$document_period = first(list({covered @concept.local-name = 'DocumentPeriodEndDate'}));
$document_period_end_date = $document_period.period.end;

/** The  first part of the expession checks if the document period end date is within three days if not the rule does not execute as rule DQC.US.0036 will have executed**/
not($document_period > ($document_period_end_date + (time-span("P4D"))) or $document_period < ($document_period_end_date - (time-span("P4D"))))
and 
exists({@concept.namespace-uri = $dei_namespace @period  where $fact.concept.name not in $exceptions_to_dei and  $fact.period.end != $document_period_end_date}#fact1)


message
"The ending date of {$fact1.period.end} used for {$fact1.concept.label.text} must be the same as the Document Period End Date,  {$document_period_end_date}. Revise either the date of the {$fact1.concept.label.text} or the date of the Document Period End Date.

The properties of this {$fact1.concept} fact are:
Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]

