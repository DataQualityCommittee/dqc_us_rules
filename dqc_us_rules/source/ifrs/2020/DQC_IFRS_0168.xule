/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: IFRS 2023

**/


RULE-NAME-PREFIX DQC


constant $MONETARY_TAX_RECONCILIATION_DEFINED = (navigate summation-item  descendants from IncomeTaxExpenseContinuingOperations returns set target-name).contains(TaxExpenseIncomeAtApplicableTaxRate)

constant $PERCENTAGE_TAX_RECONCILIATION_DEFINED = (navigate summation-item  descendants from AverageEffectiveTaxRate returns set target-name).contains(ApplicableTaxRate)

/*
Add rules to ensure taxo reconcilation calculations have been reported for percentage items
*/
ASSERT IFRS.0168.10108 SATISFIED
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

if set('10-K','10-KT','20-F').contains([covered @concept.local-name ='DocumentType']) 

        $is_statutory_taxRate_in_calc = navigate summation-item  descendants from IncomeTaxExpenseContinuingOperations returns set target-name

        not $is_statutory_taxRate_in_calc.contains(TaxExpenseIncomeAtApplicableTaxRate)
        and 
        exists([@concept = TaxExpenseIncomeAtApplicableTaxRate]#stat)
        and
        exists([@concept = IncomeTaxExpenseContinuingOperations]#effective)
        and 
        not $PERCENTAGE_TAX_RECONCILIATION_DEFINED

else
    skip

message
"The concept {$stat.concept.name} is used in the filing with a value of {$stat}.  This element is reported but is not included in a calculation relationship with the element  {$effective.concept.name} with a value of {$effective}.  The filer should add a calculation relationship between these two elements to capture the details of the effective tax rate reconciliation.  A calculation should be provided even if some items in the calculation are represented using dimension members.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$ruleId]

rule-focus $stat
/*
Add rules to ensure taxo reconcilation calculations have been reported for monetary items
*/

ASSERT IFRS.0168.10109 SATISFIED
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

if set('10-K','10-KT','20-F').contains([covered @concept.local-name ='DocumentType']) 


        $is_statutory_taxRate_in_calc = navigate summation-item  descendants from AverageEffectiveTaxRate returns set target-name

        not $is_statutory_taxRate_in_calc.contains(ApplicableTaxRate)
        and 
        exists([@concept = ApplicableTaxRate]#stat)
        and
        exists([@concept = AverageEffectiveTaxRate]#effective)
        and 
        not $MONETARY_TAX_RECONCILIATION_DEFINED

else
    skip

message
"The concept {$stat.concept.name} is used in the filing with a value of {$stat}.  This element is reported but is not included in a calculation relationship with the element  {$effective.concept.name} with a value of {$effective}.  The filer should add a calculation relationship between these two elements to capture the details of the effective tax rate reconciliation.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$ruleId]

rule-focus $stat

ASSERT IFRS.0168.10110 SATISFIED
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

/**Check calc weights match taxonomy **/

$percentage_calc = navigate summation-item  descendants from AverageEffectiveTaxRate returns list (source-name, target-name, weight)

for $relationship in $percentage_calc
    $expectedWeight = $ifrs-gaap.effective-weight($relationship[1],$relationship[2])
    if $expectedWeight == none
        skip
    else
        $relationship[3] != $expectedWeight


message
"The calculation weight between the concept {$relationship[1]} and {$relationship[2]} is {$relationship[3]} in the extension taxonomy.  This weight does not match the weight between these two concepts defined in the IFRS taxonomy which is {$expectedWeight}. The calculation weight for this relationship should be updated to match the IFRS taxonomy.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$ruleId]

rule-focus taxonomy().concept($relationship[1])