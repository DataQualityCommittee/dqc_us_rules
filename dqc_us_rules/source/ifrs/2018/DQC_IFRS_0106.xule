/**
DQC Rules
 (c) Copyright 2017 - 2020, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: IFRS 2018
**/


rule-name-prefix DQC


/** RULES for the axis AssetsAndLiabilitiesClassifiedAsHeldForSaleAxis **/
assert IFRS.0106.9550 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$invalidFact = {@concept.period-type = duration @AssetsAndLiabilitiesClassifiedAsHeldForSaleAxis = *};
exists($invalidFact)

message
"The filer has used the axis AssetsAndLiabilitiesClassifiedAsHeldForSaleAxis with the concept {$invalidFact.concept.name} and a reported value of {$invalidFact}. This concept is a duration concept. This axis should only be used to disaggrate balances on the balance sheet between held for sale and not held for sale. If this value represents the loss or gain on a disposal group then the value should be tagged with the axis ContinuingAndDiscontinuedOperationsAxis with a member that reflects the disposal group or discontinued operation. If the members of DiscontinuedOperationsMember or DisposalGroupsClassifiedAsHeldForSaleMember are not used because either the member reflects a specific discontinued operation or specific disposal group then the specifc member should be included as either a child of DiscontinuedOperationsMember or DisposalGroupsClassifiedAsHeldForSaleMember.

The properties of this {$invalidFact.concept} fact are:
Period :{$invalidFact.period}
Dimensions :  {$invalidFact.dimensions.join(', ','=')}
Unit : {$invalidFact.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity
error

effectiveDate $effective_dates[$rule_id]
