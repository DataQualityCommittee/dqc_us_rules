/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: IFRS 2023

**/

RULE-NAME-PREFIX DQC
/**
This rule checks that the filer has not used a Us-GAAP element in their filing for IFRS." **/

ASSERT IFRS.0176.10118 SATISFIED
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

if set('F-1','F-4', 'F-4/A').contains([covered @concept.local-name ='DocumentType']) 
    skip
else
    $us-gaap-concepts = filter taxonomy().concepts where $item.namespace-uri.contains('http://fasb.org/us-gaap/') and $item.name.local-name not in $USGAAP_ALLOWED_LOCAL-NAME returns $item

    $invalidFact = {@concept in $us-gaap-concepts}

    true
message
"The filer has used the concept {$invalidFact.concept} with a value of {$invalidFact} in the filing.  This is a US-GAAP concept and should be replaced with an IFRS concept.

The properties of this {$invalidFact.concept.name} fact are:
Period :{$invalidFact.period}
Dimensions : {$invalidFact.dimensions.join(', ','=')}

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$ruleId]


ASSERT IFRS.0176.10119 SATISFIED
$ruleId = (rule-name().split('.'))[rule-name().split('.').length];

if set('F-1','F-4', 'F-4/A').contains([covered @concept.local-name ='DocumentType']) 
    skip
else
    $us-gaap-concepts = filter taxonomy().concepts where $item.namespace-uri.contains('http://fasb.org/us-gaap/') and $item.name.local-name not in $USGAAP_ALLOWED_LOCAL-NAME returns $item

    $presentationNetworks = taxonomy().networks(parent-child)

    /** Exclude ECD networks as these pull n US GAAP elements and cannot be changed **/

    $ECD_Presentation = $ECD.networks(parent-child)

    $ECD_Presentation_Role_URI = filter $ECD_Presentation returns $item.role.uri

    $presentationNetworksExcludingECDNetworks = filter $presentationNetworks where $item.role.uri not in $ECD_Presentation_Role_URI 

    $PresentationConcepts = sum(filter $presentationNetworksExcludingECDNetworks returns $item.concepts)

    $us-gaap_concepts = $PresentationConcepts.intersect($us-gaap-concepts)

    for $concept in $us-gaap_concepts 
        true

message
"The filer has used the US-GAAP concept {$concept.name} in the presentation tree of the filing please choose an IFRS concept as a replacement.

Rule Element Id:{$ruleId}
Rule version: {$ruleVersion}"

SEVERITY error

effectiveDate $effective_dates[$ruleId]