/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: IFRS 2021
**/


rule-name-prefix DQC

assert IFRS.0102.9539 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];
$Assets = [@Assets];
$LiabilitiesAndStockholdersEquity = [@EquityAndLiabilities];
$decimal_tolerance_factor = 2;

tolerance_for_decimals($Assets, $LiabilitiesAndStockholdersEquity, $decimal_tolerance_factor)

message

"{$Assets.concept.label.text} with a value of {$Assets} is not equal to the total of {$LiabilitiesAndStockholdersEquity.concept.label.text} with a value of {$LiabilitiesAndStockholdersEquity}. These values should be equal.
The properties of this {$Assets.concept} fact are:
Period :{$Assets.period}
Dimensions :  {$Assets.dimensions.join(', ','=')}
Unit : {$Assets.unit}
Decimals of Assets : {$Assets.decimals}
Decimals of Liabilities and Equity : {$LiabilitiesAndStockholdersEquity.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


severity error
effectiveDate $effective_dates[$rule_id]

/**
Assets = AssetsCurrent + AssetsNoncurrent
**/

assert IFRS.0102.9540 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];


$Assets = {nonils @Assets @MaturityAxis=none @PastDueStatusAxis=none};
$CalcAssets = {nonils @CurrentAssets @MaturityAxis=none @PastDueStatusAxis=none}#ca <+> {nonils @NoncurrentAssets @MaturityAxis=none @PastDueStatusAxis=none}#nca;
$Decimals = list({nonils @CurrentAssets @MaturityAxis=none @PastDueStatusAxis=none},{nonils @Assets @MaturityAxis=none @PastDueStatusAxis=none},{nonils @NoncurrentAssets @MaturityAxis=none @PastDueStatusAxis=none});

$minDecimals = min(list(for $x in $Decimals
				if $x == none 
					skip
				else
					$x.decimals));

$decimal_tolerance_factor = 2;

if $CalcAssets == none
	skip
else
	tolerance_for_decimals_comp($Assets, $CalcAssets, $minDecimals, $decimal_tolerance_factor)


message

"{$Assets.concept.label.text} with a value of {$Assets} is not equal to the combined total of Current Assets and Noncurrent assets ( {$ca} + {$nca} ) with a value of {$CalcAssets}. These values should be equal.  If assets held for sale is included in the Statement of Financial Position presentation, then consider using CurrentAssetsOtherThanAssetsOrDisposalGroupsClassifiedAsHeldForSaleOrAsHeldForDistributionToOwners instead of current assets.
The properties of this {$Assets.concept} fact are:
Period :{$Assets.period}
Dimensions :  {$Assets.dimensions.join(', ','=')}
Unit : {$Assets.unit}
Decimals of Assets : {$Assets.decimals}
Decimals of Current Assets : {$ca.decimals}
Decimals of Non Current Assets : {$nca.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


severity error

effectiveDate $effective_dates[$rule_id]

/**
Liabilities = LiabilitiesCurrent + LiabilitiesNoncurrent
**/

assert IFRS.0102.9541 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$Liabilities = {nonils @Liabilities @MaturityAxis=none @PastDueStatusAxis=none};
$CalcLiabilities = {nonils @CurrentLiabilities @MaturityAxis=none @PastDueStatusAxis=none}#cl <+> {nonils @NoncurrentLiabilities @MaturityAxis=none @PastDueStatusAxis=none}#ncl;
$Decimals = list({nonils @Liabilities @MaturityAxis=none @PastDueStatusAxis=none},{nonils @CurrentLiabilities @MaturityAxis=none @PastDueStatusAxis=none},{nonils @NoncurrentLiabilities @MaturityAxis=none @PastDueStatusAxis=none});

$minDecimals = min(list(for $x in $Decimals
				if $x == none 
					skip
				else
					$x.decimals));

$decimal_tolerance_factor = 2;

if $CalcLiabilities == none
	skip
else	
	tolerance_for_decimals_comp($Liabilities, $CalcLiabilities, $minDecimals, $decimal_tolerance_factor)

message

"{$Liabilities.concept.label.text} with a value of {$Liabilities} is not equal to the combined total of Current Liabilities and Noncurrent Liabilites ( {$cl} + {$ncl} ) with a value of {$CalcLiabilities}. If liabilities held for sale is included in the Statement of Financial Position presentation, then consider using CurrentLiabilitiesOtherThanLiabilitiesIncludedInDisposalGroupsClassifiedAsHeldForSale instead of current liabilities. 

These values should be equal.
The properties of this {$Liabilities.concept} fact are:
Period :{$Liabilities.period}
Dimensions :  {$Liabilities.dimensions.join(', ','=')}
Unit : {$Liabilities.unit}
Decimals of Liabilities : {$Liabilities.decimals}
Decimals of Current Liabilities : {$cl.decimals}
Decimals of Non Current Liabilities : {$ncl.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


severity error

effectiveDate $effective_dates[$rule_id]

/**
StockholdersEquityIncludingPortionAttributableToNoncontrollingInterest = StockholdersEquity + MinorityInterest
**/

assert IFRS.0102.9542 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$Elements_That_Are_Children_of_Equity = navigate summation-item children from Equity returns by network set (target);

/** Get the correct elements from the right calculation network**/

$Extension_Credit_Elements_That_Are_Children_of_Equity = sum(set(for ($network in $Elements_That_Are_Children_of_Equity.keys)
					$NoncontrollingInterests = taxonomy().concept(NoncontrollingInterests);
					$EquityAttributableToOwnersOfParent = taxonomy().concept(EquityAttributableToOwnersOfParent);

					if ($Elements_That_Are_Children_of_Equity[$network].contains($NoncontrollingInterests) and  $Elements_That_Are_Children_of_Equity[$network].contains($EquityAttributableToOwnersOfParent))
					/** Only get elements that are temp equity and are an extension **/
						filter $Elements_That_Are_Children_of_Equity[$network] where $item.balance == credit and $item.name.namespace-uri == $extension_ns returns $item.name
					else
						skip
					))
$SEINCI = [@Equity];

$oth = if $Extension_Credit_Elements_That_Are_Children_of_Equity.length == 0 or $Extension_Credit_Elements_That_Are_Children_of_Equity == none
			0
		else
			sum(list([@concept in $Extension_Credit_Elements_That_Are_Children_of_Equity]));
			
$CalcSE = [@EquityAttributableToOwnersOfParent]#se <+> [@NoncontrollingInterests]#mi + $oth
$Decimals = list([@Equity],[@EquityAttributableToOwnersOfParent],[@NoncontrollingInterests]);

$minDecimals = min(list(for $x in $Decimals
				if $x == none 
					skip
				else
					$x.decimals));

$decimal_tolerance_factor = 2;

if $CalcSE == none
	skip
else		
	tolerance_for_decimals_comp($SEINCI, $CalcSE, $minDecimals, $decimal_tolerance_factor)

message

"{$SEINCI.concept.label.text} with a value of {$SEINCI} is not equal to the combined total of Stockholders Equity Excluding NI and Noncontrolling Interest ( {$se} + {$mi} {if $oth != 0 " + " + $oth.string else ""}) with a value of {$CalcSE}. 

These values should be equal.
The properties of this {$SEINCI.concept} fact are:
Period :{$SEINCI.period}
Dimensions :  {$SEINCI.dimensions.join(', ','=')}
Unit : {$SEINCI.unit}
Decimals of Stockholders Equity Including Noncontrolling Interest : {$SEINCI.decimals}
Decimals of Stockholders Equity Excluding NI : {$se.decimals}
Decimals of Noncontrolling Interest : {$mi.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"


severity error

effectiveDate $effective_dates[$rule_id]


/**
ComprehensiveIncomeNetOfTaxIncludingPortionAttributableToNoncontrollingInterest = ProfitLoss + OtherComprehensiveIncomeLossNetOfTax

**/

assert IFRS.0102.9543 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

/** Have to use the where clause as there is no guarantee that they use the srt taxonomy **/
$base = {nonils @ComprehensiveIncome where not $fact.dimensions().keys.name.local-name.contains('ConsolidationItemsAxis')};
$agg = {nonils @ProfitLoss where not $fact.dimensions().keys.name.local-name.contains('ConsolidationItemsAxis')}#a1 <+> {nonils @OtherComprehensiveIncome where not $fact.dimensions().keys.name.local-name.contains('ConsolidationItemsAxis')}#a2;
$Decimals = list({nonils @ComprehensiveIncome where not $fact.dimensions().keys.name.local-name.contains('ConsolidationItemsAxis')},{nonils @ProfitLoss where not $fact.dimensions().keys.name.local-name.contains('ConsolidationItemsAxis')},{nonils @OtherComprehensiveIncome where not $fact.dimensions().keys.name.local-name.contains('ConsolidationItemsAxis')});

$minDecimals = min(list(for $x in $Decimals
				if $x == none 
					skip
				else
					$x.decimals));

$decimal_tolerance_factor = 2;

if ($agg == none)
	skip
else	
	tolerance_for_decimals_comp($base, $agg, $minDecimals, $decimal_tolerance_factor)

message

"{$base.concept.label.text} with a value of {$base} is not equal to the combined total of {$a1.concept.label.text} and {$a2.concept.label.text} ( {$a1} + {$a2} ) with a value of {$agg}. 

These values should be equal.
The properties of this {$base.concept} fact are:
Period :{$base.period}
Dimensions :  {$base.dimensions.join(', ','=')}
Unit : {$base.unit}
Decimals of {$base.concept.label.text} : {$base.decimals}
Decimals of {$a1.concept.label.text} : {$a1.decimals}
Decimals of {$a2.concept.label.text} : {$a2.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]



/** CashFlow

CashAndCashEquivalentsPeriodIncreaseDecreaseExcludingExchangeRateEffect = NetCashProvidedByUsedInOperatingActivities + NetCashProvidedByUsedInInvestingActivities + NetCashProvidedByUsedInFinancingActivities
**/

assert IFRS.0102.9544 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$base = {nonils @IncreaseDecreaseInCashAndCashEquivalentsBeforeEffectOfExchangeRateChanges};

$agg = {nonils @CashFlowsFromUsedInOperatingActivities}#a1 <+> {nonils @CashFlowsFromUsedInInvestingActivities}#a2 <+> {nonils @CashFlowsFromUsedInFinancingActivities}#a3;
$decimal_tolerance_factor = 2;

if $base == none
	skip
else
	tolerance_for_decimals($base, $agg, $decimal_tolerance_factor)

message

"{$base.concept.label.text} with a value of {$base} is not equal to the combined total of {$a1.concept.label.text}, {$a2.concept.label.text} and {$a3.concept.label.text} ( {$a1} + {$a2} + {$a3} ) with a value of {$agg}. 

These values should be equal.
The properties of this {$base.concept} fact are:
Period :{$base.period}
Dimensions :  {$base.dimensions.join(', ','=')}
Unit : {$base.unit}
Decimals of {$base.concept.label.text} : {$base.decimals}
Decimals of {$a1.concept.label.text} : {$a1.decimals}
Decimals of {$a2.concept.label.text} : {$a2.decimals}
Decimals of {$a3.concept.label.text} : {$a3.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]
/**
NetCashProvidedByUsedInFinancingActivities = NetCashProvidedByUsedInFinancingActivitiesContinuingOperations + CashProvidedByUsedInFinancingActivitiesDiscontinuedOperations

**/

assert IFRS.0102.9545 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$base = {nonils @CashFlowsFromUsedInFinancingActivities};
$agg = {nonils @CashFlowsFromUsedInFinancingActivitiesContinuingOperations}#a1 <+> {nonils @CashFlowsFromUsedInFinancingActivitiesDiscontinuedOperations}#a2;
$decimal_tolerance_factor = 2;

tolerance_for_decimals($base, $agg, $decimal_tolerance_factor)

message

"{$base.concept.label.text} with a value of {$base} is not equal to the combined total of {$a1.concept.label.text} and {$a2.concept.label.text} ( {$a1} + {$a2} ) with a value of {$agg}. 

These values should be equal.
The properties of this {$base.concept} fact are:
Period :{$base.period}
Dimensions :  {$base.dimensions.join(', ','=')}
Unit : {$base.unit}
Decimals of {$base.concept.label.text} : {$base.decimals}
Decimals of {$a1.concept.label.text} : {$a1.decimals}
Decimals of {$a2.concept.label.text} : {$a2.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]

/**
NetCashProvidedByUsedInInvestingActivities = NetCashProvidedByUsedInInvestingActivitiesContinuingOperations + CashProvidedByUsedInInvestingActivitiesDiscontinuedOperations
**/

assert IFRS.0102.9546 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$base = {nonils @CashFlowsFromUsedInInvestingActivities};
$agg = {nonils @CashFlowsFromUsedInInvestingActivitiesContinuingOperations}#a1 <+> {nonils @CashFlowsFromUsedInInvestingActivitiesDiscontinuedOperations}#a2;
$decimal_tolerance_factor = 2;

tolerance_for_decimals($base, $agg, $decimal_tolerance_factor)

message

"{$base.concept.label.text} with a value of {$base} is not equal to the combined total of {$a1.concept.label.text} and {$a2.concept.label.text} ( {$a1} + {$a2} ) with a value of {$agg}. 

These values should be equal.
The properties of this {$base.concept} fact are:
Period :{$base.period}
Dimensions :  {$base.dimensions.join(', ','=')}
Unit : {$base.unit}
Decimals of {$base.concept.label.text} : {$base.decimals}
Decimals of {$a1.concept.label.text} : {$a1.decimals}
Decimals of {$a2.concept.label.text} : {$a2.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $base

effectiveDate $effective_dates[$rule_id]

/**
CashFlowsFromUsedInOperatingActivities = CashFlowsFromUsedInOperatingActivitiesContinuingOperations + CashFlowsFromUsedInOperatingActivitiesDiscontinuedOperations

**/

assert IFRS.0102.9547 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$base = {nonils @CashFlowsFromUsedInOperatingActivities};
$agg = {nonils @CashFlowsFromUsedInOperatingActivitiesContinuingOperations}#a1 <+> {nonils @CashFlowsFromUsedInOperatingActivitiesDiscontinuedOperations}#a2;
$decimal_tolerance_factor = 2;

tolerance_for_decimals($base, $agg, $decimal_tolerance_factor)

message

"{$base.concept.label.text} with a value of {$base} is not equal to the combined total of {$a1.concept.label.text} and {$a2.concept.label.text} ( {$a1} + {$a2} ) with a value of {$agg}. 

These values should be equal.
The properties of this {$base.concept} fact are:
Period :{$base.period}
Dimensions :  {$base.dimensions.join(', ','=')}
Unit : {$base.unit}
Decimals of {$base.concept.label.text} : {$base.decimals}
Decimals of {$a1.concept.label.text} : {$a1.decimals}
Decimals of {$a2.concept.label.text} : {$a2.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]

/**
IncreaseDecreaseInCashAndCashEquivalentsDiscontinuedOperations = CashFlowsFromUsedInOperatingActivitiesDiscontinuedOperations + CashFlowsFromUsedInInvestingActivitiesDiscontinuedOperations + CashFlowsFromUsedInFinancingActivitiesDiscontinuedOperations
**/

assert IFRS.0102.9548 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$base = {nonils @IncreaseDecreaseInCashAndCashEquivalentsDiscontinuedOperations};
$agg = {nonils @CashFlowsFromUsedInOperatingActivitiesDiscontinuedOperations}#a1 <+> {nonils @CashFlowsFromUsedInInvestingActivitiesDiscontinuedOperations}#a2 <+> {nonils @CashFlowsFromUsedInFinancingActivitiesDiscontinuedOperations}#a3;
$decimal_tolerance_factor = 2;

tolerance_for_decimals($base, $agg, $decimal_tolerance_factor)

message

"{$base.concept.label.text} with a value of {$base} is not equal to the combined total of {$a1.concept.label.text}, {$a2.concept.label.text} and {$a3.concept.label.text} ( {$a1} + {$a2} + {$a3} ) with a value of {$agg}. 

These values should be equal.
The properties of this {$base.concept} fact are:
Period :{$base.period}
Dimensions :  {$base.dimensions.join(', ','=')}
Unit : {$base.unit}
Decimals of {$base.concept.label.text} : {$base.decimals}
Decimals of {$a1.concept.label.text} : {$a1.decimals}
Decimals of {$a2.concept.label.text} : {$a2.decimals}
Decimals of {$a3.concept.label.text} : {$a3.decimals}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]



