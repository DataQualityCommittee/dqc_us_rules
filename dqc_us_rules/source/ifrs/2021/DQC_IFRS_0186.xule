/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: IFRS 2021

**/

RULE-NAME-PREFIX DQC

constant $DIVIDEND_CONCEPTS = list(DividendsPaid, DividendsPaidOrdinaryShares, DividendsPaidOtherShares, DividendsRecognisedAsDistributionsToOwnersOfParentRelatingToPriorYears, DividendsRecognisedAsDistributionsToOwnersOfParentRelatingToCurrentYear, DividendsRecognisedAsDistributionsToOwnersOfParent, DividendsRecognisedAsDistributionsToNoncontrollingInterests)

constant $INVALID_DIVIDEND_CONCEPTS_WITH_MEMBER = dict(
                                               /** list(TreasurySharesMember,$DIVIDEND_CONCEPTS),  Removed as example of neg amount against treasury shares **/
                                                list(AccumulatedOtherComprehensiveIncomeMember,$DIVIDEND_CONCEPTS),
                                                list(ReserveOfSharebasedPaymentsMember,$DIVIDEND_CONCEPTS)
                                            )

/**--NONNEG Dividend Items Impact on Retained earnings - This should never increase retained earnings**/


ASSERT IFRS.0186.10170 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$retainedEarningsTypes = list(RetainedEarningsMember)

$negativeDividends = {@concept in $DIVIDEND_CONCEPTS @ComponentsOfEquityAxis in $retainedEarningsTypes where $fact < 0}

exists($negativeDividends)

message
"The filing includes a value for dividends using the concept {$negativeDividends.concept.name} and the retained earnings member with a value of {$negativeDividends}. Dividends must be deducted from retained earnings.  Please check that the sign of the dividends value is positive.

The properties of this  fact are:
Period :{$negativeDividends.period}
Dimensions : {$negativeDividends.dimensions.join(', ','=')}
Unit : {$negativeDividends.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $negativeDividends

effectiveDate $effective_dates[$rule_id]


ASSERT IFRS.0186.10171 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

for $memberToCheck in $INVALID_DIVIDEND_CONCEPTS_WITH_MEMBER.keys
           $divFact = {nonils @concept in $INVALID_DIVIDEND_CONCEPTS_WITH_MEMBER[$memberToCheck] @ComponentsOfEquityAxis = $memberToCheck where $fact != 0}
           exists($divFact)

message
"The filing includes a value for dividends using the concept {$divFact.concept.name} with a value of {$divFact}. This value is reported with the dimension member {$memberToCheck}.  This member should not be used with the concept {$divFact.concept.name}.  Please check the filing and select a more appropriate concept.

The properties of this  fact are:
Period :{$divFact.period}
Dimensions : {$divFact.dimensions.join(', ','=')}
Unit : {$divFact.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $divFact

effectiveDate $effective_dates[$rule_id]


ASSERT IFRS.0186.10172 satisfied

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

$StockWithInconsistentClass = {nonils @concept in list(DividendsPaidOrdinarySharesPerShare) @ClassesOfShareCapitalAxis in list(OrdinarySharesMember, PreferenceSharesMember)}

exists($StockWithInconsistentClass)

message
"The filing includes a  value of {$StockWithInconsistentClass} for  dividends declared using the concept {$StockWithInconsistentClass.concept.name} and the dimension member of {$StockWithInconsistentClass.dimension(ClassesOfShareCapitalAxis).name} on the axis ClassesOfShareCapitalAxis. This member should not be used with this concept as the type of stock declared is already communicated with the concept name, and the dimensions provides no additional disaggregation. 

The properties of this  fact are:
Period :{$StockWithInconsistentClass.period}
Dimensions : {$StockWithInconsistentClass.dimensions.join(', ','=')}
Unit : {$StockWithInconsistentClass.unit}

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
rule-focus $StockWithInconsistentClass

effectiveDate $effective_dates[$rule_id]

