/**
DQC Rules
 (c) Copyright 2017 - 2025, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: IFRS 2024

**/

RULE-NAME-PREFIX DQC


                                      
ASSERT IFRS.0196.10640 satisfied

/** Therule check where the commonstock member is used on the StatementEquityComponentsAxis with an instant element that is not an expected element.}**/

$rule_id = (rule-name().split('.'))[rule-name().split('.').length];

if $STATEMENT_SHE_CUBES.length == 0
        skip
else
        $exclusionList = list(Equity, EquityAttributableToOwnersOfParent)

        $fact1 = {@concept not in  $exclusionList  @ComponentsOfEquityAxis = * where $fact.concept.period-type == instant and $fact.concept.is-monetary and $fact != 0}
        $SHE_Name = (filter $STATEMENT_SHE_CUBES  returns $item.drs-role.description).to-list;
        $message = if is_extension($fact1.concept, list($ifrs-gaap))
        "If this extension element was used to represent the changes in equity, the period type should be changed to a duration. If it represents an opening or closing balance then the element should be removed and the standard elements of either {$exclusionList.join(' or ')} should be used instead."
        else
                ""
        exists($fact1) and ($fact1.sid in sum(filter $STATEMENT_SHE_CUBES.facts returns $item.sid))

message
"{$fact1.concept} has a value of {$fact1} and has been used with the {$fact1.dimension(ComponentsOfEquityAxis).name}, in the disclosure {$SHE_Name.join(", ")}. This fact value has been used with the ComponentsOfEquityAxis and {$fact1.dimension(ComponentsOfEquityAxis).name}. This element should not be used to represent an opening or closing balance in the changes of equity.  Please remove the member from this fact or consider using a different element to represent the opening or closing balance of {$fact1.dimension(ComponentsOfEquityAxis).name}.
{$message}

The properties of this {$fact1.concept} fact are:
Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}
Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error

effectiveDate $effective_dates[$rule_id]

