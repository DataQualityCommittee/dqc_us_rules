/**

DQC Rules
 (c) Copyright 2017 - 2018, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: US GAAP 2015

**/

RULE-NAME-PREFIX DQC

ASSERT US.0065.7502 SATISFIED

$rule-suffix = 7502

$networkQname = navigate parent-child DESCENDANTS FROM SupplementalCashFlowInformationAbstract returns set (target-name)
	 
$supplementalElements =  FILTER $networkQname returns $item.local-name
								
$supplementalElements.contains('InterestPaid')	and
not $supplementalElements.contains('InterestPaidNet') and
not $supplementalElements.contains('InterestPaidCapitalized') and
exists([@InterestPaid where $fact != 0]#fact1)

message
"The Supplemental cash flow section has reported a value of {$fact1} for the element {$fact1.name} but the element InterestPaidNet is not included in the supplemental cash flow section. Interest Paid Net of the capitalized component must be reported in this section. 
The element {$fact1.name} includes capitalized interest. If this element has no capitalized interest component then use the element InterestPaidNet instead.

The properties of this {$fact1.concept} fact are:
Period :{$fact1.period}
Dimensions : {$fact1.dimensions.join(', ','=')}
Unit : {$fact1.unit}

Rule Element Id:7502
Rule version: 5.0.0"

element_id 7502

SEVERITY error

