/**

DQC Rules
Copyright (c) 2017 - 2018 XBRL US Inc.
Taxonomy: US GAAP 2017

**/

rule-name-prefix DQC

assert US.0044.6834 satisfied

$Accrual_items_in_cashflows = 
NAVIGATE summation-item DESCENDANTS 
FROM list(NetCashProvidedByUsedInFinancingActivities, NetCashProvidedByUsedInFinancingActivitiesContinuingOperations)  where $relationship.target.name in $Accrual_Items 

$fact = {@concept in $Accrual_items_in_cashflows}

$fact != 0

message
"The concept {$fact.name} with a value of {$fact} is an accrual-based item in the US GAAP taxonomy that is included in the sum of cash provided by (used in) financing activities in the cash flows of the extension taxonomy.

Accrual-based concepts do not belong and should not be included in the financing section of the cash flows statement of the extension taxonomy.  We have seen this problem occur for a couple of reasons. 
1. The company has used an item that is an accrual item that would normally be in the income statement or shareholders equity and has used it as a replacement for a financing cashflow element.  These are different concepts and should not be interchanged.  This can occur for operating cashflow items where a reconciliation to net income is presented.
2. In other cases we have seen the filer use the sum of financing cash flows to represent operating cash flows.  This is an element selection error that sometime occurs and is easily addressed by replacing either the element NetCashProvidedByUsedInFinancingActivitiesContinuingOperations or NetCashProvidedByUsedInFinancingActivities with the operating cash flow item.

If case one above applies replace {$fact.name} with an appropriate US GAAP taxonomy cash flows item, if available or, if not, consider extending a concept. Alternatively if case two applies then replace the investing activities parent with the operating activities parent.

The properties of this {$fact.concept} fact are:
Period :{$fact.period}
Dimensions : {$fact.dimensions.join(', ','=')}
Unit : {$fact.unit}
Entity : {$fact.entity}

Rule Element Id:6834
Rule Version : 5.0"

element_id 6834

severity
error


assert US.0044.7503 satisfied

$Accrual_items_in_cashflows = 
NAVIGATE summation-item DESCENDANTS 
FROM list(NetCashProvidedByUsedInInvestingActivities, NetCashProvidedByUsedInInvestingActivitiesContinuingOperations)  where $relationship.target.name in $Accrual_Items

{@concept in $Accrual_items_in_cashflows}#fact != 0

message
"The concept {$fact.name} with a value of {$fact} is an accrual-based item in the US GAAP taxonomy that is included in the sum of cash provided by (used in) investing activities in the cash flows of the extension taxonomy.

Accrual-based concepts do not belong and should not be included in the investing section of the cash flows statement of the extension taxonomy.  We have seen this problem occur for a couple of reasons. 
1. The company has used an item that is an accrual item that would normally be in the income statement or shareholders equity and has used it as a replacement for a cashflow element.  These are different concepts and should not be interchanged.  This can occur for operating cashflow items where a reconciliation to net income is presented.
2. In other cases we have seen the filer use the sum of investing cash flows to represent operating cash flows.  This is an element selection error that sometime occurs and is easily addressed by replacing either the element NetCashProvidedByUsedInInvestingActivitiesContinuingOperations or NetCashProvidedByUsedInInvestingActivities with the operating cash flow item.

If case one above applies replace {$fact.name} with an appropriate US GAAP taxonomy cash flows item, if available or, if not, consider extending a concept. Alternatively if case two applies then replace the investing activities parent with the operating activities parent.

The properties of this {$fact.concept} fact are:
Period :{$fact.period}
Dimensions : {$fact.dimensions.join(', ','=')}
Unit : {$fact.unit}
Entity : {$fact.entity}

Rule Element Id:7503
Rule Version : 5.0"

severity
error
