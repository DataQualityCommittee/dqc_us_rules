/**
DQR Rules
 (c) Copyright 2017 - 2022, XBRL US Inc. All rights reserved.
 See https://xbrl.us/dqc-license for license information.
 See https://xbrl.us/dqc-patent for patent infringement notice.
Taxonomy: IFRS 2019
**/

rule-name-prefix DQR

assert IFRS.0138.9839 satisfied
$rule_id = (rule-name().split('.'))[rule-name().split('.').length];
$presNetworks = FILTER taxonomy().networks(parent-child) where ($item.roots.name.contains(StatementOfFinancialPositionAbstract));

if $presNetworks.length > 0
    skip
else
    true

message
"The filing does not report the element StatementOfFinancialPositionAbstract in any presentation linkroles.  According to ESMA filing rules this abstract item must be reported as the root element of a Statement of Financial Position appearing in the filing.

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$rule_id]

assert IFRS.0138.9840 satisfied
$rule_id = (rule-name().split('.'))[rule-name().split('.').length];
$presNetworksIS = FILTER taxonomy().networks(parent-child) where ($item.roots.name.contains(IncomeStatementAbstract));
$presNetworksCI = FILTER taxonomy().networks(parent-child) where ($item.roots.name.contains(StatementOfComprehensiveIncomeAbstract));

$presNetworks = $presNetworksIS  + $presNetworksCI

if $presNetworks.length > 0
    skip
else
    true

message
"The filing does not report either the element IncomeStatementAbstract or StatementOfComprehensiveIncomeAbstract in any presentation linkroles.  According to ESMA filing rules this abstract item must be reported as the root element of a Income Statement or StatementOfComprehensiveIncomeAbstract appearing in the filing.

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$rule_id]

assert IFRS.0138.9841 satisfied
$rule_id = (rule-name().split('.'))[rule-name().split('.').length];
$presNetworks = FILTER taxonomy().networks(parent-child) where ($item.roots.name.contains(StatementOfCashFlowsAbstract));

if $presNetworks.length > 0
    skip
else
    true

message
"The filing does not report the element StatementOfCashFlowsAbstract in any presentation linkroles.  According to ESMA filing rules this abstract item must be reported as the root element of a Statement Of Cash Flows  appearing in the filing.

Rule Element Id:{$rule_id}
Rule version: {$ruleVersion}"

severity error
effectiveDate $effective_dates[$rule_id]
